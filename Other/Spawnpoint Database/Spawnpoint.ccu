//(Spawnpoint)
///	C:\Users\Austin\Documents\Austin\powder game code\eGO\CCU Events\Other\Spawnpoint Database\Spawnpoint.ccu

//<Defines
///	DEF $SA$ x=#,y=#,z=#,dx=#,dy=#,dz=#
///	DEF $SPid$ #
///	DEF $SPpvp$ 0
///	DEF $SPsat$ 0

DEF $CMDName$ SP
DEF $CMDSizeX$ 16
DEF $CMDSizeY$ 16
DEF $CMDSizeZ$ 16

/*
DEF $TextStart$ 
{"text":"","extra":[
{"text":"[","color":"gray"},
{"text":"SP","color":"yellow","bold":"true",
	"hoverEvent":{"action":"show_text","value":{"text":"SpawnPoint System","color":"yellow"}},
	"clickEvent":{"action":"run_command","value":"/scoreboard players operation @p SPtp = cmd SPna"}
},{"text":"]","color":"gray"},{"text":": "},
*/

DEF $BlockOption$ stonebrick 0
DEF $StyleDirection$ +X
DEF COORDS $CoordsOption$ 351 7 111

IMPORT {LIBRARY GETDIR General}
IMPORT {GETCOORDS C:\Users\Austin\Documents\Austin\powder game code\eGO\CCU Events\Other\Spawnpoint Database\Spawnpoint_Book_dat.txt}

OPTIONS:
	blockOption $BlockOption$
	coordsOption $CoordsOption$
	styleOption $StyleDirection$ 16
	parseOption true
	commandOption true
	combinerOption true
///	filePathFuncOption 
	keepCoordsOption false

DEF $SP$ x=350,y=14,z=13,dx=90,dy=30,dz=90
DEF $SpawnStand$ 397 17 61
DEF $SPPortal$ x=350,y=14,z=13,dx=90,dy=5,dz=90
DEF $SPTree$ x=397,y=17,z=61,r=5
DEF $SPNotTree$ x=397,y=17,z=61,rm=5
DEF $NotMM$ x=77,y=5,z=42,rm=40
DEF $MM$ x=77,y=5,z=42,r=40
DEF $Dim$ Dimension:0

/*
DEF $TSTokenTextStart$ 
	{"text":"","extra":[{"text":"[","color":"gray"},{"text":"TS","color":"dark_aqua","bold":"true",
	"hoverEvent":{"action":"show_text","value":{"text":"Teamspeak Token","color":"dark_aqua"}}},
	{"text":"]","color":"gray"},{"text":": "},
*/
DEF $TSSelect$ x=544,y=19,z=37,dx=10,dy=5,dz=10
DEF COORDS $TSWorldedit$ 549 17 49
DEF COORDS $TSTokenGlass$ 548 14 47
DEF COORDS $TSTokenClear$ 550 14 47
DEF COORDS $TSTokenFloor$ 554 19 37 544 19 47

///	Lime 		5		Minigames
///	Magenta		2		PVP
///	Orange		1		Mastermind
///	Cyan		9		Races
//>

//<Spawnpoint Defines
ARRAY {Arr_Spawnpoint}:
	{
	///	display coords
		49 36 -45
		
	///	teleport coords
		49 36 -45 -90 0
		
	///	full display name
		Ice Race
		
	///	display name
		Ice Race
		
	///	color
		aqua
		
	///	shortcut
		ir
	} {
		75 7 -80
		75 7 -80 90 0
		Slow Race
		Slow Race
		gray
		sr
	} {
		73 7 -102
		73 7 -102 90 0
		Nether Race
		Nether Race
		red
		nr
	} {
		23 7 -133
		23 7 -133 -90 0
		Evil Race
		Evil Race
		gray
		evilr
	} {
		218 18 -37
		218 18 -37 -90 0
		Diamond Race
		Diamond Race
		aqua
		dr
	} {
		276 17 -96
		276 17 -96 90 0
		Emerald Race
		Emerald Race
		green
		er
	} {
		-349 94 191
		-349 94 191 -90 0
		NvS
		NvS
		red
		nvs
	} {
		229 8 -157
		229 8 -157 -90 0
		Quartz Race
		Quartz Race
		white
		qr
	} {
		77 5 41
		77 5 41 0 -15
		Mastermind
		Mastermind
		gold
		mm
	} {
		-105 25 200
		-105 25 200 -135 0
		The Pit 3
		The Pit 3
		green
		tpl3
	} {
		242 26 -222
		242 26 -222 -90 0
		1.8 Race
		1.8 Race
		gold
		18r
	} {
		424 12 -122
		424 12 -122 -90 0
		Epic Race
		Epic Race
		gold
		epicr
	} {
		467 8 -33
		467 8 -33 -90 0
		Salt Race
		Salt Race
		gray
		saltr
	} {
		-250 25 18
		-250 25 18 -90 0
		Sand Tomb
		Sand Tomb
		yellow
		st
	} {
		397 17 61
		397 17 61 90 0
		Spawn
		Spawn
		dark_red
		spawn
	} {
		-77 56 -85
		-77 56 -85 -45 0
		Virus 2
		Virus 2
		yellow
		virus2
	} {
		-85 45 -241
		-85 45 -241 -45 0
		Virus 1
		Virus 1
		yellow
		virus1
	} {
		549 20 42
		549 20 42
		Teamspeak Token
		Teamspeak Token
		dark_aqua
		tstoken
	} {
		517 11 -827
		517 11 -827 90 0
		Frostburn Run
		Frostburn Run
		aqua
		fbr
	} {
		460 3 -331
		460 3 -331 -90 0
		EverChanging Race
		EverChanging Race
		yellow
		ecr
	} {
		-16 5 87
		-16 5 87 90 0
		Anvil Drop
		Anvil Drop
		green
		ad
	} {
		31 20 -240
		31 20 -240 -45 0
		Death Run
		Death Run
		red
		deathr
	} {
		329 24 171
		329 24.065 171 90 0
		The Minigame Hub
		The Minigame Hub
		green
		mghub
	} {
		385 15 159
		385 15 159 180 0
		The PVP hub
		The PVP hub
		light_purple
		pvphub
	} {
		329 24 154
		329 24.065 154 90 0
		The Mastermind Hub
		The Mastermind Hub
		gold
		mmhub
	} {
		329 24 137
		329 24.076 137 90 0
		The Race Hub
		The Race Hub
		dark_aqua
		racehub
	} {
		348 4 114
		348 4 114 -90 0
		Spawnpoint System
		Spawnpoint System
		yellow
		cmd
	} {
		-268 49 -39
		-268 49 -39 -90 0
		PVP: Old CTF
		PVP: Old CTF
		red
		oldctf
	} {
		-187 24 96
		-187 24 96 -90 0
		Death Pit
		Death Pit
		red
		dp
	} {
		214 27 334
		214 27 334 -90 0
		Rabbit Ball
		Rabbit; ;Ball
		red;white;blue
		rb
	} {
		23 15 -189
		23 15 -189.0 -90 0
		Dirt Race
		Dirt Race
		gray
		dirtr
	} {
		445 68 -669
		445 68 -669.0 -90 0
		Mycelium Race
		Mycelium Race
		dark_purple
		mr
	} {
		558 107 159
		558 107 159.0 90 0
		Capture the flag
		Capture; the ;flag
		red;white;blue
		ctf
	} {
		161 4 180
		161 4 180 90 0
		Pictionary
		P;i;c;t;i;o;n;a;r;y
		light_purple;red;gold;yellow;green;dark_green;blue;dark_aqua;aqua;white
		pc
	} {
		-103 19 482
		-103 19 482 -180 0
		Royal Rumble
		Royal; ;Rumble
		blue;white;dark_green
		rr
	
	}
//>


//<Main Start
FUNC {Func_GeneralScoreboardStart}:
	USE {ScOA}:
		CCU_NumberObjAdd
	
	ARRAY {GLOBAL ACTIVATE CCU.objectiveAdd(Obj_GeneralScoreboardStart) Obj_GeneralScoreboardStart}:
		SPid dummy SpawnPoint ID
		SPtp dummy SpawnPoint TP
		SPna dummy SpawnPoint Name
		SPti dummy SpawnPoint Timer
		SPmm dummy SpawnPoint Mastermind
		SPbk dummy SpawnPoint Book
		SPsp dummy SpawnPoint Spawn
		SPlg stat.leaveGame Spawnpoint Leave Game
		
		SPgam dummy Game Toggle
		SPgac dummy Game Toggle Calc
		SPpvp dummy PVP toggle
		SPsat dummy Saturation toggle
		SPwea dummy Weather toggle
		SPgmd dummy Gamemode toggle
		SPaff dummy Affected by Gamemode
		SPafc dummy Affected by Gamemode Calc
		SPdim dummy Dimension Number
		SPvot trigger Vote Trigger
		
		SPeti dummy Spawnpoint Event Timer - Ticks
		SPetm dummy Spawnpoint Event Timer - Mins
		SPetc dummy Spawnpoint Event Timer - Calc
		SPafk dummy Spawnpoint AFK Toggle
		SPafkc dummy Spawnpoint AFK Toggle - Calc
		SPafk1 stat.sneakTime AFK Crouch
		SPafk2 stat.swimOneCm AFK Swim
		SPafk3 stat.flyOneCm AFK Fly
		SPafk4 stat.walkOneCm AFK Walk
		SPafk5 stat.sprintOneCm AFK Sprint
		
		TSToken trigger SP TSToken Trigger
		SPts dummy TSToken
		SPtsl stat.leaveGame SP TSToken Leave Game
		SPtss dummy SP TSToken Selection
		SPtsi dummy SP TSToken Initialization
		SPtsn dummy SP TSToken Number of tries
		SPtsc dummy SP TSToken Calc
		SPtso dummy SP TSToken Option
	
	ARRAY {ACTIVATE CCU.iterateNumbers ARGS}:
		-1
	
	LOOP {0 Arr_Spawnpoint[-1] + 1} {1 Arr_Spawnpoint[L] + 1}:
		Arr_Spawnpoint[|0|][5] SPna = |1|
	
	kill @e[type=ArmorStand,SpawnStand]
	summon ArmorStand $SpawnStand$ {Tags:["SpawnStand"],Invulnerable:1,PersistenceRequired:1,Invisible:1,Marker:1,NoGravity:1}
	
	USE {@e[type=ArmorStand,SpawnStand]}:
		SPtp = 0
		SPpvp = 0
		SPsat = 1
		SPgmd = 1
		SPgam = 0
		SPaff = 7
//>

//<Adding Scoreboards Pulse
FUNC {Func_AddingScoreboardsPulse}:
	
	ARRAY {GLOBAL ACTIVATE CCU.objectiveRemove(Obj_AddingScoreboardsPulse) Obj_AddingScoreboardsPulse}:
		EC dummy Event Coordinator List
		MCTeams dummy MC Specialty Teams
		ADSave dummy Anvil Drop Save
		BASave dummy Breakaway Save
		DPSave dummy Death Pit Save
		ECRSave dummy EverChanging Race Save
		PVPSave dummy PVP Save
		STSave dummy Sand Tomb Save
		TPSave dummy The Pit Save
		TPMobSave dummy The Pit Mob Save
		VirusSave dummy Virus Save
		Health health
	
	CCU.objectiveAdd(Obj_AddingScoreboardsPulse)
	ScOS list Health
	
	ARRAY {GLOBAL ACTIVATE CCU.teamAdd(Team_AddingScoreboardsPulse) Team_AddingScoreboardsPulse}:
		NoPush
		OPT collisionRule never
		OPT friendlyfire false
		NoPVP
		OPT friendlyfire false
		
	FUNC {Func_AddUsersToScoreboard}:
		//= Things
			APPS = 2
			AT = 4
			EC = 8
			LE = 16
			APWBS = 32
		=//
		
		LOOP {Arr_Usernames[S]}:
			SPLIT {Arr_Temp |0|} { }
			
		///	APPS
			IF {Arr_Temp[0] = APPS}:
				Arr_Temp[1] MCTeams = 2
			
		///	AT
			IF {Arr_Temp[0] = AT}:
				Arr_Temp[1] MCTeams = 4
			
		///	EC
			IF {Arr_Temp[0] = EC}:
				Arr_Temp[1] MCTeams = 8
				Arr_Temp[1] EC = 0
			
		///	LE
			IF {Arr_Temp[0] = LE}:
				Arr_Temp[1] MCTeams = 16
			
		///	APWBS (aka exceptions)
			IF {Arr_Temp[0] = APWBS}:
				Arr_Temp[1] MCTeams = 32
				
		UNASSIGN {ARRAY Arr_Temp}
		
	ARRAY {ACTIVATE Func_AddUsersToScoreboard Arr_Usernames}:
		APPS Coopdog55
		APPS TheDarkOne239
		APPS DELHI123
		APPS Pikaslack
		APPS Soupyahtzee
		
		AT BUTTERLOVER7683
		
		EC Daa_
		EC french_man
		EC jamboree_lee
		EC CynRyn
		EC CMDZane
		
		LE FoxyTheBoomQueen
		LE idk_lobsters
		LE JadeofallTrades
		LE FirezFury
		
		APWBS Witch_Doctor
		APWBS Kilgore90
		APWBS Icohedron
	
///	Anvil Drop
	Percent1 ADSave = 50
	Percent2 ADSave = 0
	Walls ADSave = 0
	Timer1 ADSave = 50
	Timer2 ADSave = 0
	Timer3 ADSave = 0
	Jump ADSave = 0
	
///	Breakaway
	Arena BASave = 1
	Hunger BASave = 0
	Pickaxe BASave = 0
	Shovel BASave = 0
	Axe BASave = 0
	Shears BASave = 0
	SpeedPotion BASave = 0
	PVP BASave = 0
	Snowball BASave = 0
	
///	Death Pit
	MobID DPSave = 1
	Melee DPSave = 1
	Bow DPSave = 1
	Shield DPSave = 0
	Armor DPSave = 1
	Potion DPSave = 1
	Food DPSave = 2
	Lapis DPSave = 1
	Snowballs DPSave = 0
	Bones DPSave = 0
	
///	Everchanging Race
	Delay1 ECRSave = 60
	Delay2 ECRSave = 30
	Percent ECRSave = 35
	
/// PVP
	Weapons PVPSave = 1
	Mode PVPsave = 0
	Countdown PVPSave = 10
	GameTime PVPSave = 300
	
///	Sand Tomb
	LOOP {AF;CL}:
		JumpBoost|0| STSave = 0
		Blindness|0| STSave = 0
		NoSprint|0| STSave = 0
		Collisions|0| STSave = 0
		Potions|0| STSave = 0
		Delay|0| STSave = 5
	
///	The Pit 3
	Arena_Type TPSave = 0
	Arena_Size TPSave = 0
	Weapon TPSave = 10
	Pumpkin TPSave = 0
	PumpkinDelay TPSave = 40
	Teams TPSave = 0
	Movement TPSave = 0
	NoSprint TPSave = 0
	Jump TPSave = 1
	
	M_Regular TPMobSave = 0
	M_Pumpkin TPMobSave = 0
	M_Thorns TPMobSave = 0
	M_Porkchop TPMobSave = 0
	M_Knockback5 TPMobSave = 0
	M_Skeleton TPMobSave = 0	
	
///	Virus 
	V1GameTime VirusSave = 600
	V2GameTime VirusSave = 600
	V1Countdown VirusSave = 60
	V2Countdown VirusSave = 60
//>

//<SpawnPoint Testfor Clock
FUNC {Func_SpawnpointTFClock}:
	testfor @a[SPid>=1]
	CCU_SetblockTestfor
	
	LOOP {0 Arr_Spawnpoint[-1] + 1} {1 Arr_Spawnpoint[L] + 1}:
		spawnpoint @a[SPid=|1|] Arr_Spawnpoint[|0|][0]
		
		/*
		tellraw @a[SPid=|1|] $TextStart$
		{"text":"Your spawnpoint has been set to ","color":"gray"},
		{"text":"Arr_Spawnpoint[|0|][0]","color":"yellow","bold":"true"},
		{"text":" at ","color":"gray"},;
		*/
	///	2 = full name = |0|, 3 = name = |1|, 4 = color = |2|
		LOOP {Arr_Spawnpoint[|0|][3]} {Arr_Spawnpoint[|0|][4]}:
			/*
			{"text":"|0|","color":"|1|","bold":"true",
				"clickEvent":{"action":"run_command","value":"/scoreboard players set @p SPtp |1;1|"},
				"hoverEvent":{"action":"show_text","value":{"text":"Teleport to Arr_Spawnpoint[|0;1|][2]","color":"|1|"}}
			},;
			*/
		/*
		{"text":"!","color":"gray"}]}
		*/
	
	@a[SPid>=1] playsound Snd_XP voice @a[c=1,SPid>=1]
	ScOP @a[SPid>=1] SPid *= -1 Number
//>

//<Teleport Testfor Clock
FUNC {Func_TeleportTFClock}:
	testfor @a[SPtp>=1]
	CCU_SetblockTestfor
	
	LOOP {0 Arr_Spawnpoint[-1] + 1} {1 Arr_Spawnpoint[L] + 1}:
		tp @a[SPtp=|1|] Arr_Spawnpoint[|0|][1]
		
		/*
		tellraw @a[SPtp=|1|] $TextStart$
		{"text":"You have been teleported to ","color":"gray"},
		{"text":"Arr_Spawnpoint[|0|][0]","color":"yellow","bold":"true"},
		{"text":" at ","color":"gray"},;
		*/
	///	2 = full name = |0|, 3 = name = |1|, 4 = color = |2|
		LOOP {Arr_Spawnpoint[|0|][3]} {Arr_Spawnpoint[|0|][4]}:
			/*
			{"text":"|0|","color":"|1|","bold":"true",
				"clickEvent":{"action":"run_command","value":"/scoreboard players set @p SPtp |1;1|"},
				"hoverEvent":{"action":"show_text","value":{"text":"Teleport to Arr_Spawnpoint[|0;1|][2]","color":"|1|"}}
			},;
			*/
		/*
		{"text":"!","color":"gray"}]}
		*/
	
	@a[SPtp>=1] playsound Snd_XP voice @a[c=1,SPtp>=1]
	@a[SPtp>=1] SPtp = 0
//>

//<Game Start Tellraw Testfor Clock
FUNC {Func_GameStartTellrawTFClock}:
/// Warning: "x" must be turned off before another game can start.
	testfor @e[type=ArmorStand,SpawnStand,SPgac>=1]
	CCU_SetblockTestfor
	
	LOOP {0 Arr_Spawnpoint[-1] + 1} {1 Arr_Spawnpoint[L] + 1}:
		/*
		@e[type=ArmorStand,SpawnStand,SPgac>=1,SPgam=|1|] tellraw @a[EC=0] $TextStart$
		{"text":"Warning: ","color":"red"},;
		*/
	///	2 = full name = |0|, 3 = name = |1|, 4 = color = |2|
		LOOP {Arr_Spawnpoint[|0|][3]} {Arr_Spawnpoint[|0|][4]}:
			/*
			{"text":"|0|","color":"|1|","bold":"true",
				"clickEvent":{"action":"run_command","value":"/scoreboard players set @p SPtp |1;1|"},
				"hoverEvent":{"action":"show_text","value":{"text":"Teleport to Arr_Spawnpoint[|0;1|][2]","color":"|1|"}}
			},;
			*/
		/*
		{"text":" must be turned off before another game can start.","color":"gray"}
		]}
		*/
	
	@e[type=ArmorStand,SpawnStand,SPgac>=1] SPgac = 0
//>

//<Spawn Testfor Clock
FUNC {Func_SpawnTFClock}:
	testfor @a[$SP$]
	CCU_SetblockTestfor
	
///	@a SPsp = 0
///	@a[$SP$] SPsp = 1
	
///	effect @a[$SP$] 1 1 3 true
	effect @a[$SPTree$] 23 2 0 true
	gamemode 2 @a[$SPTree$,m=0]
	
	testfor @e[type=ArmorStand,SpawnStand,SPpvp=0]
	COND:
		J> NoPVP @a[$SPTree$,m=2]
	
	@a[$SPTree$,m=!3] SPti + 1
	@a[$SPNotTree$,m=!3] SPti = 0
	ScOP @a[$SPTree$,m=!3,SPti>=10] SPtp = @e[type=ArmorStand,SpawnStand,SPtp>=1] SPtp 
	
	@a[$SPPortal$,m=!3] stained_hardened_clay 5 ~ 3 ~ ScOP @a[c=1,m=!3] SPtp = mghub SPna
	@a[$SPPortal$,m=!3] stained_hardened_clay 2 ~ 3 ~ ScOP @a[c=1,m=!3] SPtp = pvphub SPna
	@a[$SPPortal$,m=!3] stained_hardened_clay 1 ~ 3 ~ ScOP @a[c=1,m=!3] SPtp = mmhub SPna
	@a[$SPPortal$,m=!3] stained_hardened_clay 9 ~ 3 ~ ScOP @a[c=1,m=!3] SPtp = racehub SPna
//>


//<Timer Testfor Clock
FUNC {Func_TimerTFClock}:
	testfor @e[type=ArmorStand,SPeti=1]
	CCU_SetblockTestfor
	
	@a SPafkc = 1
	@a SPetm + 0
	LOOP {1 5 + 1}:
		@a[SPdim=6,SPafk|0|>=1] SPafkc = 0
	
	LOOP {1 5 + 1}:
		@a[SPafk|0|>=1] SPafk|0| = 0
	
	@a[SPafkc=0] SPetc = 0
	@a[SPdim=6,SPafkc=1] SPetc + 1
	@a[SPetc>=6000] SPafk = 1
	@a[SPetc=0] SPafk = 0
	
	@e[type=ArmorStand,SPeti=1] @a[SPdim=6,SPafk=0] SPeti + 1
	@a[SPeti>=1200] SPetm + 1
	@a[SPeti>=1200] SPeti = 0
//>

//<Timer Reset Testfor Clock
FUNC {Func_ResetTimerTFClock}:
	testfor @e[type=ArmorStand,SpawnStand,SPeti>=2<=4]
	CCU_SetblockTestfor
	
	@e[type=ArmorStand,SpawnStand,SPeti=2] ScSS SPetm
	@e[type=ArmorStand,SpawnStand,SPeti=3] ScSS
	@e[type=ArmorStand,SpawnStand,SPeti=3] SPeti = 1
	
	@e[type=ArmorStand,SpawnStand,SPeti=4] + SpawnStandReset
	
	USE {@e[type=ArmorStand,SpawnStandReset] * reset}:
		SPeti
		SPetm
		SPetc
		SPafk
		SPafkc
	
	@e[type=ArmorStand,SpawnStandReset] SPeti = 0
	@e[type=ArmorStand,SpawnStandReset] + SpawnStandReset
	
	@e[type=ArmorStand,SpawnStand,SPeti>=2<=4] SPeti = 0
//>

//<TS Token Passcode Testfor Clock
FUNC {Func_TSTokenPasscodeTFClock}:
	testfor @e[type=ArmorStand,SpawnStand,TSToken>=1]
	CCU_SetblockTestfor
	
	@e[type=ArmorStand,SpawnStand,TSToken>=1] @a SPtsi + 0
	@e[type=ArmorStand,SpawnStand,TSToken>=1] @a[SPtsi=0] enable TSToken
	
	/*
	@e[type=ArmorStand,SpawnStand,TSToken>=1] tellraw @a[SPtsi=0] $TSTokenTextStart$
	{"text":"The ","color":"gray"},
	{"text":"Teamspeak Token","color":"dark_aqua","bold":"true",
		"hoverEvent":{"action":"show_text","value":{"text":"TP to the TSToken (ECs)","color":"dark_aqua"}},
		"clickEvent":{"action":"run_command","value":"/scoreboard players operation @p SPtp = tstoken SPna"}
	},
	{"text":" is now open! Click ","color":"gray"},
	{"text":"here","color":"yellow","bold":"true",
		"hoverEvent":{"action":"show_text","value":{"text":"Set the TSToken password","color":"yellow"}},
		"clickEvent":{"action":"suggest_command","value":"/guess "}
	},
	{"text":" to submit the password.","color":"gray"}
	]}
	*/
	
	@e[type=ArmorStand,SpawnStand,TSToken>=1] @a[SPtsi=0] SPtsi = 1
	
	@a[TSToken>=1] ScOP @a[c=1,r=1,TSToken>=1] SPtsc = @a[c=1,r=1,TSToken>=1] TSToken
	ScOP @a[TSToken<=-1] SPtsc *= -1 Number
	ScOP @a[TSToken>=1] SPtsc -= @e[type=ArmorStand,SpawnStand,TSToken>=1] TSToken
	ScOP @a[TSToken>=1,SPtsc=0] SPtp = tstoken SPna
	
	@a[TSToken>=1,SPtsc<=-1] SPtsc = 1
	tellraw @a[TSToken>=1,SPtsc>=1] $TSTokenTextStart${"text":"You have submitted an ","color":"gray"},{"text":"incorrect","color":"red"},{"text":" number.","color":"gray"}]}
	@a[TSToken>=1,SPtsc>=1] playsound Snd_XP voice @a[c=1] ~ ~ ~ 1 0.5
	/// tellraw @a[TSToken=0] $TSTokenTextStart${"text":"The number literally cannot be 0. It's time to stop.","color":"gray"}]}
	/// @a[TSToken=0] enable TSToken
	
	@a[TSToken>=1,SPtsc>=1] SPtsn + 1
	@a[TSToken>=1,SPtsc>=1,SPtsn<10] enable TSToken
	tellraw @a[TSToken>=1,SPtsc>=1,SPtsn=10] $TSTokenTextStart${"text":"You have submitted 10 incorrect answers, and have 0 tries left.","color":"gray"}]}
	@a[TSToken>=1,SPtsc>=1] SPtsc = 0
	@a[TSToken>=1] TSToken = 0
	
	@e[type=ArmorStand,SpawnStand,SPtss>=1<=2] * reset SPts
	@e[type=ArmorStand,SpawnStand,SPtss>=1<=2] SPtss + 2
	
	@e[type=ArmorStand,SpawnStand,SPtss=3] @a[$TSSelect$] stained_hardened_clay 5 ~ 19 ~ @a[c=1] reset SPts
	@e[type=ArmorStand,SpawnStand,SPtss=3] @a[$TSSelect$] coal_block 0 ~ 19 ~ @a[c=1] SPts = 1
	@e[type=ArmorStand,SpawnStand,SPtss=4] @a[$TSSelect$] stained_hardened_clay 5 ~ 19 ~ @a[c=1] SPts = 1
	@e[type=ArmorStand,SpawnStand,SPtss=4] @a[$TSSelect$] coal_block 0 ~ 19 ~ @a[c=1] SPts = 0
//>

//<TS Token Options Testfor Clock
FUNC {Func_TSTokenOptionsTFClock}:
	testfor @e[type=ArmorStand,SpawnStand,SPtso>=1]
	CCU_SetblockTestfor
	
///	Set passcode starts
///	Regular selection		SPtss 1
///	Opposite selection		SPtss 2

///	Stops TSToken				32
///	Opens arena					16
///	Removes coal				8
///	Closes arena				4
///	Chooses player (@r / WE)	2 / 1
	
	/*
	@e[type=ArmorStand,SpawnStand,SPtso>=64] tellraw @a[EC=0] $TSTokenTextStart$
	{"text":"Click here!","color":"yellow","italic":"true",
		"hoverEvent":{"action":"show_text","value":{"text":"Click here to set the TS Token Password","color":"yellow"}},
		"clickEvent":{"action":"suggest_command","value":"/scoreboard players set @e[type=ArmorStand,SpawnStand] TSToken #"}
	}
	]}
	*/
	
	@e[type=ArmorStand,SpawnStand,SPtso>=64] SPtso - 64

	@e[type=ArmorStand,SpawnStand,SPtso>=32] ScOR TSToken
	@e[type=ArmorStand,SpawnStand,SPtso>=32] ScOA TSToken trigger
	@e[type=ArmorStand,SpawnStand,SPtso>=32] ScOR SPts
	@e[type=ArmorStand,SpawnStand,SPtso>=32] ScOA SPts dummy TSToken
	@e[type=ArmorStand,SpawnStand,SPtso>=32] * reset SPtsi
	@e[type=ArmorStand,SpawnStand,SPtso>=32] * reset SPtsn
	@e[type=ArmorStand,SpawnStand,SPtso>=32] * reset SPtss
	@e[type=ArmorStand,SpawnStand,SPtso>=32] SPtso - 32
	
	@e[type=ArmorStand,SpawnStand,SPtso>=16] setblock $TSTokenClear$ redstone_block 0
	@e[type=ArmorStand,SpawnStand,SPtso>=16] SPtso - 16

	@e[type=ArmorStand,SpawnStand,SPtso>=8] fill $TSTokenFloor$ stained_hardened_clay 5 replace coal_block 0
	@e[type=ArmorStand,SpawnStand,SPtso>=8] SPtso - 8
	
	@e[type=ArmorStand,SpawnStand,SPtso>=4] ScSS SPts
	@e[type=ArmorStand,SpawnStand,SPtso>=4] setblock $TSTokenGlass$ redstone_block 0
	@e[type=ArmorStand,SpawnStand,SPtso>=4] SPtso - 4
	
	@e[type=ArmorStand,SpawnStand,SPtso>=2] @r[$TSSelect$] stained_hardened_clay 5 ~ 19 ~ setblock ~ 19 ~ coal_block 0
	@e[type=ArmorStand,SpawnStand,SPtso>=2] SPtso - 2
	
	@e[type=ArmorStand,SpawnStand,SPtso>=1] setblock $TSWorldedit$ redstone_block 0
	@e[type=ArmorStand,SpawnStand,SPtso>=1] SPtso = 0
//>

//<Other Clock
FUNC {Func_OtherClock}:
///	Dimension
	@a SPdim = 0
	@a SPdim = 6 {$Dim$}

///	Mastermind
	@a[$MM$,SPdim=6] SPmm = 1
	@a[$NotMM$,SPmm=1] SPmm = 2
	Mastermind SPmm ? 0 0
	COND:
		@a[SPmm=1] SPmm = 2
	@a[SPmm=2,EC=0] SPmm = 0
	clear @a[SPmm=2] wool -1 -1 {display:{Lore:["Mastermind Wool"]}}
	clear @a[SPmm=2] shears -1 -1 {display:{Lore:["Mastermind Shears"]}}
	@a[SPmm=2] SPmm = 0
	
///	The following are under conditional blocks because if not, they'll spam everything lol
///	PVP
	testfor @e[type=ArmorStand,SpawnStand,SPpvp=0]
	COND:
		J> NoPVP @a[team=!NoPVP,SPdim=6]
	
///	Saturation
	testfor @e[type=ArmorStand,SpawnStand,SPsat=1]
	COND:
		effect @a[SPdim=6] 23 2 0 true
	
///	Weather
	testfor @e[type=ArmorStand,SpawnStand,SPwea=0]
	COND:
		weather clear 1000000
	
	testfor @e[type=ArmorStand,SpawnStand,SPwea=1]
	COND:
		weather rain 1000000
	
	testfor @e[type=ArmorStand,SpawnStand,SPwea=2]
	COND:
		weather thunder 1000000
	
///	Regular gamemode
	testfor @e[type=ArmorStand,SpawnStand,SPgmd=1]
	COND:
		gamemode 2 @a[m=0,SPdim=6]
	
///	Affected players gamemode
	@a MCTeams + 0
	@a[MCTeams=0] MCTeams = 1
	
//=
SPaff
	OT		1
	APPS	2
	AT		4
	EC		8
	LE		16
	APWBS	32
	
	Normal settings: 1 + 2 + 4 = 7

SPst	Specialty teams		Players
	OT = 1
	APPS = 2
	AT = 4
	EC = 8 
	LE = 16
	APWBS = 32
=//

///	Calculating exceptions
	ScOP @e[type=ArmorStand,SpawnStand,SPaff>=1] SPafc = @e[type=ArmorStand,SpawnStand,SPaff>=1] SPaff
///	@e[type=ArmorStand,SpawnStand,SPafc>=64] @a[MCTeams=64,SPdim=6] SPafc = 1
///	@e[type=ArmorStand,SpawnStand,SPafc>=64] SPafc - 64
	testfor @e[type=ArmorStand,SpawnStand,SPafc>=32]
	COND:
		@a[MCTeams=32,SPdim=6] SPafc = 1
	@e[type=ArmorStand,SpawnStand,SPafc>=32] SPafc - 32
	testfor @e[type=ArmorStand,SpawnStand,SPafc>=16]
	COND:
		@a[MCTeams=16,SPdim=6] SPafc = 1
	@e[type=ArmorStand,SpawnStand,SPafc>=16] SPafc - 16
	testfor @e[type=ArmorStand,SpawnStand,SPafc>=8]
	COND:
		@a[MCTeams=8,SPdim=6] SPafc = 1
	@e[type=ArmorStand,SpawnStand,SPafc>=8] SPafc - 8
	testfor @e[type=ArmorStand,SpawnStand,SPafc>=4]
	COND:
		@a[MCTeams=4,SPdim=6] SPafc = 1
	@e[type=ArmorStand,SpawnStand,SPafc>=4] SPafc - 4
	testfor @e[type=ArmorStand,SpawnStand,SPafc>=2]
	COND:
		@a[MCTeams=2,SPdim=6] SPafc = 1
	@e[type=ArmorStand,SpawnStand,SPafc>=2] SPafc - 2
	testfor @e[type=ArmorStand,SpawnStand,SPafc>=1]
	COND:
		@a[MCTeams=1,SPdim=6] SPafc = 1
	@e[type=ArmorStand,SpawnStand,SPafc>=1] SPafc = 0
	
///	Gamemode
	gamemode 2 @a[m=1,SPafc=1,SPdim=6]
	gamemode 2 @a[m=0,SPafc=1,SPdim=6]
	
///	Leave Game Teleport
	ScOP @a[SPafc=1,SPlg>=1,SPdim=6] SPtp = spawn SPna
	
///	Clear is under SPaff because anytime you respawn from death, your inventory will be cleared for some reason because of this
///	This is even if the spawnpoint is somewhere else
	clear @a[$SPTree$,m=2,SPafc=1,SPdim=6]
	
	@a[SPafc=1] SPafc = 0
	@a[SPlg>=1] SPlg = 0
	
///	For voting
	* enable SPvot
	/*
	tellraw @a[SPvot>=1] $TextStart$
	{"text":"Vote here: ","color":"gray"},
	{"text":"Link 1","color":"green","bold":"true","underlined":"true",
		"clickEvent":{"action":"open_url","value":"http://minecraft-server-list.com/server/200887/vote/"},
		"hoverEvent":{"action":"show_text","value":{"text":"Vote link #1","color":"green"}}
	},
	{"text":" "},
	{"text":"Link 2","color":"dark_green","bold":"true","underlined":"true",
		"clickEvent":{"action":"open_url","value":"http://minecraftservers.org/vote/109809"},
		"hoverEvent":{"action":"show_text","value":{"text":"Vote link #2","color":"dark_green"}}
	},
	{"text":" "},
	{"text":"Link 3","color":"green","bold":"true","underlined":"true",
		"clickEvent":{"action":"open_url","value":"https://minecraftservers.biz/servers/2511/"},
		"hoverEvent":{"action":"show_text","value":{"text":"Vote link #3","color":"green"}}
	},
	{"text":" "},
	{"text":"Link 4","color":"dark_green","bold":"true","underlined":"true",
		"clickEvent":{"action":"open_url","value":"http://minecraft-mp.com/server/86376/vote/"},
		"hoverEvent":{"action":"show_text","value":{"text":"Vote link #4","color":"dark_green"}}
	},
	{"text":" "},
	{"text":"Link 5","color":"green","bold":"true","underlined":"true",
		"clickEvent":{"action":"open_url","value":"http://www.planetminecraft.com/server/edge-gamerscom/vote/"},
		"hoverEvent":{"action":"show_text","value":{"text":"Vote link #5","color":"green"}}
	}]}
	*/
	@a[SPvot>=1] playsound Snd_XP voice @a[c=1]
	@a[SPvot>=1] SPvot = 0

//>

//<Main End
FUNC {Func_GeneralScoreboardEnd}:
	CCU.objectiveRemove(Obj_GeneralScoreboardStart)
	
	kill @e[type=ArmorStand,SpawnStand]
//>


//<Groups
GROUP {Grp_MainStart}:
	CCU_Deactivate(Grp_MainStart)
	Func_GeneralScoreboardStart()
	CCU_Activate(Grp_SpawnpointTFClock)
	CCU_Activate(Grp_TeleportTFClock)
	CCU_Activate(Grp_GameStartTellrawTFClock)
	CCU_Activate(Grp_SpawnTFClock)
	CCU_Activate(Grp_BookTFClock)
	CCU_Activate(Grp_TimerTFClock)
	CCU_Activate(Grp_ResetTimerTFClock)
	CCU_Activate(Grp_TSTokenPasscodeTFClock)
	CCU_Activate(Grp_TSTokenOptionsTFClock)
	CCU_Activate(Grp_OtherClock)
	CCU_Activate(Spawnpoint_Book.Grp_BookTFClock)

GROUP {Grp_AddingScoreboardsPulse}:
	CCU_Deactivate(Grp_AddingScoreboardsPulse)
	Func_AddingScoreboardsPulse()

GROUP {Grp_SpawnpointTFClock}:
	Func_SpawnpointTFClock()

GROUP {Grp_TeleportTFClock}:
	Func_TeleportTFClock()

GROUP {Grp_GameStartTellrawTFClock}:
	Func_GameStartTellrawTFClock()

GROUP {Grp_SpawnTFClock}:
	Func_SpawnTFClock()

GROUP {Grp_TimerTFClock}:
	Func_TimerTFClock()

GROUP {Grp_ResetTimerTFClock}:
	Func_ResetTimerTFClock()

GROUP {Grp_TSTokenPasscodeTFClock}:
	Func_TSTokenPasscodeTFClock()

GROUP {Grp_TSTokenOptionsTFClock}:
	Func_TSTokenOptionsTFClock()

GROUP {Grp_OtherClock}:
	Func_OtherClock()

GROUP {Grp_MainEnd}:
	CCU_Deactivate(Grp_MainEnd)
	Func_GeneralScoreboardEnd()
	CCU_Deactivate(Grp_SpawnpointTFClock)
	CCU_Deactivate(Grp_TeleportTFClock)
	CCU_Deactivate(Grp_GameStartTellrawTFClock)
	CCU_Deactivate(Grp_SpawnTFClock)
	CCU_Deactivate(Grp_BookTFClock)
	CCU_Deactivate(Grp_TimerTFClock)
	CCU_Deactivate(Grp_ResetTimerTFClock)
	CCU_Deactivate(Grp_TSTokenPasscodeTFClock)
	CCU_Deactivate(Grp_TSTokenOptionsTFClock)
	CCU_Deactivate(Grp_OtherClock)
	CCU_Deactivate(Spawnpoint_Book.Grp_BookTFClock)

FINALIZE:
	CCU.cmdSave()
//>