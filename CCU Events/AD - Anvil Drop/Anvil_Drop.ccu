//(Anvil Drop)
///	C:\Users\Austin\Documents\Austin\powder game code\eGO\Events & Commands\AD - Anvil Drop\CBP_AnvilDrop.cbp

//<General definitions
DEF $SA$ x=-38,y=0,z=76,dx=22,dy=35,dz=22
DEF $SPid$ 21
DEF $SPpvp$ 1
DEF $SPsat$ 1

DEF $CMDName$ AD
DEF $CMDSizeX$ 16
DEF $CMDSizeY$ 10
DEF $CMDSizeZ$ 9

DEF $StyleDirection$ -X
DEF COORDS $CoordsOption$ -65 15 79

DEF $TextStart$ {"text":"","extra":[{"text":"[","color":"gray"},{"text":"AD","color":"green","bold":"true","hoverEvent":{"action":"show_text","value":{"text":"Anvil Drop","color":"green"}}},{"text":"]","color":"gray"},{"text":": "},

IMPORT {LIBRARY GETDIR General}
//>

//<Options
OPTIONS:
	blockOption stonebrick 0
	coordsOption $CoordsOption$
	styleOption $StyleDirection$ 10
	parseOption true
	commandOption true
	combinerOption true
///	filePathFuncOption 
	keepCoordsOption false
//>

//<Definitions
///	Spawn
DEF $SP$ x=-19,y=4,z=85,dx=7,dy=5,dz=4
DEF $SelectArena$ x=-33,y=2,z=81,dx=12,dy=5,dz=12

///	Redstone Block for the structure block coords
DEF COORDS $RBlock1$ -36 15 80
DEF COORDS $RBlock2$ -36 14 80
DEF COORDS $RBlock3$ -36 13 80

///	Structure block
DEF COORDS $SBlock1$ -36 15 79
DEF COORDS $SBlock2$ -36 14 79
DEF COORDS $SBlock3$ -36 13 79

///	Other
DEF COORDS $Door$ -20 7 88 -20 5 86
DEF COORDS $ArenaFill$ -33 15 93 -21 3 81
//>

//<Comments
//=
	ADSave:
	Percent1	0 - 100
	Percent2	0 - 100
	Walls		0 - 1
	Timer1		0 - 1000000
	Timer2		0 - 1000000
	Timer3		0 - 1000000
	Jump		0 / 1
	
	{mode:"LOAD",ignoreEntities:1b,rotation:"NONE",posX:-1,posY:5,posZ:0,sizeX:21,sizeY:1,sizeZ:21,name:"TP_|0|",showboundingbox:0b}
=//
//>


//<Open Doors
FUNC {Func_OpenDoors}:
	fill $Door$ air 0 replace glass 0
	title @a[$SA$] title {"text":""}
	title @a[$SA$] subtitle {"text":"The door is now open","color":"green"}
//>

//<Close Doors
FUNC {Func_CloseDoors}:
	fill $Door$ glass 0 replace air 0
//>


//<Countdown Testfor Clock
FUNC {Func_CountdownTFClock}:
	testfor @e[type=AreaEffectCloud,ADCountdown]
	CCU_SetblockTestfor
	
	@e[type=AreaEffectCloud,ADCountdown] ADti + 1
	@e[type=AreaEffectCloud,ADCountdown,ADti=1] Countdown AD = 6
	@e[type=AreaEffectCloud,ADCountdown,ADti=20] @a[$SA$] playsound Snd_Pling voice @a[c=1]
	@e[type=AreaEffectCloud,ADCountdown,ADti=20] tellraw @a[$SA$] $TextStart${"text":"5","color":"yellow","bold":"true"}]}
	@e[type=AreaEffectCloud,ADCountdown,ADti=20] Countdown AD = 5
	@e[type=AreaEffectCloud,ADCountdown,ADti=40] @a[$SA$] playsound Snd_Pling voice @a[c=1]
	@e[type=AreaEffectCloud,ADCountdown,ADti=40] tellraw @a[$SA$] $TextStart${"text":"4","color":"yellow","bold":"true"}]}
	@e[type=AreaEffectCloud,ADCountdown,ADti=40] Countdown AD = 4
	@e[type=AreaEffectCloud,ADCountdown,ADti=60] @a[$SA$] playsound Snd_Pling voice @a[c=1]
	@e[type=AreaEffectCloud,ADCountdown,ADti=60] tellraw @a[$SA$] $TextStart${"text":"3","color":"yellow","bold":"true"}]}
	@e[type=AreaEffectCloud,ADCountdown,ADti=60] Countdown AD = 3
	@e[type=AreaEffectCloud,ADCountdown,ADti=80] @a[$SA$] playsound Snd_Pling voice @a[c=1]
	@e[type=AreaEffectCloud,ADCountdown,ADti=80] tellraw @a[$SA$] $TextStart${"text":"2","color":"yellow","bold":"true"}]}
	@e[type=AreaEffectCloud,ADCountdown,ADti=80] Countdown AD = 2
	@e[type=AreaEffectCloud,ADCountdown,ADti=100] @a[$SA$] playsound Snd_Pling voice @a[c=1]
	@e[type=AreaEffectCloud,ADCountdown,ADti=100] tellraw @a[$SA$] $TextStart${"text":"1","color":"yellow","bold":"true"}]}
	@e[type=AreaEffectCloud,ADCountdown,ADti=100] Countdown AD = 1
	
	@e[type=AreaEffectCloud,ADCountdown,ADti=120] tellraw @a[$SA$] $TextStart${"text":"GO!","color":"green","bold":"true"}]}
	@e[type=AreaEffectCloud,ADCountdown,ADti=120] ScOP @e[type=ArmorStand,ADStand1] ADti = Timer1 ADSave
	@e[type=AreaEffectCloud,ADCountdown,ADti=120] ScOP @e[type=ArmorStand,ADStand2] ADti = Timer2 ADSave
	@e[type=AreaEffectCloud,ADCountdown,ADti=120] ScOP @e[type=ArmorStand,ADStand3] ADti = Timer3 ADSave
	@e[type=AreaEffectCloud,ADCountdown,ADti=120] @e[type=ArmorStand,ADStand1] ADte + 0
	@e[type=AreaEffectCloud,ADCountdown,ADti=120] @e[type=ArmorStand,ADStand1] ADte2 + 0
	@e[type=AreaEffectCloud,ADCountdown,ADti=120] Countdown reset AD
	
	@e[type=AreaEffectCloud,ADCountdown,ADti=120] @a[$SA$] playsound entity.experience_orb.touch voice @a[c=1]
	@e[type=AreaEffectCloud,ADCountdown,ADti=123] @a[$SA$] playsound entity.experience_orb.touch voice @a[c=1]
	@e[type=AreaEffectCloud,ADCountdown,ADti=126] @a[$SA$] playsound entity.experience_orb.touch voice @a[c=1]
//>

//<Change Percent Testfor Clock 1 and 2
LOOP {1 2 + 1}:
	FUNC {Func_ChangePercentTFClock|0|}:
		testfor @e[type=ArmorStand,ADStand|0;1|,ADps=1]
		CCU_SetblockTestfor
		
		LOOP {0 100 + 1} {0 1 + 0.01}:
			IF {|0| < 100}:
				/*
				@e[type=ArmorStand,ADStand|0;2|,ADps=1,ADpe=|0|] setblock $SBlock|0;2|$ structure_block 0 replace {integrity:|1|f,
				mode:"LOAD",posX:3,posY:-3,posZ:2,name:"AnvilDrop",showboundingbox:0b}
				*/
				
			IF {|0| = 100}:
				/*
				@e[type=ArmorStand,ADStand|0;2|,ADps=1,ADpe=|0|] setblock $SBlock|0;2|$ structure_block 0 replace {integrity:1.0f,
				mode:"LOAD",posX:3,posY:-3,posZ:2,name:"AnvilDrop",showboundingbox:0b}
				*/
		
		@e[type=ArmorStand,ADStand|0;2|,ADps=1] ADps = 0
//>

//<Change Percent Testfor Clock 3
FUNC {Func_ChangePercentTFClock3}:
	testfor @e[type=ArmorStand,ADStand3,ADps=1]
	SetblockTestfor
	
	@e[type=ArmorStand,ADStand3,ADps=1,ADpe=0] setblock $SBlock3$ air 0
	@e[type=ArmorStand,ADStand3,ADps=1,ADpe=1] setblock $SBlock3$ structure_block 0 replace {mode:"LOAD",posX:3,posY:-1,posZ:2,name:"AnvilDropWalls",showboundingbox:0b}
	
	@e[type=ArmorStand,ADStand3,ADps=1] ADps = 0
//>

//<Reset Settings
FUNC {Func_ResetSettings}:
	Percent1 ADSave = 50
	Percent2 ADSave = 0
	Walls ADSave = 0
	Timer1 ADSave = 50
	Timer2 ADSave = 0
	Timer3 ADSave = 0
	Jump ADSave = 0
//>

//<Change Settings
FUNC {Func_ChangeSettings}:
/*
	tellraw @a[EC=0] $TextStart$
	{"text":"Layer 1: ","color":"gray"},
	{"text":"[%]","color":"gold",
		"hoverEvent":{"action":"show_text","value":{"text":"Edit the percent in layer 1","color":"gold"}},
		"clickEvent":{"action":"suggest_command","value":"/scoreboard players set Percent1 ADSave #"}
	},
	
	{"text":" ","color":"gray"},
	{"text":"[T]","color":"gold",
		"hoverEvent":{"action":"show_text","value":{"text":"Edit the timer in layer 1","color":"gold"}},
		"clickEvent":{"action":"suggest_command","value":"/scoreboard players set Timer1 ADSave #"}
	},
	
	{"text":"    Layer 2: ","color":"gray"},
	{"text":"[%]","color":"gold",
		"hoverEvent":{"action":"show_text","value":{"text":"Edit the percent in layer 2","color":"gold"}},
		"clickEvent":{"action":"suggest_command","value":"/scoreboard players set Percent2 ADSave #"}
	},
	{"text":" ","color":"gray"},
	{"text":"[T]","color":"gold",
		"hoverEvent":{"action":"show_text","value":{"text":"Edit the timer in layer 2","color":"gold"}},
		"clickEvent":{"action":"suggest_command","value":"/scoreboard players set Timer2 ADSave #"}
	},
	
	{"text":"    Walls : ","color":"gray"},
	{"text":"[+]","color":"gold",
		"hoverEvent":{"action":"show_text","value":{"text":"Turn on walls","color":"gold"}},
		"clickEvent":{"action":"run_command","value":"/scoreboard players set Walls ADSave 1"}
	},
	{"text":" ","color":"gray"},
	{"text":"[-]","color":"gold",
		"hoverEvent":{"action":"show_text","value":{"text":"Turn off walls","color":"gold"}},
		"clickEvent":{"action":"run_command","value":"/scoreboard players set Walls ADSave 0"}
	},
	{"text":" ","color":"gray"},
	{"text":"[T]","color":"gold",
		"hoverEvent":{"action":"show_text","value":{"text":"Edit the timer for the walls","color":"gold"}},
		"clickEvent":{"action":"suggest_command","value":"/scoreboard players set Timer3 ADSave #"}
	}
	]}
*/
//>


//<General Scoreboard Start
FUNC {Func_GeneralScoreboardStart}:
	USE {ScOA}:
		CCU_NumberObjAdd
		ADSave dummy Anvil Drop Save
	
	ARRAY {GLOBAL ACTIVATE CCU.objectiveAdd(Obj_GeneralScoreboardStart) Obj_GeneralScoreboardStart}:
		AD dummy Anvil Drop
		ADcl dummy Anvil Drop Calc
		ADpe dummy Anvil Drop Percent
		ADpc dummy Anvil Drop Percent Calc
		ADps dummy Anvil Drop Percent Set
		ADti dummy Anvil Drop Timer
		ADte dummy Anvil Drop Time Elapsed
		ADte2 dummy Anvil Drop Time Elapsed
		ADde deathCount Anvil Drop Deaths
		ADpl dummy Anvil Drop Player List
	
	ARRAY {GLOBAL ACTIVATE CCU.teamAdd(Team_GeneralScoreboardStart) Team_GeneralScoreboardStart}:
		AD
		OPT color green
		OPT collisionRule never
		OPT friendlyfire false
		
		ADd_y
		OPT color yellow
	
	@a[$SA$,m=2] ADpl + 0
	AD J> @a[$SA$,m=2,ADpl=0]
	
	clear @a[$SA$,m=2,ADpl=0]
	@a[$SA$,m=2,ADpl=0,EC=0] SPbk = $SPid$
	
	/*
	replaceitem entity @a[$SA$,m=2,ADpl=0] slot.armor.head stained_glass 1 5 
	{AttributeModifiers:[{AttributeName:"generic.knockbackResistance",Name:"anything",Amount:1,Operation:0,UUIDLeast:1,UUIDMost:1,Slot:"head"}]}
	*/
	
	effect @a[$SA$,m=2,ADpl=0] clear
	effect @a[$SA$,m=2,ADpl=0] 6 1 100 true
	xp -1000L @a[$SA$,m=2,ADpl=0]
	ADd_y J> Countdown Players Time_Elapsed
	
	@a[$SelectArena$,m=2,ADpl=0] ADpl = 1
	@a[$SA$,m=2,ADpl=0] ADpl = 2
	
	CCU_ExecuteSpawnStand scoreboard objectives setdisplay sidebar AD
	
	CCU_ExecuteSpawnStand summon ArmorStand ~ ~ ~ {Tags:["ADEntity","ADStand","ADStand1"],Invulnerable:1,PersistenceRequired:1,Invisible:1,Marker:1,NoGravity:1}
	CCU_ExecuteSpawnStand summon ArmorStand ~ ~ ~ {Tags:["ADEntity","ADStand","ADStand2"],Invulnerable:1,PersistenceRequired:1,Invisible:1,Marker:1,NoGravity:1}
	CCU_ExecuteSpawnStand summon ArmorStand ~ ~ ~ {Tags:["ADEntity","ADStand","ADStand3"],Invulnerable:1,PersistenceRequired:1,Invisible:1,Marker:1,NoGravity:1}
	CCU_ExecuteSpawnStand summon AreaEffectCloud ~ ~ ~ {Tags:["ADEntity","ADCountdown"],Duration:150}
//>

//<General Scoreboard Clock
FUNC {Func_GeneralScoreboardClock}:
///	General Players
	clear @a[$SA$] anvil
	kill @e[$SA$,type=Item]
	
	@a[m=2,ADpl=1,ADde>=1] ADpl = 2
	@a[$SP$,m=2,ADpl=1] ADpl = 2
	clear @a[$SP$,m=2,ADpl=2]
	@a[m=2,ADpl=2,EC=0] SPbk = $SPid$
	@a[ADde>=1] ADde = 0
	@a[ADpl=2] ADpl = 3
	NoPVP J> @a[$SA$,m=2,team=!NoPVP,ADpl=3]
	
///	Display
	Players ADpl = 0
	@a[$SA$,m=2,ADpl=1] Players ADpl + 1
	ScOP Players AD = Players ADpl
	
	@e[type=ArmorStand,ADStand1,ADte>=0] ADte + 1
	@e[type=ArmorStand,ADStand1,ADte=20] ADte2 + 1
	@e[type=ArmorStand,ADStand1,ADte=20] ADte = 0
	ScOP Time_Elapsed AD = @e[type=ArmorStand,ADStand1,ADte>=0] ADte2
	
///	Calculating percent
	@e[type=ArmorStand,ADStand] ScOP @e[c=1,r=1,type=ArmorStand,ADStand] ADpc = @e[c=1,r=1,type=ArmorStand,ADStand] ADpe
	ScOP @e[type=ArmorStand,ADStand1] ADpe = Percent1 ADSave
	ScOP @e[type=ArmorStand,ADStand2] ADpe = Percent2 ADSave
	ScOP @e[type=ArmorStand,ADStand3] ADpe = Walls ADSave
	@e[type=ArmorStand,ADStand] ScOP @e[c=1,r=1,type=ArmorStand,ADStand] ADpc -= @e[c=1,r=1,type=ArmorStand,ADStand] ADpe
	
	@e[type=ArmorStand,ADStand,ADpc<=-1] ADps = 1
	@e[type=ArmorStand,ADStand,ADpc>=1] ADps = 1
	
///	Calculating anvil timers
	@e[type=ArmorStand,ADStand,ADti>=0] ADti + 1
	ScOP @e[type=ArmorStand,ADStand1,ADti>=0] ADcl = Timer1 ADSave
	ScOP @e[type=ArmorStand,ADStand2,ADti>=0] ADcl = Timer2 ADSave
	ScOP @e[type=ArmorStand,ADStand3,ADti>=0] ADcl = Timer3 ADSave
	@e[type=ArmorStand,ADStand,ADcl=0] ADti = 0
	@e[type=ArmorStand,ADStand,ADti>=0] ScOP @e[c=1,r=1,type=ArmorStand,ADStand,ADti>=0] ADcl -= @e[c=1,r=1,type=ArmorStand,ADStand,ADti>=0] ADti
	
	@e[type=ArmorStand,ADStand1,ADti>=1,ADpe>=1,ADcl<=0] setblock $RBlock1$ redstone_block 0
	@e[type=ArmorStand,ADStand2,ADti>=1,ADpe>=1,ADcl<=0] setblock $RBlock2$ redstone_block 0
	@e[type=ArmorStand,ADStand3,ADti>=1,ADpe>=1,ADcl<=0] setblock $RBlock3$ redstone_block 0
	@e[type=ArmorStand,ADStand,ADti>=1,ADcl<=0] ADti = 0
	
///	Jump effect
	Jump ADSave ? 0 0
	COND:
		effect @a[$SA$,m=2,ADpl=1] 8 2 250 true
	
	Jump ADSave ? 1 1
	COND:
		effect @a[$SA$,m=2,ADpl=1] 8 0 0 true
//>

//<General Scoreboard End
FUNC {Func_GeneralScoreboardEnd}:
	CCU.objectiveRemove(Obj_GeneralScoreboardStart)
	CCU.teamRemove(Team_GeneralScoreboardStart)
	
	clear @a[$SA$,m=2]
	@a[$SA$,m=2] SPbk = $SPid$
	
	kill @e[ADEntity]
	kill @e[$SA$,type=FallingSand]
	fill $ArenaFill$ air 0
	effect @a[$SA$] 6 1 100 true
//>


//<Groups
GROUP {Grp_MainStart}:
	CCU.spawnStandStart()
	CCU_Deactivate(GSELF)
	Func_GeneralScoreboardStart()
	CCU_Activate(Grp_MainClock)
	CCU_Activate(Grp_ChangePercentTFClock1)
	CCU_Activate(Grp_ChangePercentTFClock2)
	CCU_Activate(Grp_ChangePercentTFClock3)
	CCU_Activate(Grp_CountdownTFClock)
	CCU_Activate(Grp_CloseDoors)
	CCU_FinalSpawnStand

GROUP {Grp_MainClock}:
	CCU.spawnIDSetting()
	Func_GeneralScoreboardClock()

GROUP {Grp_MainEnd}:
	CCU.spawnStandEnd()
	CCU_Deactivate(GSELF)
	Func_GeneralScoreboardEnd()
	CCU_Deactivate(Grp_MainClock)
	CCU_Deactivate(Grp_ChangePercentTFClock1)
	CCU_Deactivate(Grp_ChangePercentTFClock2)
	CCU_Deactivate(Grp_ChangePercentTFClock3)
	CCU_Deactivate(Grp_CountdownTFClock)

GROUP {Grp_CountdownTFClock}:
	Func_CountdownTFClock()

LOOP {1 3 + 1}:
	GROUP {CLOCK Grp_ChangePercentTFClock|0|}:
		Func_ChangePercentTFClock|0|()

GROUP {Grp_OpenDoors}:
	CCU_Deactivate(GSELF)
	Func_OpenDoors()

GROUP {Grp_CloseDoors}:
	CCU_Deactivate(GSELF)
	Func_CloseDoors()

GROUP {Grp_ResetSettings}:
	CCU_Deactivate(GSELF)
	Func_ResetSettings()

GROUP {Grp_ChangeSettings}:
	CCU_Deactivate(GSELF)
	Func_ChangeSettings()

CCU.cmdSave()
//>




















