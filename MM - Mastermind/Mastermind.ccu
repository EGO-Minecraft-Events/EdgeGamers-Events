//(Mastermind)
///	C:\Users\Austin\Documents\Austin\powder game code\eGO\Events & Commands\MM - Mastermind\PCBP_Mastermind.cbp

//<Defines: Regular
DEF $SA$ x=45,y=3,z=22,dx=64,dy=30,dz=65
DEF $SPid$ 9
DEF $SPpvp$ 1
DEF $SPsat$ 1

DEF $CMDName$ MM
DEF $CMDSizeX$ 32
DEF $CMDSizeY$ 16
DEF $CMDSizeZ$ 16

DEF $BlockOption$ stonebrick 0
DEF $StyleDirection$ +Z
DEF COORDS $CoordsOption$ 63 15 79

DEF $TextStart$ {"text":"","extra":[{"text":"[","color":"gray"},{"text":"MM","color":"gold","bold":"true","hoverEvent":{"action":"show_text","value":{"text":"MasterMind","color":"gold"}}},{"text":"]","color":"gray"},{"text":": "},
//>

//<Options
OPTIONS:
	blockOption $BlockOption$
	coordsOption $CoordsOption$
	styleOption $StyleDirection$ 9
	parseOption true
	commandOption true
	combinerOption true
///	filePathFuncOption 
	parseChangesOption false
//>

//<Defines: Large
/*
DEF $SignRegular$ 
	{Text1:"{\"text\":\"-=-\"}",
	Text2:"{\"text\":\"\",\"extra\":[{\"score\":{\"name\":\"@e[type=ArmorStand,MMAR,r=1]\",\"objective\":\"MMpa2\"}},{\"text\":\" red\"}]}",
	Text3:"{\"text\":\"\",\"extra\":[{\"score\":{\"name\":\"@e[type=ArmorStand,MMAW,r=1]\",\"objective\":\"MMpa2\"}},{\"text\:\" white\"}]}",
	Text4:"{\"text\":\"-=-\"}"}

-/-

DEF $SignWin$ 
	{Text1:"{\"text\":\"--=--\"}",
	Text2:"{"text":"4 red","bold":"true"}",
	Text3:"{"text":"Shhhh","bold":"true"}",
	Text4:"{\"text\":\"--=--\"}"}

-/-

DEF $SignRed$ 
	{Text1:"{\"text\":\"-=-\"}",
	Text2:"{"{\"text\":\"\",\"extra\":[{\"score\":{\"name\":\"@e[type=ArmorStand,MMAR,r=1]\",\"objective\":\"MMpa2\"}},{\"text\":\" red\"}]}",
	Text3:"{\"text\":\"-=-\"}",
	Text4:"{\"text\":\"\"}"}

-/-

DEF $SignWhite$ 
	{Text1:"{\"text\":\"-=-\"}",
	Text2:"{\"text\":\"\",\"extra\":[{\"score\":{\"name\":\"@e[type=ArmorStand,MMAW,r=1]\",\"objective\":\"MMpa2\"}},{\"text\:\" white\"}]}",
	Text3:"{\"text\":\"-=-\"}",
	Text4:"{\"text\":\"\"}"}

-/-

DEF $SignFix$ 
	{Text1:"{\"text\":\"-=-\"}",
	Text2:"{\"text\":\"\"}",
	Text3:"{\"text\":\"\"}",
	Text4:"{\"text\":\"-=-\"}",}

-/-

DEF $AreaEffectCloudBeg$ 
	{Age:6000s,Item:{id:"minecraft:stone",Count:1b},Passengers:[
		{id:AreaEffectCloud,Tags:["MMEntity","MMRA","MMRA1"],Duration:5},
		{id:AreaEffectCloud,Tags:["MMEntity","MMRA","MMRA2"],Duration:5},
		{id:AreaEffectCloud,Tags:["MMEntity","MMRA","MMRA3"],Duration:5},
		{id:AreaEffectCloud,Tags:["MMEntity","MMRA","MMRA4"],Duration:5},
		{id:AreaEffectCloud,Tags:["MMEntity","MMRA","MMRA5"],Duration:5},
		{id:AreaEffectCloud,Tags:["MMEntity","MMRA","MMRA6"],Duration:5},
		{id:AreaEffectCloud,Tags:["MMEntity","MMRW","MMAW"],Duration:5},
		{id:AreaEffectCloud,Tags:["MMEntity","MMRW","MMAR"],Duration:5}
	]}

-/-
	
DEF $AreaEffectCloudRand$ 
	{Age:6000s,Item:{id:"minecraft:stone",Count:1b},Passengers:[
		{id:AreaEffectCloud,Duration:5,Tags:["MMRAN","MMRAN1","MMRANred"]},
		{id:AreaEffectCloud,Duration:5,Tags:["MMRAN","MMRAN2","MMRANlime"]},
		{id:AreaEffectCloud,Duration:5,Tags:["MMRAN","MMRAN3","MMRANcyan"]},
		{id:AreaEffectCloud,Duration:5,Tags:["MMRAN","MMRAN4","MMRANmagenta"]},
		{id:AreaEffectCloud,Duration:5,Tags:["MMRAN","MMRAN5","MMRANpink"]},
		{id:AreaEffectCloud,Duration:5,Tags:["MMRAN","MMRAN6","MMRANorange"]}
	]}

-/-
	
DEF $FakeBroadcast$ 
	{"text":"","color":"green","extra":[
	{"text":"[","color":"gray"},
	{"text":"MM","color":"gold","bold":"true","hoverEvent":{"action":"show_text","value":{"text":"MasterMind","color":"gold"}}},
	{"text":"]","color":"gray"},
	{"text":": ","color":"white"},
	{"text":"The correct combo is "},
	{"selector":"@e[type=ArmorStand,MMCA1]"},
	{"text":", "},
	{"selector":"@e[type=ArmorStand,MMCA2]"},
	{"text":", "},
	{"selector":"@e[type=ArmorStand,MMCA3]"},
	{"text":" and "},
	{"selector":"@e[type=ArmorStand,MMCA4]"},
	{"text":"!"}
	]}

-/-

DEF $CustomAS1$ 
Rotation:[180f,0f],
Pose:{
	Body:[0f,0f,0f],
	Head:[0f,0f,0f],
	LeftArm:[0f,0f,0f],
	RightArm:[0f,0f,0f],
	LeftLeg:[-90f,0f,0f],
	RightLeg:[-90f,0f,0f]
},ArmorItems:[
	{id:leather_boots,Count:1b,tag:{display:{color:1644825}}},
	{id:leather_leggings,Count:1b,tag:{display:{color:1644825}}},
	{id:leather_chestplate,Count:1b,tag:{display:{color:1644825}}},
	{id:skull,Count:1b,Damage:3s,tag:{SkullOwner:Daa_}}
],HandItems:[{},{}]

-/-

DEF $CustomAS2$ 
Rotation:[180f,0f],
Pose:{
	Body:[0f,0f,0f],
	Head:[0f,0f,0f],
	LeftArm:[0f,0f,-20f],
	RightArm:[-50f,0f,20f],
	LeftLeg:[-90f,0f,0f],
	RightLeg:[-90f,0f,0f]
},ArmorItems:[
	{id:leather_boots,Count:1b,tag:{display:{color:3361970}}},
	{id:leather_leggings,Count:1b,tag:{display:{color:3361970}}},
	{id:leather_chestplate,Count:1b,tag:{display:{color:3361970}}},
	{id:skull,Count:1b,Damage:3s,tag:{SkullOwner:french_man}}
],HandItems:[{id:"minecraft:command_block",Count:1b},{}]

-/-

DEF $CustomAS3$ 
Rotation:[180f,0f],
Pose:{
	Body:[0f,0f,0f],
	Head:[0f,0f,0f],
	LeftArm:[0f,0f,0f],
	RightArm:[0f,0f,f],
	LeftLeg:[-90f,0f,0f],
	RightLeg:[-90f,0f,0f]
},ArmorItems:[
	{id:leather_boots,Count:1b,tag:{display:{color:15892389}}},
	{id:leather_leggings,Count:1b,tag:{display:{color:15892389}}},
	{id:leather_chestplate,Count:1b,tag:{display:{color:15892389}}},
	{id:skull,Count:1b,Damage:3s,tag:{SkullOwner:jamboree_lee}}
],HandItems:[{},{}]

-/-

DEF $CustomAS4$ 
Rotation:[180f,0f],
Pose:{
	Body:[0f,0f,0f],
	Head:[0f,0f,0f],
	LeftArm:[0f,0f,0f],
	RightArm:[0f,0f,0f],
	LeftLeg:[-90f,0f,0f],
	RightLeg:[-90f,0f,0f]
},ArmorItems:[
	{id:leather_boots,Count:1b,tag:{display:{color:11685080}}},
	{id:leather_leggings,Count:1b,tag:{display:{color:11685080}}},
	{id:leather_chestplate,Count:1b,tag:{display:{color:11685080}}},
	{id:skull,Count:1b,Damage:3s,tag:{SkullOwner:CynRyn}}
],HandItems:[{},{}]
*/
//>

//<Defines: Room Coords
///	Room coordinates: North +11
DEF COORDS $RoomN$ 46 4 24

///	Room coordinates: South +11
DEF COORDS $RoomS$ 46 4 47

DEF $RoomSD$ dx=7,dy=4,dz=12

///	Select rooms 
///	Defines $SR1$ up to $SR12$
LOOP {1 12 + 1} {0 121 + 11}:
	IF {|0| <= 6}:
		DEF GLOBAL COORDS $MMRoom|0;1|$ $RoomN$[x + |1;1|, y, z]
		
	IF {|0| > 6}:
		DEF GLOBAL COORDS $MMRoom|0;1|$ $RoomS$[x + |1;1| - 66, y, z]

LOOP {1 12 + 1}:
	DEF GLOBAL $SR|0;1|$ x=$MMRoom|0;1|$[x],y=$MMRoom|0;1|$[y],z=$MMRoom|0;1|$[z],$RoomSD$

DEF COORDS $FillDoorsN$ 49 8 37 105 6 37
DEF COORDS $FillDoorsS$ 49 8 46 105 6 46

DEF COORDS $FillButtonsN$ 46 6 26 101 6 34
DEF COORDS $FillButtonsS$ 53 6 57 108 6 49

DEF COORDS $FillStructure$ 45 2 60 109 2 23

IMPORT {LIBRARY GETDIR 1.10/General}
//>

//<Defines: Other
///	Number of groups
DEF $MaxNum$ 12

///	Structure block save answer
DEF COORDS $StructureSave$ 48 10 72

///	Select Hallway
DEF $Hallway$ x=46,y=5,z=38,dx=62,dy=5,dz=7

/// MMCA Armor Stand Coords
DEF COORDS $MMCACoords$ 50 17.67 72

///	Wool Color
DEF $WoolC1$ 14
DEF $WoolC2$ 5
DEF $WoolC3$ 9
DEF $WoolC4$ 2
DEF $WoolC5$ 6
DEF $WoolC6$ 1

///	Wool Color Word
DEF $WoolCW1$ red
DEF $WoolCW2$ lime
DEF $WoolCW3$ cyan
DEF $WoolCW4$ magenta
DEF $WoolCW5$ pink
DEF $WoolCW6$ orange
//>

//<Comments
//=
///	How it works
	Clones guess down, set player, tp (3+(x-1),x=guess #)
	Summons 4 temp armorstands + passenger cluster (5)
	Sets score with r=0,c=1 (6)
	Scoreboard operation (4)
	Adds score to red or white (28)
	Adds MMAR and MMAW with 0 (2)
	Detects if there's a repeated color (11)
	Detects if there's blocks missing (6)
	Sets sign (1)
	Sets tripwire (1)
	Sets redstone block (1)
	Sets sea lantern on win (1)
	Replaces sign on win (1)
	Kills all armorstands (1)
	Sets the score to 0 (1)

/// Scores legend (to colors)
	Score of 1: Red
	Score of 2: Lime
	Score of 3: Cyan
	Score of 4: Magenta
	Score of 5: Pink
	Score of 6: Orange

/// Objectives:
	MM (Selected player)
	MMpa (Player answer, recorded in armor stands. Tags: MM, MMA, MM1, MM2, MM3, MM4)
	MMpa2 (Red and white score, recorded in armor stands. Tags: MM, MMAW, MMAR)
	MMca (Recorded score of the correct answers in armor stands. Tags: MMCA, MMCA1, MMCA2, MMCA3, MMCA4)
	MMra (Used for detecting repeated colors. Tags: MM, MMRA, MMRA1, MMRA2, MMRA3, MMRA4)
	MMna (Used for detecting blocks that aren't the 6 wool colors. Tags: Uses the armor stands in MMpa)

/// Tags of AS / AEC:
AEC	Guess 1: MM, MMA, MMA#
	
AEC	Clutter / Repeating answer 1-6: MM, MMRA, MMRA#
AEC	Clutter / # of white: MM, MMRW, MMAW
AEC	Clutter / # of red: MM, MMRW, MMAR

AEC	Room Select - MM, MMRoomS, MMRoomS#
	
AS	Correct answer 1-4: MM, MMCA, MMCA#
	
AS	Room 1-12: MM, MMRoom, MMRoom#
	
///	How it will work:
	-Command block sets corresponding room armor stand to guess #
	-All Func_NorthCheck / Func_SouthCheck will be a testfor clock to test for armor stands >= 1
	-Areaeffectclouds spawn depending on gueess #
	-Sets all gm 1 and gm 2 in room to -MM score
	-execute from areaeffectcloud to multiply MM score by -1
	-Teleports pos MM score to areaeffectcloud
	-Regular stuff happens
=//
//>


//<General Scoreboard Start
FUNC {Func_GeneralScoreboardStart}:
	USE {ScOA}:
		CCU_NumberObjAdd
	
	ARRAY {GLOBAL ACTIVATE CCU.objectiveAdd(Obj_GeneralScoreboardStart) Obj_GeneralScoreboardStart}:
		MM dummy Mastermind
		MMpl dummy Mastermind Player List
		MMre dummy Mastermind (EC)
		MMrs dummy Mastermind Room Select
		MMrn dummy Mastermind Room Number
		MMgn dummy Mastermind Guess Number
		MMpa dummy Mastermind Predicted Answer
		MMca dummy Mastermind Correct Answer
		MMsd dummy Mastermind Sign Display
		MMra dummy Mastermind Repeated Answer
		MMna dummy Mastermind No Answer
		MMst stat.sneakTime Mastermind Sneak Timer
		MMgc dummy Mastermind Guess Check
		MMgl dummy Mastermind Guess Calc
		MMit dummy Mastermind Item
		MMti dummy Mastermind Timer
		MMti2 dummy Mastermind Timer 2
		MMop dummy Mastermind Options
		MMrg dummy Mastermind Right Guess
	
	CCU_ExecuteSpawnStand ScSS MM
	CCU_ExecuteSpawnStand ScOS sidebar.team.white MMre
	
	ARRAY {GLOBAL ACTIVATE CCU.teamAdd(Team_GeneralScoreboardStart) Team_GeneralScoreboardStart}:
		MMred
		OPT color red
		
		MMlime
		OPT color green
		
		MMcyan
		OPT color dark_aqua
		
		MMmagenta
		OPT color dark_purple
		
		MMpink
		OPT color light_purple
		
		MMorange
		OPT color gold
		
		MMwhite
		OPT color white
		
	ARRAY {ACTIVATE CCU.iterateNumbers ARGS}:
		-1
	
///	Summon Room Armor Stands
	CCU_ExecuteSpawnStand summon ArmorStand ~ ~ ~ {Tags:["MMEntity","MMStand"],Invulnerable:1,PersistenceRequired:1,Invisible:1,Marker:1,NoGravity:1}
	LOOP {1 12 + 1}:
		CCU_ExecuteSpawnStand summon ArmorStand $MMRoom|0|$ \
				{Tags:["MMEntity","MMRoom","MMRoom|0|"],Invulnerable:1,PersistenceRequired:1,Invisible:1,Marker:1,NoGravity:1}
		CCU_ExecuteSpawnStand @e[type=ArmorStand,MMRoom|0|] MMrs = -|0|
		
	CCU_ExecuteSpawnStand @e[type=ArmorStand,MMRoom] ScOP @e[c=1,r=1,type=ArmorStand,MMRoom] MMrn = @e[c=1,r=1,type=ArmorStand,MMRoom] MMrs
	CCU_ExecuteSpawnStand ScOP @e[type=ArmorStand,MMRoom] MMrn *= -1 Number
	CCU_ExecuteSpawnStand @e[type=ArmorStand,MMRoom] MMgc = 1
	CCU_ExecuteSpawnStand @e[type=ArmorStand,MMRoom] MMgl = 0
	CCU_ExecuteSpawnStand @e[type=ArmorStand,MMRoom] MMrg = 0
	CCU_ExecuteSpawnStand @e[type=ArmorStand,MMStand] MMop = 16
	
///	Summon Correct Answer Armor Stands
	LOOP {1 4 + 1} {0 3 + 1}:
		CCU_ExecuteSpawnStand summon ArmorStand $MMCACoords$[x - |1|, y, z] \
				{Tags:["MMEntity","MMCA","MMCA|0|"],DisabledSlots:2096896,NoGravity:1,NoBasePlate:1,ShowArms:1,Small:1,Invulnerable:1,PersistenceRequired:1,$CustomAS|0|$}
	
///	Spawnpoint clock
	Mastermind SPmm = 1
//>

//<General Scoreboard Clock
FUNC {Func_GeneralScoreboardClock}:
///	Player List
	@a[$SA$] MMpl + 0
	@a[$SA$,MMpl=0] MMre = -100
	J> NoPVP @a[$SA$,MMpl=0,team=!NoPVP]
	J> MMwhite @a[$SA$,MMpl=0,EC=0,team=!MMwhite]
	@a[$SA$,MMpl=0] MMpl = 1
	
///	Item Clock - Doesn't have a pickup delay of 0 when the player is crouching
	@e[$SA$,type=Item] MMit + 0 {Item:{id:"minecraft:wool"}}
	entitydata @e[$SA$,type=Item,MMit=0] {Item:{tag:{CanPlaceOn:["minecraft:glass"],HideFlags:127,display:{Lore:["Mastermind Wool"]}}},PickupDelay:1s}
	@a[$SA$,MMst>=1] entitydata @e[r=1,type=Item,MMit=0] {PickupDelay:40s}
	@e[$SA$,type=Item,MMit=0] MMit = 1 {Item:{tag:{CanPlaceOn:["minecraft:glass"]}}}
	@a[MMst>=1] MMst = 0
	
///	Room Number
	@a[$Hallway$,m=2] MM = 0
	@a[$Hallway$,m=1] MM = 0
	@e[type=ArmorStand,MMRoom] ScOP @a[$RoomSD$,m=2] MM = @e[c=1,r=1,type=ArmorStand,MMRoom] MMrn
	@e[type=ArmorStand,MMRoom] ScOP @a[$RoomSD$,m=1] MM = @e[c=1,r=1,type=ArmorStand,MMRoom] MMrn
	@a[$SA$,m=3] reset MM
//>

//<General Scoreboard End
FUNC {Func_GeneralScoreboardEnd}:
	CCU.objectiveRemove(Obj_GeneralScoreboardStart)
	CCU.teamRemove(Team_GeneralScoreboardStart)
	
	kill @e[type=ArmorStand,MMEntity]
	
///	Spawnpoint clock
	Mastermind SPmm = 0
//>


//<Guess Check Testfor Clock
FUNC {Func_GuessCheckTFClock}:
	testfor @e[type=ArmorStand,MMRoom|0|,MMgn>=1]
	CCU_SetblockTestfor
	
///	Tests for correct guess
	LOOP {1 5 + 1}:
		@e[type=ArmorStand,MMRoom|0;1|,MMgn=|0|,MMgc=|0|] MMgl = 1
		
		/*
		@e[type=ArmorStand,MMRoom|0;1|,MMgn>=1,MMgl=0,MMgc=|0|] 
			tellraw @a[$SR|0;1|$] $TextStart$
			{"text":"Warning: ","color":"red","bold":"true"},
			{"text":"Please use guess |0|.","color":"gold"}]}
		*/
///	@e[type=ArmorStand,MMRoom|0|,MMgl=0] tellraw @a[$SR|0|$] $TextStart${"text":"Please use guess ","color":"gold"},{"score":{"name":"@e[type=ArmorStand,MMRoom|0|,MMgl=0]","objective":"MMgc"},"color":"gold"}]}
	@e[type=ArmorStand,MMRoom|0|,MMgl=0] MMgn = 0
	
///	Summons AreaEffectClouds at wherever the button was pressed
	IF {|0| <= 6}:
		LOOP {1 5 + 1} {0 8 + 2}:
			@e[type=ArmorStand,MMRoom|0;1|,MMgn=|0|] summon AreaEffectCloud ~ ~2 ~CALC(|1| + 2) {Tags:["MMEntity","MMRoomN","MMRoomN|0|"],Duration:5,Rotation:[180f,0f]}
	ELSE:
		LOOP {1 5 + 1} {0 8 + 2}:
			@e[type=ArmorStand,MMRoom|0;1|,MMgn=|0|] summon AreaEffectCloud ~7 ~2 ~CALC(10 - |1|) {Tags:["MMEntity","MMRoomS","MMRoomS|0|"],Duration:5}
	
	ScOP @a[$SR|0|$] MMrs = @e[type=ArmorStand,MMRoom|0|,MMgn>=1] MMrs
	
	IF {|0| <= 6}:
		@e[$SR|0|$,type=AreaEffectCloud,MMRoomN] ScOP @p[r=5,MMrs=-|0|] MMrs *= -1 Number
		tp @a[$SR|0|$,MMrs=|0|] @e[$SR|0|$,type=AreaEffectCloud,MMRoomN]
	ELSE:
		@e[$SR|0|$,type=AreaEffectCloud,MMRoomS] ScOP @p[r=5,MMrs=-|0|] MMrs *= -1 Number
		tp @a[$SR|0|$,MMrs=|0|] @e[$SR|0|$,type=AreaEffectCloud,MMRoomS]
		
	IF {|0| <= 6}:
		DEF GLOBAL $IsNeg$
	ELSE:
		DEF GLOBAL $IsNeg$ -
	
///	Summons all armor stands (temporary)
	@a[$SR|0|$,MMrs=|0|] summon AreaEffectCloud ~$IsNeg$2 ~ ~ {Tags:["MMEntity","MMA","MMA1"],Duration:5}
	@a[$SR|0|$,MMrs=|0|] summon AreaEffectCloud ~$IsNeg$3 ~ ~ {Tags:["MMEntity","MMA","MMA2"],Duration:5}
	@a[$SR|0|$,MMrs=|0|] summon AreaEffectCloud ~$IsNeg$4 ~ ~ {Tags:["MMEntity","MMA","MMA3"],Duration:5}
	@a[$SR|0|$,MMrs=|0|] summon AreaEffectCloud ~$IsNeg$5 ~ ~ {Tags:["MMEntity","MMA","MMA4"],Duration:5}
	@a[$SR|0|$,MMrs=|0|] summon Item ~$IsNeg$7 ~ ~ $AreaEffectCloudBeg$

/// Sets a score for the MMA armor stands corresponding to the color
	LOOP {1 6 + 1}:
		@a[$SR|0;1|$,MMrs=|0;1|] @e[dx=$IsNeg$5,dy=1,MMA] wool $WoolC|0|$ ~ ~-1 ~ @e[MMA,r=0,c=1] MMpa = |0|
	
/// Sets the correct answer to the MMA armor stands
	LOOP {1 4 + 1}:
		@a[$SR|0;1|$,MMrs=|0;1|] ScOP @e[dx=$IsNeg$5,dy=1,MMA|0|] MMca = @e[MMCA|0|] MMca
	
/// Counts the number of white and red
	LOOP {1 6 + 1}:
		@a[$SR|0;1|$,MMrs=|0;1|] @e[dx=$IsNeg$5,dy=1,MMca=|0|] @e[r=3,rm=1,MMpa=|0|] @e[dx=$IsNeg$6,dy=1,MMAW] MMsd + 1
		@a[$SR|0;1|$,MMrs=|0;1|] @e[dx=$IsNeg$5,dy=1,MMA,MMpa=|0|,MMca=|0|] @e[dx=$IsNeg$6,dy=1,MMAR] MMsd + 1
	
/// Adds everything to 0 (to display 0 red/white, and to detect if there isn't a wool block placed)
	@e[MMRW] MMsd + 0
	@e[MMA] MMpa + 0
	
/// Detects any repeating blocks
	IF {|0| <= 6}:
		LOOP {1 6 + 1}:
			@a[$SR|0;1|$,MMrs=|0;1|] @e[dx=5,dy=1,MMA,MMpa=|0|] @e[dx=5,dy=1,MMRA|0|] MMra + 1
		@a[$SR|0|$,MMrs=|0|] @e[dx=7,dy=1,MMRA,MMra>=2] @a[dx=-7,dy=1,c=1,MMrs=|0|] MMra = 1
	ELSE:
		LOOP {1 6 + 1}:
			@a[$SR|0;1|$,MMrs=|0;1|] @e[dx=-5,dy=1,MMA,MMpa=|0|] @e[dx=-5,dy=1,MMRA|0|] MMra + 1
		@a[$SR|0|$,MMrs=|0|] @e[dx=-7,dy=1,MMRA,MMra>=2] @a[dx=7,dy=1,c=1,MMrs=|0|] MMra = 1
		
	@a[$SR|0|$,MMrs=|0|] MMra + 0
	
/// Detects any non-wool blocks
	IF {|0| <= 6}:
		@a[$SR|0|$,MMrs=|0|] @e[dx=5,dy=1,MMA,MMpa=0] @a[MMrs=|0|,dx=-5,dy=1,c=1] MMna = 1
	ELSE:
		@a[$SR|0|$,MMrs=|0|] @e[dx=-5,dy=1,MMA,MMpa=0] @a[MMrs=|0|,dx=5,dy=1,c=1] MMna = 1
	@a[$SR|0|$,MMrs=|0|] MMna + 0

/// Sets the sign if the guess is filled and has no repeating blocks
//=
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] setblock ~7 ~ ~ standing_sign 2 replace $SignRegular$
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=7,dy=1,MMAR,MMsd=4] setblock ~ ~ ~ standing_sign 2 replace $SignWin$
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=7,dy=1,MMAR,MMsd>=1<=3] @e[r=1,c=1,MMAW,MMsd=0] setblock ~ ~ ~ standing_sign 2 replace $SignRed$
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=7,dy=1,MMAW,MMsd>=1<=4] @e[r=1,c=1,MMAR,MMsd=0] setblock ~ ~ ~ standing_sign 2 replace $SignWhite$
=//

//=
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] setblock ~-7 ~ ~ standing_sign 10 replace $SignRegular$
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=-7,dy=1,MMAR,MMsd=4] setblock ~ ~ ~ standing_sign 10 replace $SignWin$
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=-7,dy=1,MMAR,MMsd>=1<=3] @e[r=1,c=1,MMAW,MMsd=0] setblock ~ ~ ~ standing_sign 10 replace $SignRed$
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=-7,dy=1,MMAW,MMsd>=1<=4] @e[r=1,c=1,MMAR,MMsd=0] setblock ~ ~ ~ standing_sign 10 replace $SignWhite$
=//
	IF {|0| <= 6}:
		@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] setblock ~7 ~ ~ standing_sign 2 replace $SignFix$
	ELSE:
		@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] setblock ~-7 ~ ~ standing_sign 10 replace $SignFix$
	
///	displays red
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=$IsNeg$7,dy=1,MMAR,MMsd=1] blockdata ~ ~ ~ \
			{Text2:"{\"text\":\"\",\"extra\":[{\"text\":\"1\",\"bold\":\"true\"},{\"text\":\" red\"}]}"}
	
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=$IsNeg$7,dy=1,MMAR,MMsd=2] blockdata ~ ~ ~ \
			{Text2:"{\"text\":\"\",\"extra\":[{\"text\":\"2\",\"bold\":\"true\"},{\"text\":\" red\"}]}"}
	
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=$IsNeg$7,dy=1,MMAR,MMsd=3] blockdata ~ ~ ~ \
			{Text2:"{\"text\":\"\",\"extra\":[{\"text\":\"3\",\"bold\":\"true\"},{\"text\":\" red\"}]}"}
	
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=$IsNeg$7,dy=1,MMAR,MMsd=4] blockdata ~ ~ ~ \
			{Text2:"{\"text\":\"4 red!\",\"bold\":\"true\"}",Text3:"{\"text\":\"Shhh\",\"bold\":\"true\"}"}
	
///	displays white
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=$IsNeg$7,dy=1,MMAR,MMsd>=1<=3] @e[r=1,c=1,MMAW,MMsd=0] blockdata ~ ~ ~ \
			{Text3:"{\"text\":\"-=-\"}",Text4:"{\"text\":\"\"}"}
	
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=$IsNeg$7,dy=1,MMAR,MMsd>=1<=3] @e[r=1,c=1,MMAW,MMsd=1] blockdata ~ ~ ~ \
			{Text3:"{\"text\":\"\",\"extra\":[{\"text\":\"1\",\"bold\":\"true\"},{\"text\":\" white\"}]}"}
	
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=$IsNeg$7,dy=1,MMAR,MMsd>=1<=3] @e[r=1,c=1,MMAW,MMsd=2] blockdata ~ ~ ~ \
			{Text3:"{\"text\":\"\",\"extra\":[{\"text\":\"2\",\"bold\":\"true\"},{\"text\":\" white\"}]}"}
	
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=$IsNeg$7,dy=1,MMAR,MMsd>=1<=3] @e[r=1,c=1,MMAW,MMsd=3] blockdata ~ ~ ~ \
			{Text3:"{\"text\":\"\",\"extra\":[{\"text\":\"3\",\"bold\":\"true\"},{\"text\":\" white\"}]}"}
	
///	displays white on 3rd line
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=$IsNeg$7,dy=1,MMAR,MMsd=0] @e[r=1,c=1,MMAW,MMsd=1] blockdata ~ ~ ~ \
			{Text2:"{\"text\":\"\",\"extra\":[{\"text\":\"1\",\"bold\":\"true\"},{\"text\":\" white\"}]}",Text3:"{\"text\":\"-=-\"}",Text4:"{\"text\":\"\"}"}
	
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=$IsNeg$7,dy=1,MMAR,MMsd=0] @e[r=1,c=1,MMAW,MMsd=2] blockdata ~ ~ ~ \
			{Text2:"{\"text\":\"\",\"extra\":[{\"text\":\"2\",\"bold\":\"true\"},{\"text\":\" white\"}]}",Text3:"{\"text\":\"-=-\"}",Text4:"{\"text\":\"\"}"}
	
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=$IsNeg$7,dy=1,MMAR,MMsd=0] @e[r=1,c=1,MMAW,MMsd=3] blockdata ~ ~ ~ \
			{Text2:"{\"text\":\"\",\"extra\":[{\"text\":\"3\",\"bold\":\"true\"},{\"text\":\" white\"}]}",Text3:"{\"text\":\"-=-\"}",Text4:"{\"text\":\"\"}"}
	
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=$IsNeg$7,dy=1,MMAR,MMsd=0] @e[r=1,c=1,MMAW,MMsd=4] blockdata ~ ~ ~ \
			{Text2:"{\"text\":\"\",\"extra\":[{\"text\":\"4\",\"bold\":\"true\"},{\"text\":\" white\"}]}",Text3:"{\"text\":\"-=-\"}",Text4:"{\"text\":\"\"}"}
	
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] setblock ~ ~ ~ tripwire 0 replace stone_button
	
	IF {|0| <= 6}:
		@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=7,dy=1,MMAR,MMsd=4] fill ~-7 ~4 ~ ~-7 ~6 ~ sea_lantern 0 replace redstone_lamp
		@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=7,dy=1,MMAR,MMsd=4] @e[type=ArmorStand,MMRoom|0|,MMgn>=1,MMrg=0] MMrg = 2
	ELSE:
		@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=-7,dy=1,MMAR,MMsd=4] fill ~7 ~4 ~ ~7 ~6 ~ sea_lantern 0 replace redstone_lamp
		@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=-7,dy=1,MMAR,MMsd=4] @e[type=ArmorStand,MMRoom|0|,MMgn>=1,MMrg=0] MMrg = 2
	
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[type=ArmorStand,MMRoom|0|,MMgn>=1,MMrg=0] MMrg = 1
	//=
		MMrg 0 --> 1 --> 0
		MMrg 0 --> 2 --> 3
		
		-1 is guess on first try
		-2 is guess on second try
		-3 is guess on third try
		-4 is guess on fourth try
		-5 is guess on fifth try
		
		-100 normal
		-102 is when first guess is successfully done
		-103 is when second guess is successfully done
		-104 is when third guess is successfully done
		-105 is when fourth guess is successfully done
		-106 is when fifth guess is successfully done
	
	
	=//
	@e[type=ArmorStand,MMRoom|0|,MMrg>=1<=2] ScOP @e[c=1,r=1,type=ArmorStand,MMRoom|0|,MMrg>=1<=2] MMpl = @e[c=1,r=1,type=ArmorStand,MMRoom|0|,MMrg>=1<=2] MMgn
	ScOP @e[type=ArmorStand,MMRoom|0|,MMrg>=1<=2] MMpl *= -1 Number
	@e[type=ArmorStand,MMRoom|0|,MMrg=1] MMpl - 100
	@e[type=ArmorStand,MMRoom|0|,MMrg>=1<=2] ScOP @a[$SR|0|$] MMre = @e[c=1,r=1,type=ArmorStand,MMRoom|0|,MMrg>=1<=2] MMpl
	@e[type=ArmorStand,MMRoom|0|,MMrg=1] MMrg = 0
	@e[type=ArmorStand,MMRoom|0|,MMrg=2] MMrg = 3
	
	IF {|0| <= 6}:
		@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] clone ~2 ~-1 ~ ~5 ~-1 ~ ~2 ~-3 ~
	ELSE:
		@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] clone ~-2 ~-1 ~ ~-5 ~-1 ~ ~-5 ~-3 ~
	
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] setblock ~ ~5 ~ redstone_block 0
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[type=ArmorStand,MMRoom|0|,MMgl=1] MMgc + 1
	@e[type=ArmorStand,MMRoom|0|] MMgl = 0
	
/// Shows the warning
	/*
	tellraw @a[$SR|0|$,MMrs=|0|,MMra=1] $TextStart$
		{"text":"Warning: ","color":"red","bold":"true"},
		{"text":"There are repeated colors. Please redo your guess.","color":"gold"}]}
	-/-
	tellraw @a[$SR|0|$,MMrs=|0|,MMna=1] $TextStart$
		{"text":"Warning: ","color":"red","bold":"true"},
		{"text":"There is at least one missing block. Please redo your guess.","color":"gold"}]}
	*/
	
/// Kills all temporary armor stands, sets the selected player's scores to 0
	kill @e[$SR|0|$,type=AreaEffectCloud,MMEntity]
	@a[$SR|0|$,MMrs=|0|] MMna = 0
	@a[$SR|0|$,MMrs=|0|] MMra = 0
	@a[$SR|0|$,MMrs=|0|] MMrs = 0
	@e[type=ArmorStand,MMRoom|0|,MMgn>=1] MMgn = 0
//>


//<General Options Testfor Clock
FUNC {Func_GeneralOptionsTFClock}:
	testfor @e[type=ArmorStand,MMStand,MMop>=1]
	CCU_SetblockTestfor
	
///	2048 = Stop the game
///	3168
	@e[type=ArmorStand,MMStand,MMop>=2048] CCU_Activate(Grp_MainEnd)
	@e[type=ArmorStand,MMStand,MMop>=2048] MMop - 2048
	
///	1024 = Open doors
	@e[type=ArmorStand,MMStand,MMop>=1024] fill $FillDoorsN$ air 0 replace bedrock 0
	@e[type=ArmorStand,MMStand,MMop>=1024] fill $FillDoorsS$ air 0 replace bedrock 0
	@e[type=ArmorStand,MMStand,MMop>=1024] MMop - 1024
	
///	512 = Close doors
	@e[type=ArmorStand,MMStand,MMop>=512] fill $FillDoorsN$ bedrock 0 replace air 0
	@e[type=ArmorStand,MMStand,MMop>=512] fill $FillDoorsS$ bedrock 0 replace air 0
	@e[type=ArmorStand,MMStand,MMop>=512] MMop - 512
	
///	256 = Set buttons
	@e[type=ArmorStand,MMStand,MMop>=256] fill $FillButtonsN$ stone_button 5 replace tripwire
	@e[type=ArmorStand,MMStand,MMop>=256] fill $FillButtonsS$ stone_button 5 replace tripwire
	@e[type=ArmorStand,MMStand,MMop>=256] MMop - 256
	
///	128 = Give Wool
	@e[type=ArmorStand,MMStand,MMop>=128] replaceitem entity @a[$SA$,m=2] slot.hotbar.6 shears 1 0 {CanDestroy:["minecraft:wool"],Unbreakable:1,ench:[{id:32,lvl:9001}],display:{Lore:["Mastermind Shears"]}}
	@e[type=ArmorStand,MMStand,MMop>=128] replaceitem entity @a[$SA$,m=2] slot.hotbar.8 golden_apple 64 0
	@e[type=ArmorStand,MMStand,MMop>=128] replaceitem entity @a[$SA$,m=2] slot.hotbar.0 wool 64 1 {CanPlaceOn:["minecraft:glass"],display:{Lore:["Mastermind Wool"]},HideFlags:127}
	@e[type=ArmorStand,MMStand,MMop>=128] replaceitem entity @a[$SA$,m=2] slot.hotbar.1 wool 64 2 {CanPlaceOn:["minecraft:glass"],display:{Lore:["Mastermind Wool"]},HideFlags:127}
	@e[type=ArmorStand,MMStand,MMop>=128] replaceitem entity @a[$SA$,m=2] slot.hotbar.2 wool 64 6 {CanPlaceOn:["minecraft:glass"],display:{Lore:["Mastermind Wool"]},HideFlags:127}
	@e[type=ArmorStand,MMStand,MMop>=128] replaceitem entity @a[$SA$,m=2] slot.hotbar.3 wool 64 9 {CanPlaceOn:["minecraft:glass"],display:{Lore:["Mastermind Wool"]},HideFlags:127}
	@e[type=ArmorStand,MMStand,MMop>=128] replaceitem entity @a[$SA$,m=2] slot.hotbar.4 wool 64 5 {CanPlaceOn:["minecraft:glass"],display:{Lore:["Mastermind Wool"]},HideFlags:127}
	@e[type=ArmorStand,MMStand,MMop>=128] replaceitem entity @a[$SA$,m=2] slot.hotbar.5 wool 64 14 {CanPlaceOn:["minecraft:glass"],display:{Lore:["Mastermind Wool"]},HideFlags:127}
	
	@e[type=ArmorStand,MMStand,MMop>=128] replaceitem entity @a[$SA$,m=1] slot.hotbar.6 sign 1 0 {ench:[{id:0,lvl:1}],HideFlags:127,display:{Name:"OP sign"}}
	@e[type=ArmorStand,MMStand,MMop>=128] replaceitem entity @a[$SA$,m=1] slot.hotbar.0 wool 1 1 {CanPlaceOn:["minecraft:glass"],display:{Lore:["Mastermind Wool"]},HideFlags:127}
	@e[type=ArmorStand,MMStand,MMop>=128] replaceitem entity @a[$SA$,m=1] slot.hotbar.1 wool 1 2 {CanPlaceOn:["minecraft:glass"],display:{Lore:["Mastermind Wool"]},HideFlags:127}
	@e[type=ArmorStand,MMStand,MMop>=128] replaceitem entity @a[$SA$,m=1] slot.hotbar.2 wool 1 6 {CanPlaceOn:["minecraft:glass"],display:{Lore:["Mastermind Wool"]},HideFlags:127}
	@e[type=ArmorStand,MMStand,MMop>=128] replaceitem entity @a[$SA$,m=1] slot.hotbar.3 wool 1 9 {CanPlaceOn:["minecraft:glass"],display:{Lore:["Mastermind Wool"]},HideFlags:127}
	@e[type=ArmorStand,MMStand,MMop>=128] replaceitem entity @a[$SA$,m=1] slot.hotbar.4 wool 1 5 {CanPlaceOn:["minecraft:glass"],display:{Lore:["Mastermind Wool"]},HideFlags:127}
	@e[type=ArmorStand,MMStand,MMop>=128] replaceitem entity @a[$SA$,m=1] slot.hotbar.5 wool 1 14 {CanPlaceOn:["minecraft:glass"],display:{Lore:["Mastermind Wool"]},HideFlags:127}
	@e[type=ArmorStand,MMStand,MMop>=128] @a[$SA$,EC=0] SPbk = $SPid$
	@e[type=ArmorStand,MMStand,MMop>=128] MMop - 128
	
///	64 = Clear Inventory
	@e[type=ArmorStand,MMStand,MMop>=64] clear @a[$SA$]
	@e[type=ArmorStand,MMStand,MMop>=64] effect @a[$SA$] clear
	@e[type=ArmorStand,MMStand,MMop>=64] @a[$SA$,EC=0] SPbk = $SPid$
	@e[type=ArmorStand,MMStand,MMop>=64] MMop - 64
	
///	32 = Clear Lanes
	@e[type=ArmorStand,MMStand,MMop>=32] fill $FillStructure$ redstone_block 0 replace planks 0
	@e[type=ArmorStand,MMStand,MMop>=32] fill $FillStructure$ planks 0 replace redstone_block 0
	@e[type=ArmorStand,MMStand,MMop>=32] @e[type=ArmorStand,MMRoom] MMgc = 1
	@e[type=ArmorStand,MMStand,MMop>=32] @e[type=ArmorStand,MMRoom] MMrg = 0
	@e[type=ArmorStand,MMStand,MMop>=32] @e[type=ArmorStand,MMRoom] reset MMpl
	@e[type=ArmorStand,MMStand,MMop>=32] * reset MMre
	@e[type=ArmorStand,MMStand,MMop>=32] @a[$SA$] MMre = -100
	@e[type=ArmorStand,MMStand,MMop>=32] MMop - 32
	
///	16 = Set answer
	LOOP {1 6 + 1}:
		@e[type=ArmorStand,MMStand,MMop>=16] @e[type=ArmorStand,MMCA] wool $WoolC|0|$ ~ ~-3 ~ @e[MMCA,r=0,c=1] MMca = |0|
	
	@e[type=ArmorStand,MMStand,MMop>=16] setblock $StructureSave$ redstone_block 0
	@e[type=ArmorStand,MMStand,MMop>=16] MMop - 16
	
///	8 = Randomizer
	@e[type=ArmorStand,MMStand,MMop>=8] summon Item ~ ~ ~ $AreaEffectCloudRand$

	LOOP {1 6 + 1}:
		@e[type=ArmorStand,MMStand,MMop>=8] @e[type=AreaEffectCloud,MMRAN|0|,c=1] MMca = |0|
	
	LOOP {1 3 + 1}:
		@e[type=ArmorStand,MMStand,MMop>=8] @r[type=AreaEffectCloud,MMRAN] + MMRCA
		@e[type=ArmorStand,MMStand,MMop>=8] ScOP @e[type=ArmorStand,MMCA|0|] MMca = @e[type=AreaEffectCloud,MMRCA] MMca
		@e[type=ArmorStand,MMStand,MMop>=8] kill @e[type=AreaEffectCloud,MMRCA]
	
	@e[type=ArmorStand,MMStand,MMop>=8] @r[type=AreaEffectCloud,MMRAN] + MMRCA
	@e[type=ArmorStand,MMStand,MMop>=8] ScOP @e[type=ArmorStand,MMCA4] MMca = @e[type=AreaEffectCloud,MMRCA] MMca
	@e[type=ArmorStand,MMStand,MMop>=8] kill @e[type=AreaEffectCloud,MMRAN]
	
	LOOP {1 6 + 1}:
		@e[type=ArmorStand,MMStand,MMop>=8] @e[type=ArmorStand,MMCA,MMca=|0|] setblock ~ ~-3 ~ wool $WoolC|0|$
	
	@e[type=ArmorStand,MMStand,MMop>=8] setblock $StructureSave$ redstone_block 0
	@e[type=ArmorStand,MMStand,MMop>=8] MMop - 8
	
///	4 = Refresh rooms
	@e[type=ArmorStand,MMStand,MMop>=4] MMti = 0
	@e[type=ArmorStand,MMStand,MMop>=4] MMop - 4
	
///	2 = Auto broadcasts
	LOOP {1 6 + 1}:
		@e[type=ArmorStand,MMStand,MMop>=2] entitydata @e[type=ArmorStand,MMCA,MMca=|0|] {CustomName:"$WoolCW|0|$",Team:"MM$WoolCW|0|$"}
	
	@e[type=ArmorStand,MMStand,MMop>=2] tellraw @a $FakeBroadcast$
	@e[type=ArmorStand,MMStand,MMop>=2] MMop - 2
	
///	1 = Clones answer
	@e[type=ArmorStand,MMStand,MMop>=1] fill $FillStructure$ redstone_block 0 replace planks 1
	@e[type=ArmorStand,MMStand,MMop>=1] fill $FillStructure$ planks 1 replace redstone_block 0
	@e[type=ArmorStand,MMStand,MMop>=1] MMop = 0
//>


//<Refresh Rooms Testfor Clock
FUNC {Func_RefreshRoomsTFClock}:
	testfor @e[type=ArmorStand,MMStand,MMti>=0]
	CCU_SetblockTestfor
	
	@e[type=ArmorStand,MMStand,MMti>=0] MMti + 1
	@e[type=ArmorStand,MMStand,MMti=1] MMop = 40
	@e[type=ArmorStand,MMStand,MMti=5] MMop = 256
	@e[type=ArmorStand,MMStand,MMti>=5] MMti = -1
//>

//<Groups
GROUP {Grp_MainStart}:
	CCU.spawnStandStart()
	CCU_Deactivate(GSELF)
	Func_GeneralScoreboardStart()
	CCU_Activate(Grp_MainClock)
	CCU_Activate(Grp_GeneralOptionsTFClock)
	CCU_Activate(Grp_RefreshRoomsTFClock)
	LOOP {1 $MaxNum$ + 1}:
		CCU_Activate(Grp_GuessCheckTFClock|0|)
	CCU_FinalSpawnStand
	
GROUP {Grp_MainClock}:
	Func_GeneralScoreboardClock()

GROUP {Grp_GeneralOptionsTFClock}:
	Func_GeneralOptionsTFClock()

GROUP {Grp_RefreshRoomsTFClock}:
	Func_RefreshRoomsTFClock()


LOOP {1 $MaxNum$ + 1}:
	GROUP {CLOCK Grp_GuessCheckTFClock|0|}:
		Func_GuessCheckTFClock(|0|)

GROUP {Grp_MainEnd}:
	CCU.spawnStandEnd()
	CCU_Deactivate(GSELF)
	Func_ClearInventoryPulse()
	Func_GeneralScoreboardEnd()
	CCU_Deactivate(Grp_MainClock)
	CCU_Deactivate(Grp_GeneralOptionsTFClock)
	CCU_Deactivate(Grp_RefreshRoomsTFClock)
	LOOP {1 $MaxNum$ + 1}:
		CCU_Deactivate(Grp_GuessCheckTFClock|0|)

CCU.cmdSave()
//>























