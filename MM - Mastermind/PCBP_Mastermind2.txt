//#-p stonebrick 0 -print
///	63 15 79

//<Defines: Regular
DEFINE $s$ minecraft:
DEFINE $ObjA$ scoreboard objectives add
DEFINE $ObjR$ scoreboard objectives remove
DEFINE $TeamA$ scoreboard teams add
DEFINE $TeamO$ scoreboard teams option
DEFINE $TeamR$ scoreboard teams remove
DEFINE $Style$ 0 16 16
DEFINE $SA$ x=45,y=3,z=22,dx=64,dy=30,dz=65
DEFINE $TextStart$ {"text":"","extra":[{"text":"[","color":"gray"},{"text":"MM","color":"gold","bold":"true","hoverEvent":{"action":"show_text","value":{"text":"MasterMind","color":"gold"}}},{"text":"]","color":"gray"},{"text":": "},

DEFINE Deactivate fill |0| stonebrick 0
DEFINE Activate fill |0| redstone_block 0
DEFINE ScTG scoreboard players tag
DEFINE ScOP scoreboard players operation
DEFINE RevST scoreboard players tag @a[|0|] remove |0|
DEFINE SetblockTestfor setblock ~ ~ ~ repeating_command_block 11 replace {TrackOutput:0b,auto:1b}
DEFINE NumberAdd |0| Number = |0|
DEFINE NumberObjAdd Number dummy
//>

//<Defines: Large
DEFINE $SignRegular$ ;
	{;
	Text1:"ESCAPE({"text":"-=-"})",;
	Text2:"ESCAPE({"text":"","extra":[{"score":{"name":"@e[type=ArmorStand,MMAR,r=1]","objective":"MMpa2"}},{"text":" red"}]})",;
	Text3:"ESCAPE({"text":"","extra":[{"score":{"name":"@e[type=ArmorStand,MMAW,r=1]","objective":"MMpa2"}},{"text":" white"}]})",;
	Text4:"ESCAPE({"text":"-=-"})";
	}
	
DEFINE $SignWin$ ;
	{;
	Text1:"ESCAPE({"text":"--=--"})",;
	Text2:"ESCAPE({"text":"4 red","bold":"true"})",;
	Text3:"ESCAPE({"text":"Shhhh","bold":"true"})",;
	Text4:"ESCAPE({"text":"--=--"})";
	}
	
DEFINE $SignRed$ ;
	{;
	Text1:"ESCAPE({"text":"-=-"})",;
	Text2:"ESCAPE({"text":"","extra":[{"score":{"name":"@e[type=ArmorStand,MMAR,r=1]","objective":"MMpa2"}},{"text":" red"}]})",;
	Text3:"ESCAPE({"text":"-=-"})",;
	Text4:"ESCAPE({"text":""})";
	}

DEFINE $SignWhite$ ;
	{;
	Text1:"ESCAPE({"text":"-=-"})",;
	Text2:"ESCAPE({"text":"","extra":[{"score":{"name":"@e[type=ArmorStand,MMAW,r=1]","objective":"MMpa2"}},{"text":" white"}]})",;
	Text3:"ESCAPE({"text":"-=-"})",;
	Text4:"ESCAPE({"text":""})";
	}

DEFINE $SignFix$ ;
	{;
	Text1:"ESCAPE({"text":"-=-"})",;
	Text2:"ESCAPE({"text":""})",;
	Text3:"ESCAPE({"text":""})",;
	Text4:"ESCAPE({"text":"-=-"})",;
	}

DEFINE $AreaEffectCloudBeg$ ;
	{Age:6000s,Item:{id:"minecraft:stone",Count:1b},Passengers:[;
		{id:AreaEffectCloud,Tags:["MM2","MM2RA","MM2RA1"],Duration:5},;
		{id:AreaEffectCloud,Tags:["MM2","MM2RA","MM2RA2"],Duration:5},;
		{id:AreaEffectCloud,Tags:["MM2","MM2RA","MM2RA3"],Duration:5},;
		{id:AreaEffectCloud,Tags:["MM2","MM2RA","MM2RA4"],Duration:5},;
		{id:AreaEffectCloud,Tags:["MM2","MM2RA","MM2RA5"],Duration:5},;
		{id:AreaEffectCloud,Tags:["MM2","MM2RA","MM2RA6"],Duration:5},;
		{id:AreaEffectCloud,Tags:["MM2","MM2RW","MM2AW"],Duration:5},;
		{id:AreaEffectCloud,Tags:["MM2","MM2RW","MM2AR"],Duration:5};
	]}
	
DEFINE $AreaEffectCloudRand$ ;
	{Age:6000s,Item:{id:"minecraft:stone",Count:1b},Passengers:[;
		{id:AreaEffectCloud,Duration:5,Tags:["MM2RAN","MM2RAN1","MM2RANred"]},;
		{id:AreaEffectCloud,Duration:5,Tags:["MM2RAN","MM2RAN2","MM2RANlime"]},;
		{id:AreaEffectCloud,Duration:5,Tags:["MM2RAN","MM2RAN3","MM2RANcyan"]},;
		{id:AreaEffectCloud,Duration:5,Tags:["MM2RAN","MM2RAN4","MM2RANmagenta"]},;
		{id:AreaEffectCloud,Duration:5,Tags:["MM2RAN","MM2RAN5","MM2RANpink"]},;
		{id:AreaEffectCloud,Duration:5,Tags:["MM2RAN","MM2RAN6","MM2RANorange"]};
	]}
	
DEFINE $FakeBroadcast$ ;
	{"text":"","color":"green","extra":[;
	{"text":"[","color":"gray"},;
	{"text":"MM2","color":"gold","bold":"true","hoverEvent":{"action":"show_text","value":{"text":"MasterMind","color":"gold"}}},;
	{"text":"]","color":"gray"},;
	{"text":": ","color":"white"},;
	{"text":"The correct combo is "},;
	{"selector":"@e[type=ArmorStand,MMCA1]"},;
	{"text":", "},;
	{"selector":"@e[type=ArmorStand,MMCA2]"},;
	{"text":", "},;
	{"selector":"@e[type=ArmorStand,MMCA3]"},;
	{"text":" and "},;
	{"selector":"@e[type=ArmorStand,MMCA4]"},;
	{"text":"!"};
	]}



DEFINE $CustomAS1$ ;
Rotation:[180f,0f],;
Pose:{;
	Body:[0f,0f,0f],;
	Head:[0f,0f,0f],;
	LeftArm:[0f,0f,0f],;
	RightArm:[0f,0f,0f],;
	LeftLeg:[-90f,0f,0f],;
	RightLeg:[-90f,0f,0f];
},ArmorItems:[;
	{id:leather_boots,Count:1b,tag:{display:{color:1644825}}},;
	{id:leather_leggings,Count:1b,tag:{display:{color:1644825}}},;
	{id:leather_chestplate,Count:1b,tag:{display:{color:1644825}}},;
	{id:skull,Count:1b,Damage:3s,tag:{SkullOwner:Daa_}};
],HandItems:[{},{}]

DEFINE $CustomAS2$ ;
Rotation:[180f,0f],;
Pose:{;
	Body:[0f,0f,0f],;
	Head:[0f,0f,0f],;
	LeftArm:[0f,0f,-20f],;
	RightArm:[-50f,0f,20f],;
	LeftLeg:[-90f,0f,0f],;
	RightLeg:[-90f,0f,0f];
},ArmorItems:[;
	{id:leather_boots,Count:1b,tag:{display:{color:3361970}}},;
	{id:leather_leggings,Count:1b,tag:{display:{color:3361970}}},;
	{id:leather_chestplate,Count:1b,tag:{display:{color:3361970}}},;
	{id:skull,Count:1b,Damage:3s,tag:{SkullOwner:french_man}};
],HandItems:[{id:"minecraft:command_block",Count:1b},{}]

DEFINE $CustomAS3$ ;
Rotation:[180f,0f],;
Pose:{;
	Body:[0f,0f,0f],;
	Head:[0f,0f,0f],;
	LeftArm:[0f,0f,0f],;
	RightArm:[0f,0f,f],;
	LeftLeg:[-90f,0f,0f],;
	RightLeg:[-90f,0f,0f];
},ArmorItems:[;
	{id:leather_boots,Count:1b,tag:{display:{color:15892389}}},;
	{id:leather_leggings,Count:1b,tag:{display:{color:15892389}}},;
	{id:leather_chestplate,Count:1b,tag:{display:{color:15892389}}},;
	{id:skull,Count:1b,Damage:3s,tag:{SkullOwner:jamboree_lee}};
],HandItems:[{},{}]

DEFINE $CustomAS4$ ;
Rotation:[180f,0f],;
Pose:{;
	Body:[0f,0f,0f],;
	Head:[0f,0f,0f],;
	LeftArm:[0f,0f,0f],;
	RightArm:[0f,0f,0f],;
	LeftLeg:[-90f,0f,0f],;
	RightLeg:[-90f,0f,0f];
},ArmorItems:[;
	{id:leather_boots,Count:1b,tag:{display:{color:11685080}}},;
	{id:leather_leggings,Count:1b,tag:{display:{color:11685080}}},;
	{id:leather_chestplate,Count:1b,tag:{display:{color:11685080}}},;
	{id:skull,Count:1b,Damage:3s,tag:{SkullOwner:CynRyn}};
],HandItems:[{},{}]
//>

//<Defines: Room Coords
///	Room coordinates: North +11
DEFINE $RoomNX$ 46
DEFINE $RoomNY$ 4
DEFINE $RoomNZ$ 24

///	Room coordinates: South +11
DEFINE $RoomSX$ 46
DEFINE $RoomSY$ 4
DEFINE $RoomSZ$ 47

DEFINE $RoomSD$ dx=7,dy=4,dz=12

///	Select rooms 
///	North - 1 to 6
DEFINE $SR1$ x=PARSE(ADD(PARSE($RoomNX$);0)),y=PARSE($RoomNY$),z=PARSE($RoomNZ$),PARSE($RoomSD$)
DEFINE $SR2$ x=PARSE(ADD(PARSE($RoomNX$);11)),y=PARSE($RoomNY$),z=PARSE($RoomNZ$),PARSE($RoomSD$)
DEFINE $SR3$ x=PARSE(ADD(PARSE($RoomNX$);22)),y=PARSE($RoomNY$),z=PARSE($RoomNZ$),PARSE($RoomSD$)
DEFINE $SR4$ x=PARSE(ADD(PARSE($RoomNX$);33)),y=PARSE($RoomNY$),z=PARSE($RoomNZ$),PARSE($RoomSD$)
DEFINE $SR5$ x=PARSE(ADD(PARSE($RoomNX$);44)),y=PARSE($RoomNY$),z=PARSE($RoomNZ$),PARSE($RoomSD$)
DEFINE $SR6$ x=PARSE(ADD(PARSE($RoomNX$);55)),y=PARSE($RoomNY$),z=PARSE($RoomNZ$),PARSE($RoomSD$)

///	South - 7 to 12
DEFINE $SR7$ x=PARSE(ADD(PARSE($RoomNX$);0)),y=PARSE($RoomSY$),z=PARSE($RoomSZ$),PARSE($RoomSD$)
DEFINE $SR8$ x=PARSE(ADD(PARSE($RoomNX$);11)),y=PARSE($RoomSY$),z=PARSE($RoomSZ$),PARSE($RoomSD$)
DEFINE $SR9$ x=PARSE(ADD(PARSE($RoomNX$);22)),y=PARSE($RoomSY$),z=PARSE($RoomSZ$),PARSE($RoomSD$)
DEFINE $SR10$ x=PARSE(ADD(PARSE($RoomNX$);33)),y=PARSE($RoomSY$),z=PARSE($RoomSZ$),PARSE($RoomSD$)
DEFINE $SR11$ x=PARSE(ADD(PARSE($RoomNX$);44)),y=PARSE($RoomSY$),z=PARSE($RoomSZ$),PARSE($RoomSD$)
DEFINE $SR12$ x=PARSE(ADD(PARSE($RoomNX$);55)),y=PARSE($RoomSY$),z=PARSE($RoomSZ$),PARSE($RoomSD$)
//>

//<Defines: Other
/// MMCA Armor Stand Coords
DEFINE $MMCAx$ 50
DEFINE $MMCAy$ 17.67
DEFINE $MMCAz$ 72

///	Wool Color
DEFINE $WoolC1$ 14
DEFINE $WoolC2$ 5
DEFINE $WoolC3$ 9
DEFINE $WoolC4$ 2
DEFINE $WoolC5$ 6
DEFINE $WoolC6$ 1

///	Wool Color Word
DEFINE $WoolCW1$ red
DEFINE $WoolCW2$ lime
DEFINE $WoolCW3$ cyan
DEFINE $WoolCW4$ magenta
DEFINE $WoolCW5$ pink
DEFINE $WoolCW6$ orange
//>

//<To Do 
FUNC Func_ToDoThings
TDWorks TDTest TDFix
Mandatory Unimplemented Changes:
	-Book
	
General Unimplemented Changes:
	-
	
Bugs:
	-
	
Implemented Changes:
	-Change most armor stands to AreaEffectCloud
	-Have fix for when signs display scores correctly
	-Coordinates for MMCA
	
Bug fixes:
	-
	
END
//>

//<Comments
FUNC Func_Comments
///	How it works
	Clones guess down, set player, tp (3+(x-1),x=guess #)
	Summons 4 temp armorstands + passenger cluster (5)
	Sets score with r=0,c=1 (6)
	Scoreboard operation (4)
	Adds score to red or white (28)
	Adds MMAR and MMAW with 0 (2)
	Detects if there's a repeated color (11)
	Detects if there's blocks missing (6)
	Sets sign (1)
	Sets tripwire (1)
	Sets redstone block (1)
	Sets sea lantern on win (1)
	Replaces sign on win (1)
	Kills all armorstands (1)
	Sets the score to 0 (1)

/// Scores legend (to colors)
	Score of 1: Red
	Score of 2: Lime
	Score of 3: Cyan
	Score of 4: Magenta
	Score of 5: Pink
	Score of 6: Orange

/// Objectives:
	MM (Selected player)
	MMpa (Player answer, recorded in armor stands. Tags: MM, MMA, MM1, MM2, MM3, MM4)
	MMpa2 (Red and white score, recorded in armor stands. Tags: MM, MMAW, MMAR)
	MMca (Recorded score of the correct answers in armor stands. Tags: MMCA, MMCA1, MMCA2, MMCA3, MMCA4)
	MMra (Used for detecting repeated colors. Tags: MM, MMRA, MMRA1, MMRA2, MMRA3, MMRA4)
	MMna (Used for detecting blocks that aren't the 6 wool colors. Tags: Uses the armor stands in MMpa)

/// Tags of AS / AEC:
AEC	Guess 1: MM, MMA, MMA#
	
AEC	Clutter / Repeating answer 1-6: MM, MMRA, MMRA#
AEC	Clutter / # of white: MM, MMRW, MMAW
AEC	Clutter / # of red: MM, MMRW, MMAR

AEC	Room Select - MM, MMRoomS, MMRoomS#
	
AS	Correct answer 1-4: MM, MMCA, MMCA#
	
AS	Room 1-12: MM, MMRoom, MMRoom#
	
///	How it will work:
	-Command block sets corresponding room armor stand to guess #
	-All Func_NorthCheck / Func_SouthCheck will be a testfor clock to test for armor stands >= 1
	-Areaeffectclouds spawn depending on gueess #
	-Sets all gm 1 and gm 2 in room to -MM score
	-execute from areaeffectcloud to multiply MM score by -1
	-Teleports pos MM score to areaeffectcloud
	-Regular stuff happens
END
//>


//<General Scoreboard Start
FUNC Func_GeneralScoreboardStart
	USE $ObjA$
		NumberObjAdd
		MMrs dummy MasterMind Room Select
		MMgn dummy MasterMind Guess Number
		MMpa dummy MasterMind Predicted Answer
		MMca dummy MasterMind Correct Answer
		MMsd dummy MasterMind Sign Display
		MMra dummy MasterMind Repeated Answer
		MMna dummy MasterMind No Answer
		MMst stat.sneakTime MasterMind Sneak Timer
		MMit dummy MasterMind Item
	UNUSE
	
	USE $TeamA$
		MMred
		MMlime
		MMcyan
		MMmagenta
		MMpink
		MMorange
	UNUSE
	
	USE $TeamO$
		MMred color red
		MMlime color green
		MMcyan color dark_aqua
		MMmagenta color dark_purple
		MMpink color light_purple
		MMorange color gold
	UNUSE
	
///	Summon Room Armor Stands
	LOOP 1 6 + 1 () 0 55 + 11
		summon ArmorStand ADD($RoomNX$;|2|) $RoomNY$ $RoomNZ$ ;
		{Tags:["MM","MMRoom","MMRoom|0|"],Invulnerable:1,PersistenceRequired:1,Invisible:1,Marker:1,NoGravity:1}
	END
	
	LOOP 7 12 + 1 () 0 55 + 11
		summon ArmorStand ADD($RoomSX$;|2|) $RoomSY$ $RoomSZ$ ;
		{Tags:["MM","MMRoom","MMRoom|0|"],Invulnerable:1,PersistenceRequired:1,Invisible:1,Marker:1,NoGravity:1}
	END
	
	LOOP 1 12 + 1
		@e[type=ArmorStand,MMRoom|0|] MMrs = -|0|
	END
	
///	Summon Correct Answer Armor Stands
	LOOP 1 4 + 1 () 0 3 + 1
		summon ArmorStand SUB($MMCAx$;|2|) $MMCAy$ $MMCAz$ ;
		{Tags:["MM","MMCA","MMCA|0|"],DisabledSlots:2096896,NoGravity:1,NoBasePlate:1,ShowArms:1,Small:1,Invulnerable:1,PersistenceRequired:1,$CustomAS|0|$}
	END
	NumberAdd(-1)
END
//>

//<General Scoreboard Clock
FUNC Func_GeneralScoreboardClock
///	General
	scoreboard teams join NoPVP @a[$SA$,team=!NoPVP]
	$s$weather clear 100000
	
///	Item Clock - Doesn't have a pickup delay of 0 when the player is crouching
	@e[$SA$,type=Item] MMit + 0 {Item:{id:"minecraft:wool"}}
	entitydata @e[$SA$,type=Item,MMit=0] {Item:{tag:{CanPlaceOn:["minecraft:glass"],HideFlags:127}}}
	@a[$SA$,MMst=0] entitydata @e[r=5,type=Item,MMit=0] {PickupDelay:0s}
	@e[$SA$,type=Item,MMit=0] MMit = 1 {Item:{tag:{CanPlaceOn:["minecraft:glass"]}}}
	@a[MMst>=1] MMst = 0
END
//>

//<General Scoreboard End
FUNC Func_GeneralScoreboardEnd
	USE $ObjR$
		MMrs
		MMgn
		MMpa
		MMca
		MMsd
		MMra
		MMna
		MMst
	UNUSE
	
	USE $TeamR$
		MMred
		MMlime
		MMcyan
		MMmagenta
		MMpink
		MMorange
	UNUSE
	
	$s$kill @e[type=ArmorStand,MM]
END
//>


//<North Check
FUNC Func_NorthCheck
	testfor @e[type=ArmorStand,MMRoom|0|,MMgn>=1]
	SetblockTestfor
	
///	Summons AreaEffectClouds at wherever the button was pressed
	LOOP 1 5 + 1 () 0 8 + 2
		@e[type=ArmorStand,MMRoom|0||1|,MMgn=|0|] summon AreaEffectCloud ~ ~2 ~ADD(2;|2|) {Tags:["MM","MMRoomS","MMRoomS|0|"],Duration:5,Rotation:[180f,0f]}
	END
	
	ScOP @a[$SR|0|$] MMrs = @e[type=ArmorStand,MMRoom|0|,MMgn>=1] MMrs
	@e[$SR|0|$,type=AreaEffectCloud,MMRoomS] ScOP @p[r=5,MMrs=-|0|] MMrs *= -1 Number
	$s$tp @a[$SR|0|$,MMrs=|0|] @e[$SR|0|$,type=AreaEffectCloud,MMRoomS]

///	Summons all armor stands (temporary)
	@a[$SR|0|$,MMrs=|0|] summon AreaEffectCloud ~2 ~ ~ {Tags:["MM","MMA","MMA1"],Duration:5}
	@a[$SR|0|$,MMrs=|0|] summon AreaEffectCloud ~3 ~ ~ {Tags:["MM","MMA","MMA2"],Duration:5}
	@a[$SR|0|$,MMrs=|0|] summon AreaEffectCloud ~4 ~ ~ {Tags:["MM","MMA","MMA3"],Duration:5}
	@a[$SR|0|$,MMrs=|0|] summon AreaEffectCloud ~5 ~ ~ {Tags:["MM","MMA","MMA4"],Duration:5}
	@a[$SR|0|$,MMrs=|0|] summon Item ~7 ~ ~ $AreaEffectCloudBeg$

/// Sets a score for the MMA armor stands corresponding to the color
	LOOP 1 6 + 1
		@a[$SR|0||1|$,MMrs=|0||1|] @e[dx=5,dy=1,MMA] wool $WoolC|0|$ ~ ~-1 ~ @e[MMA,r=0,c=1] MMpa = |0|
	END
	
/// Sets the correct answer to the MMA armor stands
	LOOP 1 4 + 1
		@a[$SR|0||1|$,MMrs=|0||1|] ScOP @e[dx=5,dy=1,MMA|0|] MMca = @e[MMCA|0|] MMca
	END
	
/// Counts the number of white and red
	LOOP 1 6 + 1
		@a[$SR|0||1|$,MMrs=|0||1|] @e[dx=5,dy=1,MMca=|0|] @e[r=3,rm=1,MMpa=|0|] @e[dx=6,dy=1,MMAW] MMsd + 1
		@a[$SR|0||1|$,MMrs=|0||1|] @e[dx=5,dy=1,MMA,MMpa=|0|,MMca=|0|] @e[dx=6,dy=1,MMAR] MMsd + 1
	END
	
/// Adds everything to 0 (to display 0 red/white, and to detect if there isn't a wool block placed)
	@e[MMRW] MMsd + 0
	@e[MMA] MMpa + 0
	
/// Detects any repeating blocks
	LOOP 1 6 + 1
		@a[$SR|0||1|$,MMrs=|0||1|] @e[dx=5,dy=1,MMA,MMpa=|0|] @e[dx=5,dy=1,MMRA|0|] MMra + 1
	END
	@a[$SR|0|$,MMrs=|0|] @e[dx=7,dy=1,MMRA,MMra>=2] @a[dx=-7,dy=1,c=1,MMrs=|0|] MMra = 1
	@a[$SR|0|$,MMrs=|0|] MMra + 0
	
/// Detects any non-wool blocks
	@a[$SR|0|$,MMrs=|0|] @e[dx=5,dy=1,MMA,MMpa=0] @a[MMrs=|0|,dx=-5,dy=1,c=1] MMna = 1
	@a[$SR|0|$,MMrs=|0|] MMna + 0

/// Sets the sign if the guess is filled and has no repeating blocks
//=
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] setblock ~7 ~ ~ standing_sign 2 replace $SignRegular$
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=7,dy=1,MMAR,MMsd=4] setblock ~ ~ ~ standing_sign 2 replace $SignWin$
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=7,dy=1,MMAR,MMsd>=1<=3] @e[r=1,c=1,MMAW,MMsd=0] setblock ~ ~ ~ standing_sign 2 replace $SignRed$
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=7,dy=1,MMAW,MMsd>=1<=4] @e[r=1,c=1,MMAR,MMsd=0] setblock ~ ~ ~ standing_sign 2 replace $SignWhite$
\\=

	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] setblock ~7 ~ ~ standing_sign 2 replace $SignFix$
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=7,dy=1,MMAR,MMsd=1] blockdata ~ ~ ~ ;
	{Text2:"ESCAPE({"text":"","extra":[{"text":"1","bold":"true"},{"text":" red"}]})"}
	
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=7,dy=1,MMAR,MMsd=2] blockdata ~ ~ ~ ;
	{Text2:"ESCAPE({"text":"","extra":[{"text":"2","bold":"true"},{"text":" red"}]})"}
	
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=7,dy=1,MMAR,MMsd=3] blockdata ~ ~ ~ ;
	{Text2:"ESCAPE({"text":"","extra":[{"text":"3","bold":"true"},{"text":" red"}]})"}
	
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=7,dy=1,MMAR,MMsd=4] blockdata ~ ~ ~ ;
	{Text2:"ESCAPE({"text":"4 red!","bold":"true"})",Text3:"ESCAPE({"text":"Shhh","bold":"true"})"}
	
	
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=7,dy=1,MMAR,MMsd>=1<=3] @e[r=1,c=1,MMAW,MMsd=0] blockdata ~ ~ ~ {Text3:"ESCAPE({"text":"-=-"})",Text4:"ESCAPE({"text":""})"}
	
	
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=7,dy=1,MMAR,MMsd>=1<=3] @e[r=1,c=1,MMAW,MMsd=1] blockdata ~ ~ ~ ;
	{Text3:"ESCAPE({"text":"","extra":[{"text":"1","bold":"true"},{"text":" white"}]})"}
	
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=7,dy=1,MMAR,MMsd>=1<=3] @e[r=1,c=1,MMAW,MMsd=2] blockdata ~ ~ ~ ;
	{Text3:"ESCAPE({"text":"","extra":[{"text":"2","bold":"true"},{"text":" white"}]})"}
	
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=7,dy=1,MMAR,MMsd>=1<=3] @e[r=1,c=1,MMAW,MMsd=3] blockdata ~ ~ ~ ;
	{Text3:"ESCAPE({"text":"","extra":[{"text":"3","bold":"true"},{"text":" white"}]})"}
	
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=7,dy=1,MMAR,MMsd=0] @e[r=1,c=1,MMAW,MMsd=1] blockdata ~ ~ ~ ;
	{Text2:"ESCAPE({"text":"","extra":[{"text":"1","bold":"true"},{"text":" white"}]})",Text3:"ESCAPE({"text":"-=-"})",Text4:"ESCAPE({"text":""})"}
	
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=7,dy=1,MMAR,MMsd=0] @e[r=1,c=1,MMAW,MMsd=2] blockdata ~ ~ ~ ;
	{Text2:"ESCAPE({"text":"","extra":[{"text":"2","bold":"true"},{"text":" white"}]})",Text3:"ESCAPE({"text":"-=-"})",Text4:"ESCAPE({"text":""})"}
	
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=7,dy=1,MMAR,MMsd=0] @e[r=1,c=1,MMAW,MMsd=3] blockdata ~ ~ ~ ;
	{Text2:"ESCAPE({"text":"","extra":[{"text":"3","bold":"true"},{"text":" white"}]})",Text3:"ESCAPE({"text":"-=-"})",Text4:"ESCAPE({"text":""})"}
	
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=7,dy=1,MMAR,MMsd=0] @e[r=1,c=1,MMAW,MMsd=4] blockdata ~ ~ ~ ;
	{Text2:"ESCAPE({"text":"","extra":[{"text":"4","bold":"true"},{"text":" white"}]})",Text3:"ESCAPE({"text":"-=-"})",Text4:"ESCAPE({"text":""})"}
	
	
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] setblock ~ ~ ~ tripwire 0 replace stone_button
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=7,dy=1,MMAR,MMsd=4] fill ~-7 ~4 ~ ~-7 ~6 ~ sea_lantern 0 replace redstone_lamp
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] clone ~2 ~-1 ~ ~5 ~-1 ~ ~2 ~-3 ~
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] setblock ~ ~5 ~ redstone_block 0
	
/// Shows the warning
	tellraw @a[$SR|0|$,MMrs=|0|,MMra=1] $TextStart$;
		{"text":"Warning: ","color":"red","bold":"true"},;
		{"text":"There are repeated colors. Please redo your guess.","color":"gold"}]}
	tellraw @a[$SR|0|$,MMrs=|0|,MMna=1] $TextStart$;
		{"text":"Warning: ","color":"red","bold":"true"},;
		{"text":"There is at least one missing block. Please redo your guess.","color":"gold"}]}
	
/// Kills all temporary armor stands, sets the selected player's scores to 0
	$s$kill @e[$SR|0|$,type=AreaEffectCloud,MM]
	@a[$SR|0|$,MMrs=|0|] MMna = 0
	@a[$SR|0|$,MMrs=|0|] MMra = 0
	@a[$SR|0|$,MMrs=|0|] MMrs = 0
	@e[type=ArmorStand,MMRoom|0|,MMgn>=1] MMgn = 0
END
//>

//<South Check
FUNC Func_SouthCheck
	testfor @e[type=ArmorStand,MMRoom|0|,MMgn>=1]
	SetblockTestfor
	
///	Summons AreaEffectClouds at wherever the button was pressed
	LOOP 1 5 + 1 () 0 8 + 2
		@e[type=ArmorStand,MMRoom|0||1|,MMgn=|0|] summon AreaEffectCloud ~7 ~2 ~SUB(10;|2|) {Tags:["MM","MMRoomS","MMRoomS|0|"],Duration:5}
	END
	
	ScOP @a[$SR|0|$] MMrs = @e[type=ArmorStand,MMRoom|0|,MMgn>=1] MMrs
	@e[$SR|0|$,type=AreaEffectCloud,MMRoomS] ScOP @p[r=5,MMrs=-|0|] MMrs *= -1 Number
	$s$tp @a[$SR|0|$,MMrs=|0|] @e[$SR|0|$,type=AreaEffectCloud,MMRoomS]

///	Summons all armor stands (temporary)
	@a[$SR|0|$,MMrs=|0|] summon AreaEffectCloud ~-2 ~ ~ {Tags:["MM","MMA","MMA1"],Duration:5}
	@a[$SR|0|$,MMrs=|0|] summon AreaEffectCloud ~-3 ~ ~ {Tags:["MM","MMA","MMA2"],Duration:5}
	@a[$SR|0|$,MMrs=|0|] summon AreaEffectCloud ~-4 ~ ~ {Tags:["MM","MMA","MMA3"],Duration:5}
	@a[$SR|0|$,MMrs=|0|] summon AreaEffectCloud ~-5 ~ ~ {Tags:["MM","MMA","MMA4"],Duration:5}
	@a[$SR|0|$,MMrs=|0|] summon Item ~-7 ~ ~ $AreaEffectCloudBeg$

/// Sets a score for the MMA armor stands corresponding to the color
	LOOP 1 6 + 1
		@a[$SR|0||1|$,MMrs=|0||1|] @e[dx=-5,dy=1,MMA] wool $WoolC|0|$ ~ ~-1 ~ @e[MMA,r=0,c=1] MMpa = |0|
	END
	
/// Sets the correct answer to the MMA armor stands
	LOOP 1 4 + 1
		@a[$SR|0||1|$,MMrs=|0||1|] ScOP @e[dx=-5,dy=1,MMA|0|] MMca = @e[MMCA|0|] MMca
	END
	
/// Counts the number of white and red
	LOOP 1 6 + 1
		@a[$SR|0||1|$,MMrs=|0||1|] @e[dx=-5,dy=1,MMca=|0|] @e[r=3,rm=1,MMpa=|0|] @e[dx=-6,dy=1,MMAW] MMsd + 1
		@a[$SR|0||1|$,MMrs=|0||1|] @e[dx=-5,dy=1,MMA,MMpa=|0|,MMca=|0|] @e[dx=-6,dy=1,MMAR] MMsd + 1
	END
	
/// Adds everything to 0 (to display 0 red/white, and to detect if there isn't a wool block placed)
	@e[MMRW] MMsd + 0
	@e[MMA] MMpa + 0
	
/// Detects any repeating blocks
	LOOP 1 6 + 1
		@a[$SR|0||1|$,MMrs=|0||1|] @e[dx=-5,dy=1,MMA,MMpa=|0|] @e[dx=-5,dy=1,MMRA|0|] MMra + 1
	END
	@a[$SR|0|$,MMrs=|0|] @e[dx=-7,dy=1,MMRA,MMra>=2] @a[dx=7,dy=1,c=1,MMrs=|0|] MMra = 1
	@a[$SR|0|$,MMrs=|0|] MMra + 0
	
/// Detects any non-wool blocks
	@a[$SR|0|$,MMrs=|0|] @e[dx=-5,dy=1,MMA,MMpa=0] @a[MMrs=|0|,dx=5,dy=1,c=1] MMna = 1
	@a[$SR|0|$,MMrs=|0|] MMna + 0

/// Sets the sign if the guess is filled and has no repeating blocks
//=
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] setblock ~-7 ~ ~ standing_sign 10 replace $SignRegular$
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=-7,dy=1,MMAR,MMsd=4] setblock ~ ~ ~ standing_sign 10 replace $SignWin$
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=-7,dy=1,MMAR,MMsd>=1<=3] @e[r=1,c=1,MMAW,MMsd=0] setblock ~ ~ ~ standing_sign 10 replace $SignRed$
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=-7,dy=1,MMAW,MMsd>=1<=4] @e[r=1,c=1,MMAR,MMsd=0] setblock ~ ~ ~ standing_sign 10 replace $SignWhite$
\\=

	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] setblock ~-7 ~ ~ standing_sign 10 replace $SignFix$
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=-7,dy=1,MMAR,MMsd=1] blockdata ~ ~ ~ {Text2:"ESCAPE({"text":"","extra":[{"text":"1","bold":"true"},{"text":" red"}]})"}
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=-7,dy=1,MMAR,MMsd=2] blockdata ~ ~ ~ {Text2:"ESCAPE({"text":"","extra":[{"text":"2","bold":"true"},{"text":" red"}]})"}
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=-7,dy=1,MMAR,MMsd=3] blockdata ~ ~ ~ {Text2:"ESCAPE({"text":"","extra":[{"text":"3","bold":"true"},{"text":" red"}]})"}
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=-7,dy=1,MMAR,MMsd=4] blockdata ~ ~ ~ {Text2:"ESCAPE({"text":"4 red!","bold":"true"})",Text3:"ESCAPE({"text":"Shhh","bold":"true"})"}
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=-7,dy=1,MMAR,MMsd>=1<=3] @e[r=1,c=1,MMAW,MMsd=0] blockdata ~ ~ ~ {Text3:"ESCAPE({"text":"-=-"})",Text4:"ESCAPE({"text":""})"}
	
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=-7,dy=1,MMAR,MMsd>=1<=3] @e[r=1,c=1,MMAW,MMsd=1] blockdata ~ ~ ~ {Text3:"ESCAPE({"text":"","extra":[{"text":"1","bold":"true"},{"text":" white"}]})"}
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=-7,dy=1,MMAR,MMsd>=1<=3] @e[r=1,c=1,MMAW,MMsd=2] blockdata ~ ~ ~ {Text3:"ESCAPE({"text":"","extra":[{"text":"2","bold":"true"},{"text":" white"}]})"}
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=-7,dy=1,MMAR,MMsd>=1<=3] @e[r=1,c=1,MMAW,MMsd=3] blockdata ~ ~ ~ {Text3:"ESCAPE({"text":"","extra":[{"text":"3","bold":"true"},{"text":" white"}]})"}
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=-7,dy=1,MMAR,MMsd=0] @e[r=1,c=1,MMAW,MMsd=1] blockdata ~ ~ ~ {Text2:"ESCAPE({"text":"","extra":[{"text":"1","bold":"true"},{"text":" white"}]})",Text3:"ESCAPE({"text":"-=-"})",Text4:"ESCAPE({"text":""})"}
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=-7,dy=1,MMAR,MMsd=0] @e[r=1,c=1,MMAW,MMsd=2] blockdata ~ ~ ~ {Text2:"ESCAPE({"text":"","extra":[{"text":"2","bold":"true"},{"text":" white"}]})",Text3:"ESCAPE({"text":"-=-"})",Text4:"ESCAPE({"text":""})"}
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=-7,dy=1,MMAR,MMsd=0] @e[r=1,c=1,MMAW,MMsd=3] blockdata ~ ~ ~ {Text2:"ESCAPE({"text":"","extra":[{"text":"3","bold":"true"},{"text":" white"}]})",Text3:"ESCAPE({"text":"-=-"})",Text4:"ESCAPE({"text":""})"}
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=-7,dy=1,MMAR,MMsd=0] @e[r=1,c=1,MMAW,MMsd=4] blockdata ~ ~ ~ {Text2:"ESCAPE({"text":"","extra":[{"text":"4","bold":"true"},{"text":" white"}]})",Text3:"ESCAPE({"text":"-=-"})",Text4:"ESCAPE({"text":""})"}
	
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] setblock ~ ~ ~ tripwire 0 replace stone_button
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] @e[dx=-7,dy=1,MMAR,MMsd=4] fill ~7 ~4 ~ ~7 ~6 ~ sea_lantern 0 replace redstone_lamp
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] clone ~-2 ~-1 ~ ~-5 ~-1 ~ ~-5 ~-3 ~
	@a[$SR|0|$,MMrs=|0|,MMra=0,MMna=0] setblock ~ ~5 ~ redstone_block 0
	
/// Shows the warning
	tellraw @a[$SR|0|$,MMrs=|0|,MMra=1] $TextStart$;
		{"text":"Warning: ","color":"red","bold":"true"},;
		{"text":"There are repeated colors. Please redo your guess.","color":"gold"}]}
	tellraw @a[$SR|0|$,MMrs=|0|,MMna=1] $TextStart$;
		{"text":"Warning: ","color":"red","bold":"true"},;
		{"text":"There is at least one missing block. Please redo your guess.","color":"gold"}]}
	
/// Kills all temporary armor stands, sets the selected player's scores to 0
	$s$kill @e[$SR|0|$,type=AreaEffectCloud,MM]
	@a[$SR|0|$,MMrs=|0|] MMna = 0
	@a[$SR|0|$,MMrs=|0|] MMra = 0
	@a[$SR|0|$,MMrs=|0|] MMrs = 0
	@e[type=ArmorStand,MMRoom|0|,MMgn>=1] MMgn = 0
END
//>


//<Open Doors
FUNC Func_OpenDoors
	fill 49 8 46 105 6 46 air 0 replace bedrock 0
	fill 49 8 37 105 6 37 air 0 replace bedrock 0
END
//>

//<Close Doors
FUNC Func_CloseDoors
	fill 49 8 46 105 6 46 bedrock 0 replace air 0
	fill 49 8 37 105 6 37 bedrock 0 replace air 0
END
//>

//<Set Buttons
FUNC Func_SetButtons
	fill 53 6 57 108 6 49 stone_button 5 replace tripwire
	fill 46 6 26 101 6 34 stone_button 5 replace tripwire
///	fill 39 4 42 39 4 41 redstone_block 0 - Activates clock
END
//>

//<Give wool
FUNC Func_GiveWool
	replaceitem entity @a[$SA$,m=2] slot.hotbar.6 shears 1 0 {CanDestroy:["minecraft:wool"],Unbreakable:1,ench:[{id:32,lvl:9001}]}
	replaceitem entity @a[$SA$,m=2] slot.hotbar.8 golden_apple 64 0
	replaceitem entity @a[$SA$,m=2] slot.hotbar.0 wool 64 1 {CanPlaceOn:["minecraft:glass"],HideFlags:127}
	replaceitem entity @a[$SA$,m=2] slot.hotbar.1 wool 64 2 {CanPlaceOn:["minecraft:glass"],HideFlags:127}
	replaceitem entity @a[$SA$,m=2] slot.hotbar.2 wool 64 6 {CanPlaceOn:["minecraft:glass"],HideFlags:127}
	replaceitem entity @a[$SA$,m=2] slot.hotbar.3 wool 64 9 {CanPlaceOn:["minecraft:glass"],HideFlags:127}
	replaceitem entity @a[$SA$,m=2] slot.hotbar.4 wool 64 5 {CanPlaceOn:["minecraft:glass"],HideFlags:127}
	replaceitem entity @a[$SA$,m=2] slot.hotbar.5 wool 64 14 {CanPlaceOn:["minecraft:glass"],HideFlags:127}
	
	replaceitem entity @a[$SA$,m=1] slot.hotbar.6 sign 1 0 {ench:[{id:0,lvl:1}],HideFlags:127,display:{Name:"OP sign"}}
	replaceitem entity @a[$SA$,m=1] slot.hotbar.0 wool 1 1 {CanPlaceOn:["minecraft:glass"],HideFlags:127}
	replaceitem entity @a[$SA$,m=1] slot.hotbar.1 wool 1 2 {CanPlaceOn:["minecraft:glass"],HideFlags:127}
	replaceitem entity @a[$SA$,m=1] slot.hotbar.2 wool 1 6 {CanPlaceOn:["minecraft:glass"],HideFlags:127}
	replaceitem entity @a[$SA$,m=1] slot.hotbar.3 wool 1 9 {CanPlaceOn:["minecraft:glass"],HideFlags:127}
	replaceitem entity @a[$SA$,m=1] slot.hotbar.4 wool 1 5 {CanPlaceOn:["minecraft:glass"],HideFlags:127}
	replaceitem entity @a[$SA$,m=1] slot.hotbar.5 wool 1 14 {CanPlaceOn:["minecraft:glass"],HideFlags:127}
END
//>

//<Clear Inventory
FUNC Func_ClearInventory
	@a[$SA$] MMit + 0
	@a[$SA$,EC=0] MMit + 1
	$s$clear @a[$SA$,MMit=0]
	effect @a[$SA$,MMit=0] clear
	scoreboard players reset @a[$SA$,MMit>=0] MMit
END
//>

//<Clear Lanes
FUNC Func_ClearLanes
//=
	fill 46 6 57 101 6 49 air 0 replace standing_sign
	fill 53 6 57 108 6 49 tripwire 0 replace stone_button 5
	fill 53 11 57 108 11 49 air 0 replace redstone_block
	fill 48 9 59 106 9 59 air 0 replace wool
	fill 48 5 57 106 5 49 air 0 replace wool
	fill 48 4 57 106 4 49 glass 0 replace wool
	
	fill 53 6 26 108 6 34 air 0 replace standing_sign
	fill 46 6 26 101 6 34 tripwire 0 replace stone_button 5
	fill 46 11 26 101 11 34 air 0 replace redstone_block
	fill 48 9 24 106 9 24 air 0 replace wool
	fill 48 5 26 106 5 34 air 0 replace wool
	fill 48 4 26 106 4 34 glass 0 replace wool
	
	fill 53 12 57 108 10 49 redstone_lamp 0 replace sea_lantern 0
	fill 46 12 26 101 10 34 redstone_lamp 0 replace sea_lantern 0
\\=
	
	fill 46 2 47 108 2 36 redstone_block 0 replace planks 0
	fill 46 2 47 108 2 36 planks 0 replace redstone_block 0
END
//>


//<Set Answer
FUNC Func_SetAnswer
	LOOP 1 6 + 1
		@e[type=ArmorStand,MMCA] wool $WoolC|0|$ ~ ~-3 ~ @e[MMCA,r=0,c=1] MMca = |0|
	END
	
	clone 50 14 72 50 14 72 47 12 72
	clone 49 14 72 49 14 72 48 12 72
	clone 48 14 72 48 14 72 49 12 72
	clone 47 14 72 47 14 72 50 12 72
	
///	Structure block save
	fill 48 9 72 48 10 72 redstone_block 0
END
//>

//<Randomizer
FUNC Func_Randomizer
	summon Item ~ ~ ~ $AreaEffectCloudRand$

	LOOP 1 6 + 1
		@e[type=AreaEffectCloud,MMRAN|0|,c=1] MMca = |0|
	END
	
	LOOP 1 3 + 1
		ScTG @r[type=AreaEffectCloud,MMRAN] add MMRCA
		ScOP @e[type=ArmorStand,MMCA|0|] MMca = @e[type=AreaEffectCloud,MMRCA] MMca
		$s$kill @e[type=AreaEffectCloud,MMRCA]
	END
	ScTG @r[type=AreaEffectCloud,MMRAN] add MMRCA
	ScOP @e[type=ArmorStand,MMCA4] MMca = @e[type=AreaEffectCloud,MMRCA] MMca
	$s$kill @e[type=AreaEffectCloud,MMRAN]
	
	LOOP 1 6 + 1
		@e[type=ArmorStand,MMCA,MMca=|0|] setblock ~ ~-3 ~ wool $WoolC|0|$
	END

	clone 50 14 72 50 14 72 47 12 72
	clone 49 14 72 49 14 72 48 12 72
	clone 48 14 72 48 14 72 49 12 72
	clone 47 14 72 47 14 72 50 12 72
	
///	Structure block save
	fill 48 9 72 48 10 72 redstone_block 0
END
//>


//<Auto Broadcast
FUNC Func_AutoBroadcast
	LOOP 1 6 + 1
		entitydata @e[type=ArmorStand,MMCA,MMca=|0|] {CustomName:"$WoolCW|0|$",Team:"MM$WoolCW|0|$"}
	END
	tellraw @a $FakeBroadcast$
END
//>

//<Clone Answers
FUNC Func_CloneAnswers
	fill 106 2 59 48 2 24 redstone_block 0 replace glowstone 0
	fill 106 2 59 48 2 24 glowstone 0 replace redstone_block 0
END
//>

//<Broadcast and Clone
///	Activate both
//>
	


//<Groups
:CBPMainStart PULSE $Style$
Deactivate(CBPMainStart)
Func_GeneralScoreboardStart
Activate(CBPMainClock)
Activate(CBPSetAnswer)
LOOP 1 6 + 1
	Activate(CBPNorthCheck|0|)
END
LOOP 7 12 + 1
	Activate(CBPSouthCheck|0|)
END

:CBPMainClock CLOCK $Style$
Func_GeneralScoreboardClock

:CBPMainEnd PULSE $Style$
Deactivate(CBPMainEnd)
Func_ClearInventory
Func_GeneralScoreboardEnd
Deactivate(CBPMainClock)
LOOP 1 6 + 1
	Deactivate(CBPNorthCheck|0|)
END
LOOP 7 12 + 1
	Deactivate(CBPSouthCheck|0|)
END
Activate(CBPOpenDoors)
Activate(CBPClearLanes)



:CBPOpenDoors PULSE $Style$
Deactivate(CBPOpenDoors)
Func_OpenDoors

:CBPCloseDoors PULSE $Style$
Deactivate(CBPCloseDoors)
Func_CloseDoors

:CBPSetButtons PULSE $Style$
Deactivate(CBPSetButtons)
Func_SetButtons

:CBPGiveWool PULSE $Style$
Deactivate(CBPGiveWool)
Func_GiveWool

:CBPClearInventory PULSE $Style$
Deactivate(CBPClearInventory)
Func_ClearInventory

:CBPClearLanes PULSE $Style$
Deactivate(CBPClearLanes)
Func_ClearLanes



:CBPSetAnswer PULSE $Style$
Deactivate(CBPSetAnswer)
Func_SetAnswer

:CBPRandomizer PULSE $Style$
Deactivate(CBPRandomizer)
Func_Randomizer



:CBPAutoBroadcast PULSE $Style$
Deactivate(CBPAutoBroadcast)
Func_AutoBroadcast

:CBPCloneAnswers PULSE $Style$
Deactivate(CBPCloneAnswers)
Func_CloneAnswers

:CBPBroadcastAndClone PULSE $Style$
Deactivate(CBPBroadcastAndClone)
Activate(CBPAutoBroadcast)
Activate(CBPCloneAnswers)



:CBPNorthCheck1 CLOCK $Style$
Func_NorthCheck(1)

:CBPNorthCheck2 CLOCK $Style$
Func_NorthCheck(2)

:CBPNorthCheck3 CLOCK $Style$
Func_NorthCheck(3)

:CBPNorthCheck4 CLOCK $Style$
Func_NorthCheck(4)

:CBPNorthCheck5 CLOCK $Style$
Func_NorthCheck(5)

:CBPNorthCheck6 CLOCK $Style$
Func_NorthCheck(6)



:CBPSouthCheck7 CLOCK $Style$
Func_SouthCheck(7)

:CBPSouthCheck8 CLOCK $Style$
Func_SouthCheck(8)

:CBPSouthCheck9 CLOCK $Style$
Func_SouthCheck(9)

:CBPSouthCheck10 CLOCK $Style$
Func_SouthCheck(10)

:CBPSouthCheck11 CLOCK $Style$
Func_SouthCheck(11)

:CBPSouthCheck12 CLOCK $Style$
Func_SouthCheck(12)
//>























