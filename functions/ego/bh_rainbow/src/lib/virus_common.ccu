

$py(
from lib.consts import Effects, Sounds, Enchants
from lib.scoreboard import OBJECTIVES, TEAMS
from lib.const_ints import CONST_INTS

# general constants
select_all = virus.event.select_all

# the initials are the same as the display objective name
initials = obj_name = virus.initials

# shorthand team names
virus_team = virus.initials + "v"
hiders_team = virus.initials + "h"


OBJECTIVES.new_str("""
    # The main display objective
    . _ .

    # Holds all of the players to initialize
    # 0 = being outside the map
    # 1 = part of the virus map, meaning the players are properly initialized
    # 2 = actually participating in the round
    pl _ Player List

    # Sets the score depending on whether they are with the map or not
    # 0 = not in the map
    # 1 = outside the map
    sa _ Select All

    # Used on the armor stand to count down the time on both the
    # countdown and the game time
    ti _ Timer

    # Used on the armor stand to count how many hiders and virus
    # players exist
    chi _ Count {hiders_name}
    cvr _ Count {viruses_name}

    # Used on the armor stand to show whether the players glow or not
    # 0 = None
    # 1 = Virus
    # 2 = Hiders
    # 3 = Both
    gl _ Glowing Players

    # Used on the armor stand for misc. calculations
    calc _ Calculations

    # used on the Virus Stand to store the state of the round
    # 0 = waiting for the round to start
    # 1 = round start, tick
    # 2 = during countdown
    # 3 = after countdown, main game
    # 4 = round end -->
    # 5 = during round end
    # 6 = reset round --> waiting
    st _ State
    # xp _ XP Calc

""".format(hiders_name=hiders_name, viruses_name=viruses_name), initials, virus.obj_disp)

OBJECTIVES[initials].setdisplay("sidebar")

CONST_INTS.add_consts(20, 60, 1200)

TEAMS.new_str("""
    h {hiders_name}
        friendlyfire false
        collisionRule never
        deathMessageVisibility always
        color green
    
    v {viruses_name}
        friendlyfire false
        collisionRule never
        deathMessageVisibility always
        color yellow
    
    d_y Display Yellow
        color yellow
    
    d_g Display Green
        color green
""".format(hiders_name=hiders_name, viruses_name=viruses_name), initials, virus.obj_disp)

)


$edit_type_py_init()
$edit_py_init()


# used on a player to initialize the game and set themselves as the host
!mfunc init
    $(virus.cmd_init())
    $(OBJECTIVES.cmd_init())
    $(TEAMS.cmd_init())

    @s HOST = 0

    join $(initials)d_y Countdown Minutes Seconds $(viruses_name)
    join $(initials)d_g $(hiders_name)

    &Countdown $(initials)calc = $(20 * 60)
    &Glowing $(initials)calc = $(20 * 300)
    &GameTime $(initials)calc = $(20 * 600)

    summon armor_stand ~ ~ ~ {Tags:["$(initials)Stand","$(initials)Entity"],Invulnerable:1,PersistenceRequired:1,Invisible:1,Marker:1,NoGravity:1}
    @e[type=armor_stand,$(initials)Stand] function reset_round
    $(virus.cmd_post_init())

!mfunc main
    $(virus.cmd_main())

    # kills all items
    kill @e[$(select_all),tag=!$(initials)Entity,type=item]

    # TODO
    # prevents pvp inside the spawn

    # resets the player if they die
    # doesn't directly call the add_to_round since the function should not be ran while dead
    @a[m=2,gDE=1,$(initials)pl=0..2] $(initials)pl = 0
    
    # initializes the player
    @a $(initials)pl + 0
    @a[gSA=1,m=2,$(initials)pl=0] function reset_player

    # also resets the player if they just rejoined the game
    @a[m=2,gLG=1,$(initials)pl=1..2] function reset_player

    # adds the player to the round once started or after being reset
    # also resets all players that aren't in the round after

    @e[type=armor_stand,$(initials)Stand,$(initials)st=0] function wait_for_start
    @e[type=armor_stand,$(initials)Stand,$(initials)st=1] function start_round
    @e[type=armor_stand,$(initials)Stand,$(initials)st=2] function countdown
    @e[type=armor_stand,$(initials)Stand,$(initials)st=3] function during_round
    @e[type=armor_stand,$(initials)Stand,$(initials)st=4] function stop_round
    @e[type=armor_stand,$(initials)Stand,$(initials)st=5] function wait_for_reset
    @e[type=armor_stand,$(initials)Stand,$(initials)st=6] function reset_round

    # sets the glowing effect
    @e[type=armor_stand,$(initials)Stand] function set_glowing_effects
    
    # shows the player numbers at all times
    @e[type=armor_stand,$(initials)Stand] function calc_player_numbers

    # teleports any players to the given location as necessary
    @a[$(virus.select_spawn),m=2,$(initials)pl=1..2] function teleport_players

!mfunc term
    $(virus.cmd_term())

    @a[$(initials)pl=1..2,m=2] function full_reset_player

    # all players are removed from hosting mastermind
    * reset HOST

    kill @e[type=armor_stand,$(initials)Entity]

    $edit_type_main_end()
    $edit_main_end()

    $(OBJECTIVES.cmd_term())
    $(TEAMS.cmd_term())


!mfunc wait_for_start
    @a[gSA=1,m=2,$(initials)pl=2] function full_reset_player


# used on the Virus Stand to start the game
!mfunc start_round
    # opens the doors
    title @a actionbar {"text":"The doors are now open","color":"green"}
    @a playsound $(Sounds.level) voice @s
    $edit_type_round_start()
    $edit_round_start()

    # sets the round time
    ScOP @s $(initials)ti = &Countdown $(initials)calc

    # sets the state
    @s $(initials)st = 2


# Used on the Virus Stand to go through the countdown
!mfunc countdown
    $edit_type_countdown()
    $edit_countdown()

    # runs the timer
    function timer
    
    # sets the player effects
    function player_effects

    @a[gSA=1,m=2,$(initials)pl=1] function add_to_round

    @s[$(initials)ti=..0] function end_countdown


# Used on the Virus Stand once the countdown ends
!mfunc end_countdown
    Countdown reset $(obj_name)
    title @a actionbar {"text":"The $(viruses_name.lower()) has been released!","color":"yellow"}
    @a playsound $(Sounds.wither) voice @s
    $edit_type_countdown_end()
    $edit_countdown_end()
    
    ScOP @s $(initials)ti = &GameTime $(virus.initials)calc
    @s $(initials)st = 3


# Used on the Virus Stand during the round time
!mfunc during_round
    # runs the timer
    function timer

    # sets all hiders to virus if they stay in spawn
    @a[$(virus.select_spawn),m=2,team=$(hiders_team)] function to_virus

    # sets the player effects
    function player_effects
    function calc_glowing

    # detects whether the virus won
    @e[type=armor_stand,$(initials)Stand,$(initials)chi=0] $(initials)st = 4

    # adds anyone to the round
    @a[gSA=1,m=2,$(initials)pl=1] function add_to_round
    

# used on the Virus Stand when there are no more hiders or when the timer runs out
!mfunc stop_round
    # all players will glow
    @s $(initials)gl = 3

    # if the virus won, the hider number is 0
    @s[$(initials)chi=0] title @a title {"text":"The $(viruses_name.lower()) won!","color":"yellow"}
    @s[$(initials)chi=0] tellraw @a $(virus.event.begin(r'{"text":"The ' + viruses_name.lower() + r' won!","color":"yellow"}'))

    # if the hiders won, then is more than one hider
    @s[$(initials)chi=1..] title @a title {"text":"Time!","color":"green"}
    @s[$(initials)chi=1..] title @a subtitle {"text":"The $(hiders_name.lower()) won!","color":"green"}
    @s[$(initials)chi=1..] tellraw @a $(virus.event.begin(r'{"text":"The time is up! The ' + hiders_name.lower() + r' won!","color":"yellow"}'))
    @s[$(initials)chi=1..] Seconds reset $(obj_name)
    @s[$(initials)chi=1..] Minutes reset $(obj_name)
    $edit_type_round_end()
    $edit_round_end()

    @s $(initials)st = 5


# used on the Virus Stand when waiting for the round reset
!mfunc wait_for_reset
    @a[gSA=1,m=2,$(initials)pl=1..2] function invuln_effect


# used on the Virus Stand when the round actually resets
!mfunc reset_round
    @a[gSA=1,m=2,$(initials)pl=2] function full_reset_player
    @s $(initials)gl = 1
    $edit_type_round_reset()
    $edit_round_reset()
    
    Countdown reset $(obj_name)
    Minutes reset $(obj_name)
    Seconds reset $(obj_name)
    @s $(initials)st = 0


# used on arbitrary to set their effects
!mfunc player_effects
    effect @a[gSA=1,m=2,$(initials)pl=2,team=$(virus_team)] $(Effects.strength) 3 90 true
    effect @a[gSA=1,m=2,$(initials)pl=2] $(Effects.resist) 3 3 true
    effect @a[gSA=1,m=2,$(initials)pl=2] $(Effects.hp) 100 100 true
    $edit_type_player_effects()
    $edit_player_effects()


# used on players to give them invulnerability for a time being
!mfunc invuln_effect
    effect @s $(Effects.resist) 3 10 true
    effect @s $(Effects.hp) 100 100 true

# used on the Virus Stand to set the glowing effect
!mfunc set_glowing_effects
    @s[$(initials)gl=2..] effect @a[gSA=1,m=2,team=$(hiders_team)] $(Effects.glow) 3 0 true
    @s[$(initials)gl=2..] $(initials)gl - 2
    @s[$(initials)gl=1..] effect @a[gSA=1,m=2,team=$(virus_team)] $(Effects.glow) 3 0 true
    @s[$(initials)gl=2..] $(initials)gl - 1


# used on players to add them to the round by assigning their teams and resetting them
!mfunc add_to_round
    @s[team=!$(virus_team)] function clear_player

    @s[team=!$(virus_team)] function to_hider if @e[type=armor_stand,$(initials)Stand,$(initials)st=0..2]
    function to_virus if @e[type=armor_stand,$(initials)Stand,$(initials)st=3]
    function to_hider if @e[type=armor_stand,$(initials)Stand,$(initials)st=4..6]

    $edit_add_to_round()
    $edit_type_add_to_round()

    @s $(initials)pl = 2


# used on a given player to clear their inventory
!mfunc clear_player
    clear @s
    effect @s clear

    # gives the book only to the host
    $(virus.cmd_book("@s[HOST=0]".format(initials)))

    $edit_clear_player()
    $edit_type_clear_player()

# used on a given player to initialize themselves and set themselves as a hider
!mfunc reset_player
    function to_hider

    # sets their player state to 1 to indicate they are initialized
    @s $(initials)pl = 1

# used on a given player to reset them completely,
# meant to be used at the end of a round
!mfunc full_reset_player
    function reset_player
    function tp_to_spawn
    function to_hider


# used on players to add them to the hider team
!mfunc to_hider
    function clear_player
    join $(hiders_team) @s

    $edit_to_hider()
    $edit_type_to_hider()

    # teleports back to the spawn if the game hasn't started yet
    @s gTEMP = 0
    @s[$(virus.select_spawn)] gTEMP = 1
    @s[gTEMP=0] function tp_to_spawn if @e[type=armor_stand,$(initials)Stand,$(initials)st=0..1]


# used on players to add them to the virus team
!mfunc to_virus
    function clear_player

    $edit_type_to_virus()
    $edit_to_virus()

    # adds a golden helmet lol
    replaceitem entity @s slot.armor.head golden_helmet 1 0 {
        Unbreakable:1,ench:[{id:$(Enchants.BIND),lvl:1},{id:$(Enchants.VANISH),lvl:1}]}

    title @s title {"text":"You are now","color":"yellow"}
    title @s subtitle {"text":"the $(virus_name.lower())!","color":"yellow"}
    join $(virus_team) @s

    # teleports to the waiting room during countdown
    function tp_to_wait if @e[type=armor_stand,$(initials)Stand,$(initials)st=0..2]

    # teleports to the arena during game time
    function tp_to_arena if @e[type=armor_stand,$(initials)Stand,$(initials)st=3..6]


# used on players in the spawn room to make sure that players will be properly teleported
!mfunc teleport_players
    # teleports to the waiting room during countdown
    @s[team=$(virus_team)] function tp_to_wait if @e[type=armor_stand,$(initials)Stand,$(initials)st=0..2]

    # teleports to the arena during game time
    @s[team=$(virus_team)] function tp_to_arena if @e[type=armor_stand,$(initials)Stand,$(initials)st=3..6]

!mfunc tp_to_wait
    tp @s $(virus.wait_coords)

!mfunc tp_to_arena
    tp @s $(virus.arena_coords)

!mfunc tp_to_spawn
    $(virus.cmd_spawn())


# used on the Virus Stand to count time
!mfunc timer
    @s[$(initials)ti=1..] $(initials)ti - 1
    function disp_time_text
    function disp_time_score

# used on the Virus Stand to display the time in tellraw messages
!mfunc disp_time_text
    @s[$(initials)ti=$(5*60*20)] tellraw @a $(virus.event.begin(r'{"text":"5 minutes remain!","color":"yellow"}'))
    @s[$(initials)ti=$(4*60*20)] tellraw @a $(virus.event.begin(r'{"text":"4 minutes remain!","color":"yellow"}'))
    @s[$(initials)ti=$(3*60*20)] tellraw @a $(virus.event.begin(r'{"text":"3 minutes remain!","color":"yellow"}'))
    @s[$(initials)ti=$(2*60*20)] tellraw @a $(virus.event.begin(r'{"text":"2 minutes remain!","color":"yellow"}'))
    @s[$(initials)ti=$(1*60*20)] tellraw @a $(virus.event.begin(r'{"text":"1 minute remains!","color":"yellow"}'))
    
    @s[$(initials)ti=$(30*20)] tellraw @a $(virus.event.begin(r'{"text":"30 seconds remain!","color":"yellow"}'))
    @s[$(initials)ti=$(15*20)] tellraw @a $(virus.event.begin(r'{"text":"15 seconds remain!","color":"yellow"}'))
    @s[$(initials)ti=$(5*20)] tellraw @a $(virus.event.begin(r'{"text":"5!","color":"yellow"}'))
    @s[$(initials)ti=$(4*20)] tellraw @a $(virus.event.begin(r'{"text":"4!","color":"yellow"}'))
    @s[$(initials)ti=$(3*20)] tellraw @a $(virus.event.begin(r'{"text":"3!","color":"yellow"}'))
    @s[$(initials)ti=$(2*20)] tellraw @a $(virus.event.begin(r'{"text":"2!","color":"yellow"}'))
    @s[$(initials)ti=$(1*20)] tellraw @a $(virus.event.begin(r'{"text":"1!","color":"yellow"}'))
    @s[$(initials)ti=..0] $(initials)st = 4
    
# used on the Virus Stand to display the time on the sidebar
!mfunc disp_time_score
    ScOP Seconds $(initials)ti = @s $(initials)ti
    ScOP Seconds $(initials)ti /= 20 constants
    ScOP Seconds $(initials)ti %= 60 constants
    Seconds $(initials)ti + 1
    ScOP Seconds $(obj_name) = Seconds $(initials)ti

    ScOP Minutes $(initials)ti = @s $(initials)ti
    ScOP Minutes $(initials)ti /= 1200 constants
    ScOP Minutes $(obj_name) = Minutes $(initials)ti
    

# Used on the Virus Stand during the normal round to determine whether glowing
# can be removed or not, with the constant being (initials)Glowing
# it is determined when the calculated score goes positive,
# meaning the glowing score exceeds the timer (represented in seconds)
!mfunc calc_glowing
    ScOP @s $(initials)calc = &Glowing $(initials)calc
    ScOP @s $(initials)calc -= @s $(initials)ti

    @s[$(initials)calc=0] tellraw @a $(virus.event.begin(r'{"text":"Glowing has been removed from all ' + viruses_name.lower() + r'!","color":"yellow"}'))
    @s[$(initials)calc=0] @a playsound $(Sounds.xp) voice @a[c=1]
    @s[$(initials)calc=0] $(initials)gl = 0
    

# used on the Virus Stand to show the total number of virus and hiders
!mfunc calc_player_numbers
    # display the number of hiders on the fake name of hiders_name
    @s $(initials)chi = 0
    @a[gSA=1,m=2,team=$(hiders_team)] @e[type=armor_stand,$(initials)Stand] $(initials)chi + 1
    ScOP $(hiders_name) $(obj_name) = @s $(initials)chi
    
    # display the number of virus on the fake name of virus_name
    @s $(initials)cvr = 0
    @a[gSA=1,m=2,team=$(virus_team)] @e[type=armor_stand,$(initials)Stand] $(initials)cvr + 1
    ScOP $(viruses_name) $(obj_name) = @s $(initials)cvr
    

# used as input to manually start the round
!mfunc input_start_round
    @e[type=armor_stand,$(initials)Stand] $(initials)st = 1


# used as input to manually stop the round
!mfunc input_stop_round
    @e[type=armor_stand,$(initials)Stand] $(initials)st = 4
    

# used as input to manually reset the round
!mfunc input_reset_round
    @e[type=armor_stand,$(initials)Stand] $(initials)st = 6


# used as input to randomly select a virus
!mfunc input_select_rand_virus
    @r[gSA=1,m=2,team=$(hiders_team)] function to_virus


# used as input to randomly convert a virus to a hider
!mfunc input_remove_one_virus
    @r[gSA=1,m=2,team=$(virus_team)] function to_hider


# used as input to select a team
!mfunc input_select_team
    tellraw @a[EC=0] $(virus.event.begin())
        {"text":"Choose team: ","color":"gray"},
        {"text":"$(hider_name)","color":"green","italic":true,
            "clickEvent":{"action":"suggest_command","value":"/execute PLAYER_NAME ~ ~ ~ $(virus.cmd_func("to_hider"))"},
            "hoverEvent":{"action":"show_text","value":{"text":"Force someone to join the $(hiders_name.lower())","color":"green"}}
        },
        {"text":", ","color":"gray","italic":true},
        {"text":"$(virus_name)","color":"yellow","italic":true,
            "clickEvent":{"action":"suggest_command","value":"/execute PLAYER_NAME ~ ~ ~ $(virus.cmd_func("to_virus"))"},
            "hoverEvent":{"action":"show_text","value":{"text":"Force someone to join the $(viruses_name.lower())","color":"yellow"}}
        }]}


!mfunc input_add_one_min
    @e[type=armor_stand,$(initials)Stand] $(initials)ti + $(20*60)

!mfunc input_add_30s
    @e[type=armor_stand,$(initials)Stand] $(initials)ti + $(20*30)

!mfunc input_remove_30s
    @e[type=armor_stand,$(initials)Stand] $(initials)ti - $(20*30)

!mfunc input_remove_one_min
    @e[type=armor_stand,$(initials)Stand] $(initials)ti - $(20*60)




# used on players to get the custom book
# requires additional backslashes because pyexpander is a potato
# and changes double backslashes --> single backslash
!mfunc book
    replaceitem entity @s slot.weapon.offhand written_book 1 0 {
        title:"$(virus.event.full_name) Book",author:"eGO",pages:["[\"\",
            {\"text\":\"$(virus.event.full_name) Settings\\\n\",\"bold\":true},
            {\"text\":\"\\\n\"},
            
            {\"text\":\"$(initials): \",\"color\":\"dark_gray\"},
            {\"text\":\"Start\",\"color\":\"dark_green\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(virus.cmd_func("init"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Starts the $(type_name.lower()) map so it can be ran\",\"color\":\"green\"}}
            },
            {\"text\":\" / \",\"color\":\"gray\"},
            {\"text\":\"Stop\\\n\",\"color\":\"red\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(virus.cmd_func("term"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Ends the $(type_name.lower()) map\",\"color\":\"red\"}}
            },
            
            {\"text\":\"Round: \",\"color\":\"dark_gray\"},
            {\"text\":\"Start\",\"color\":\"dark_green\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(virus.cmd_func("input_start_round"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Starts an individual round\",\"color\":\"dark_green\"}}
            },
            {\"text\":\" / \",\"color\":\"gray\"},
            {\"text\":\"Stop\",\"color\":\"gold\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(virus.cmd_func("input_stop_round"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Stops an individual round\",\"color\":\"gold\"}}
            },
            {\"text\":\" / \",\"color\":\"gray\"},
            {\"text\":\"Reset\\\n\\\n\",\"color\":\"red\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(virus.cmd_func("input_reset_round"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Resets an individual round\",\"color\":\"red\"}}
            },
            
            
            {\"text\":\"Select $(virus_name): \",\"color\":\"dark_gray\"},
            {\"text\":\"[+]\",\"color\":\"dark_green\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(virus.cmd_func("input_select_rand_virus"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Chooses a $(virus_name.lower())\",\"color\":\"dark_green\"}}
            },
            
            {\"text\":\" \",\"color\":\"gray\"},
            {\"text\":\"[-]\\\n\",\"color\":\"red\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(virus.cmd_func("input_remove_one_virus"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Removes a $(virus_name.lower())\",\"color\":\"red\"}}
            },
            
            {\"text\":\"Edit Teams: \",\"color\":\"dark_gray\"},
            {\"text\":\"[+]\\\n\",\"color\":\"gold\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(virus.cmd_func("input_select_team"))\"}
            },
            
            {\"text\":\"Time: \",\"color\":\"dark_gray\"},
            {\"text\":\"[+]\",\"color\":\"dark_green\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(virus.cmd_func("input_add_one_min"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Adds 1 minute\",\"color\":\"dark_green\"}}
            },
            {\"text\":\" \",\"color\":\"gray\"},
            {\"text\":\"[+]\",\"color\":\"gold\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(virus.cmd_func("input_add_30s"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Adds 30 seconds\",\"color\":\"gold\"}}
            },
            {\"text\":\" \",\"color\":\"gray\"},
            {\"text\":\"[-]\",\"color\":\"gold\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(virus.cmd_func("input_remove_30s"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Removes 30 seconds\",\"color\":\"gold\"}}
            },
            {\"text\":\" \",\"color\":\"gray\"},
            {\"text\":\"[-]\",\"color\":\"red\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(virus.cmd_func("input_remove_one_min"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Removes 1 minute\",\"color\":\"red\"}}
            }
        ]"
    ]}