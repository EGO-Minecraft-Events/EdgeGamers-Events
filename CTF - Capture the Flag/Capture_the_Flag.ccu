// Capture the Flag

// Spawn Area
DEF $SA$ x=548,y=106,z=148,dx=20,dy=5,dz=21
DEF $ArenaArea$ x=535,y=0,z=129,dx=50,dy=256,dz=64
DEF $SpectatorArea$ x=542,y=100,z=136,dx=32,dy=5,dz=45
DEF COORDS $SpawnPoint$ 558 107 159 

DEF $DefaultTime$ 12000
DEF $DefaultWinScore$ 5
DEF $CountdownLength$ 200
DEF $FlagReturnTime$ 1200
DEF $FireworkInterval$ 20

DEF COORDS $CoordsOption$ 559 47 159
DEF $StyleDirection$ +X
DEF $BlockOption$ stonebrick 0
DEF $SPid$ 33
DEF $SPpvp$ 1
DEF $SPsat$ 1

DEF $CMDName$ CTF
DEF $CMDSizeX$ 16
DEF $CMDSizeY$ 16
DEF $CMDSizeZ$ 16

ARRAY {TELE Arr_RedSpawnPoints}:
	546 85 138 -45 0
	549 85 142 0 0
	551 85 140 0 0
	548 82 137 -90 0
	545 77 136 0 0
	544 79 140 -90 0
	548 81 141 0 0

ARRAY {TELE Arr_BlueSpawnPoints}:
	574 88 175 135 0
	569 86 175 180 0
	572 86 179 90 0
	576 83 172 135 0
	577 83 178 180 0
	573 83 182 90 0
	569 83 182 90 0

ARRAY {COORDS Arr_ResupplyPoints}:
	563 73 137
	571 71 152
	545 71 152
	553 73 182
	
DEF COORDS $RedFlagPos$ 549 85 140
DEF COORDS $BlueFlagPos$ 574 88 177

ARRAY {Teams}:
	CTFRed Red Team
	OPT color red
	OPT friendlyFire false
	OPT nametagVisibility hideForOtherTeams
	
	CTFBlue Blue Team
	OPT color Blue
	OPT friendlyFire false
	OPT nametagVisibility hideForOtherTeams
	
	CTFVRed Non-Player Red
	OPT color red
	
	CTFVBlue Non-Player Blue
	OPT color blue

DEF $RedWoolData$ 14
DEF $BlueWoolData$ 11

DEF $RedArmorColor$ 16711680
DEF $BlueArmorColor$ 255

DEF $RedFlag$ ArmorStand |0| |1| |2| {Tags:["CTFEntity","CTFRedFlag","CTFFlag"],Invulnerable:1b,NoBasePlate:1b,Small:1b,Marker:1b,ArmorItems:[{},{},{},{id:"wool",Count:1b,Damage:14}],HandItems:[{},{}],CustomName:"Red Flag",DisabledSlots:0,Glowing:1b,CustomNameVisible:1b,Invisible:1b}
DEF $BlueFlag$ ArmorStand |0| |1| |2| {Tags:["CTFEntity","CTFBlueFlag","CTFFlag"],Invulnerable:1b,NoBasePlate:1b,Small:1b,Marker:1b,ArmorItems:[{},{},{},{id:"wool",Count:1b,Damage:11}],HandItems:[{},{}],CustomName:"Blue Flag",DisabledSlots:0,Glowing:1b,CustomNameVisible:1b,Invisible:1b}

DEF $RedSpawn$ AreaEffectCloud |0| |1| |2| {Rotation:[|3|f,|4|f],Duration:MAX_INT,Tags:["CTFEntity","CTFRedSpawn","CTFSpawnPoint"]}
DEF $BlueSpawn$ AreaEffectCloud |0| |1| |2| {Rotation:[|3|f,|4|f],Duration:MAX_INT,Tags:["CTFEntity","CTFBlueSpawn","CTFSpawnPoint"]}
DEF $Resupply$ AreaEffectCloud |0| |1| |2| {Duration:MAX_INT,Tags:["CTFEntity","CTFResupply"]}

DEF $Firework$ FireworksRocketEntity |0| |1| |2| {LifeTime:$FireworkInterval$,Tags:["CTFEntity"]}

ARRAY {Arr_Summons}:
	AreaEffectCloud ~ ~ ~ {Duration:MAX_INT,Tags:["CTFEntity","CTFGame"]}
	AreaEffectCloud ~ ~ ~ {Duration:MAX_INT,Tags:["CTFEntity","CTFRedAoE","CTFTeamAoE"]}
	AreaEffectCloud ~ ~ ~ {Duration:MAX_INT,Tags:["CTFEntity","CTFBlueAoE","CTFTeamAoE"]}

DEF Snd_Hat minecraft:block.note.hat

ARRAY {Arr_ScoreboardObjectives}:
	CTFscore dummy Score
	CTFgen dummy General Purpose Registry
	CTFinv dummy Inventory
	CTFres dummy Resupply
	CTFflag dummy Flag Holders
	CTFdeath deathCount Death Count
	CTFleave stat.leaveGame Leave Game
	CTFEC dummy EC

IMPORT {LIBRARY GETDIR General}

DEF MAX_INT CALC(2 ^ 31 - 1)
DEF MIN_INT CALC(-(2 ^ 31))

OPTIONS:
	blockOption $BlockOption$
	coordsOption $CoordsOption$
	styleOption $StyleDirection$ 16
	
	parseOption true
	commandOption true
	combinerOption true

FUNC {Func_UpdateTime}:
	ScOP minutes CTFgen = time_left CTFgen
	ScOP minutes CTFgen /= ticks_per_minute CTFgen
	ScOP Minutes CTFscore = minutes CTFgen
	
	ScOP seconds CTFgen = time_left CTFgen
	ScOP seconds CTFgen %= ticks_per_minute CTFgen
	ScOP seconds CTFgen /= ticks_per_second CTFgen
	ScOP Seconds CTFscore = seconds CTFgen

FUNC {Func_Respawn}:
	clear @a[$SA$,m=2,team=CTFRed] minecraft:arrow
	give @a[$SA$,m=2,team=CTFRed] minecraft:arrow 32
	@a[$SA$,m=2,team=CTFRed] tp @p @r[type=AreaEffectCloud,CTFRedSpawn]
	
	clear @a[$SA$,m=2,team=CTFBlue] minecraft:arrow
	give @a[$SA$,m=2,team=CTFBlue] minecraft:arrow 32
	@a[$SA$,m=2,team=CTFBlue] tp @p @r[type=AreaEffectCloud,CTFBlueSpawn]
	
//// EC Book ////
GROUP {PULSE Grp_AddMinute}:
	Time CTFgen + 1200
	ScOP time_left CTFgen = Time CTFgen
	Func_UpdateTime()
	CCU_Deactivate(GSELF)

GROUP {PULSE Grp_RemoveMinute}:
	Time CTFgen - 1200
	ScOP time_left CTFgen = Time CTFgen
	Func_UpdateTime()
	CCU_Deactivate(GSELF)

LOOP {Red;Blue}:
	GROUP {PULSE Grp_MoveOneTo|0|}:
		IF {|0| = Red}:
			@r[team=CTFBlue] + CTFMoveTeam
		IF {|0| = Blue}:
			@r[team=CTFRed] + CTFMoveTeam
		tp @a[CTFMoveTeam] $SpawnPoint$
		@a[CTFMoveTeam] playsound Snd_TP voice @p
		@a[CTFMoveTeam] CTFflag = 0
		clear @a[CTFMoveTeam]
		J> CTF|0| @a[CTFMoveTeam]
		@a[CTFMoveTeam] - CTFMoveTeam
		CCU_Deactivate(GSELF)

GROUP {PULSE Grp_Shuffle}:
	@a[team=CTFRed,m=2] + CTFShuffle
	@a[team=CTFBlue,m=2] + CTFShuffle
	L> @a[CTFShuffle]
	tp @a[CTFShuffle] $SpawnPoint$
	@a[CTFShuffle] playsound Snd_TP voice @p
	@a[CTFShuffle] CTFflag = 0
	clear @a[CTFShuffle]
	CCU_Activate(Grp_Teams)
	@a[CTFShuffle] - CTFShuffle
	CCU_Deactivate(Grp_Shuffle)

// Allow players to join a game that is already running
GROUP {CLOCK Grp_AutoJoinGame}:
	CCU_Activate(Grp_Teams)

GROUP {PULSE Grp_Book}:
	@p[EC=0] SPbk = $SPid$
	CCU_Deactivate(GSELF)
	
//// Install ////

GROUP {PULSE Grp_Install}:
	CCU.spawnStandStart()
	
	CCU.teamAdd(Teams)
	CCU.objectiveAdd(Arr_ScoreboardObjectives)
	
	ticks_per_second CTFgen = 20
	ticks_per_minute CTFgen = 1200
	
	//< Prettify and fill the CTFscore scoreboard
	J> CTFVRed Red
	J> CTFVBlue Blue
	
	Win_Score CTFscore = $DefaultWinScore$
	Red CTFscore = 0
	Blue CTFscore = 0
	Minutes CTFscore = 0
	Seconds CTFscore = 0
	
	Time CTFgen = $DefaultTime$
	ScOP time_left CTFgen = Time CTFgen
	time_left CTFgen + 0
	minutes CTFgen + 0
	seconds CTFgen + 0
	
	Func_UpdateTime()
	
	ScSS CTFscore
	//>
	
	LOOP {Arr_Summons[S]}:
		summon |0|
	
	LOOP {Arr_RedSpawnPoints[S]}:
		SPLIT {Arr_SpawnPoint |0|} { }
		summon $RedSpawn$(Arr_SpawnPoint[S])
	LOOP {Arr_BlueSpawnPoints[S]}:
		SPLIT {Arr_SpawnPoint |0|} { }
		summon $BlueSpawn$(Arr_SpawnPoint[S])
	UNASSIGN {ARRAY Arr_SpawnPoint}
	
	LOOP {Arr_ResupplyPoints[S]}:
		SPLIT {Arr_ResupplyPoint |0|} { }
		summon $Resupply$(Arr_ResupplyPoint[S])
	UNASSIGN {ARRAY Arr_ResupplyPoint}
	
	CCU_Activate(Grp_Main)
	CCU_Activate(Grp_Lobby)
	
	gamerule naturalRegeneration false

//// Main ////

FUNC {Func_RefreshArmor}:
	@a[$ArenaArea$,m=2] + CTFHas|0|Helm {Inventory:[{id:"minecraft:leather_helmet",Slot:103b,tag:{display:{color:|1|},Unbreakable:1}}]}
	@a[$ArenaArea$,m=2] + CTFHas|0|Chest {Inventory:[{id:"minecraft:leather_chestplate",Slot:102b,tag:{display:{color:|1|},Unbreakable:1}}]}
	@a[$ArenaArea$,m=2] + CTFHas|0|Legs {Inventory:[{id:"minecraft:leather_leggings",Slot:101b,tag:{display:{color:|1|},Unbreakable:1}}]}
	@a[$ArenaArea$,m=2] + CTFHas|0|Feet {Inventory:[{id:"minecraft:leather_boots",Slot:100b,tag:{display:{color:|1|},Unbreakable:1}}]}
	
	replaceitem entity @a[$ArenaArea$,m=2,team=CTF|0|,!CTFHas|0|Helm] slot.armor.head minecraft:leather_helmet 1 0 {display:{color:|1|},Unbreakable:1}
	replaceitem entity @a[$ArenaArea$,m=2,team=CTF|0|,!CTFHas|0|Chest] slot.armor.chest minecraft:leather_chestplate 1 0 {display:{color:|1|},Unbreakable:1}
	replaceitem entity @a[$ArenaArea$,m=2,team=CTF|0|,!CTFHas|0|Legs] slot.armor.legs minecraft:leather_leggings 1 0 {display:{color:|1|},Unbreakable:1}
	replaceitem entity @a[$ArenaArea$,m=2,team=CTF|0|,!CTFHas|0|Feet] slot.armor.feet minecraft:leather_boots 1 0 {display:{color:|1|},Unbreakable:1}
	
	@a[CTFHas|0|Helm] - CTFHas|0|Helm
	@a[CTFHas|0|Chest] - CTFHas|0|Chest
	@a[CTFHas|0|Legs] - CTFHas|0|Legs
	@a[CTFHas|0|Feet] - CTFHas|0|Feet


FUNC {Func_RefreshInventory}:
	@a[$ArenaArea$,m=2] CTFinv + 0
	@a[$ArenaArea$,m=2] stats entity @p set AffectedItems @p CTFinv
	
	@a[$ArenaArea$,m=2,team=CTFRed] clear @p minecraft:iron_sword -1 0
	@a[$ArenaArea$,m=2,team=CTFBlue] clear @p minecraft:iron_sword -1 0
	give @a[$ArenaArea$,m=2,CTFinv=0,team=CTFRed] minecraft:iron_sword 1 0 {Unbreakable:1}
	give @a[$ArenaArea$,m=2,CTFinv=0,team=CTFBlue] minecraft:iron_sword 1 0 {Unbreakable:1}
	
	@a[$ArenaArea$,m=2,team=CTFRed] clear @p minecraft:bow -1 0
	@a[$ArenaArea$,m=2,team=CTFBlue] clear @p minecraft:bow -1 0
	give @a[$ArenaArea$,m=2,CTFinv=0,team=CTFRed] minecraft:bow 1 0 {Unbreakable:1}
	give @a[$ArenaArea$,m=2,CTFinv=0,team=CTFBlue] minecraft:bow 1 0 {Unbreakable:1}
	
	// Red Team
	
	@a[$ArenaArea$,m=2,team=CTFRed] clear @p minecraft:wool $RedWoolData$ 0
	replaceitem entity @a[$ArenaArea$,m=2,team=CTFRed,CTFinv=0] slot.hotbar.8 minecraft:wool 1 $RedWoolData$ {display:{Name:"Your Team"}}
	
	// Blue Team
	
	@a[$ArenaArea$,m=2,team=CTFBlue] clear @p minecraft:wool $BlueWoolData$ 0
	replaceitem entity @a[$ArenaArea$,m=2,team=CTFBlue,CTFinv=0] slot.hotbar.8 minecraft:wool 1 $BlueWoolData$ {display:{Name:"Your Team"}}
	
	// Red Team Flag Holder
	
	@a[$ArenaArea$,m=2,team=CTFRed,CTFflag=0] clear @p minecraft:wool $BlueWoolData$
	@a[$ArenaArea$,m=2,team=CTFRed,CTFflag=1] clear @p minecraft:wool $BlueWoolData$ 0
	replaceitem entity @a[$ArenaArea$,m=2,team=CTFRed,CTFinv=0,CTFflag=1] slot.hotbar.4 minecraft:wool 1 $BlueWoolData$ {display:{Name:"Enemy Flag"},ench:[{id:19,lvl:1}]}
	
	// Blue Team Flag Holder
	
	@a[$ArenaArea$,m=2,team=CTFBlue,CTFflag=0] clear @p minecraft:wool $RedWoolData$
	@a[$ArenaArea$,m=2,team=CTFBlue,CTFflag=1] clear @p minecraft:wool $RedWoolData$ 0
	replaceitem entity @a[$ArenaArea$,m=2,team=CTFBlue,CTFinv=0,CTFflag=1] slot.hotbar.4 minecraft:wool 1 $RedWoolData$ {display:{Name:"Enemy Flag"},ench:[{id:19,lvl:1}]}
	
	// Armor
	
	Func_RefreshArmor(Red;$RedArmorColor$)
	Func_RefreshArmor(Blue;$BlueArmorColor$)
	
	// EC
	@a[$ArenaArea$] CTFEC = 1 {Inventory:[{id:"minecraft:written_book",tag:{title:"Capture the Flag",author:"Icohedron"}}]}
	@a[$ArenaArea$,CTFEC=1] clear @p minecraft:written_book -1 0 {title:"Capture the Flag",author:"Icohedron"}
	@a[$ArenaArea$,CTFEC=1,CTFinv=0] SPbk = $SPid$
	
GROUP {CLOCK Grp_Main}:
	Func_UpdateTime()
	
	// Passive Regen
	regeneration CTFgen + 0
	regeneration CTFgen ? MIN_INT 0
	COND:
		effect @a[$ArenaArea$,m=2] minecraft:regeneration 3 0 true
		regeneration CTFgen = 50
	regeneration CTFgen - 1
	
	// Particles where the flags spawn
	particle reddust $RedFlagPos$[x, y + 0.8, z] 1 0 0 1 0
	particle reddust $BlueFlagPos$[x, y + 0.8, z] 0.001 0 1 1 0
	
	// Enable auto-joining game
	// CCU_Activate(Grp_Teams)
	
	Func_RefreshInventory()

//// Lobby ////

GROUP {CLOCK Grp_Lobby}:
	CCU.spawnIDSetting()
	kill @e[$SA$,type=Item]
	effect @a[$SA$,m=2] minecraft:weakness 1 100 true
	effect @a[$SA$,m=2] minecraft:instant_health 1 2 true
	clear @a[$SA$,m=2] minecraft:arrow
	
	kill @e[$SpectatorArea$,type=Item]
	L> @a[$SpectatorArea$]
	effect @a[$SpectatorArea$,m=2] minecraft:weakness 1 100 true
	effect @a[$SpectatorArea$,m=2] minecraft:instant_health 1 2 true
	
	USE {clear @a[$SpectatorArea$,m=2]}:
		minecraft:iron_sword
		minecraft:bow
		minecraft:arrow
		minecraft:leather_helmet
		minecraft:leather_chestplate
		minecraft:leather_leggings
		minecraft:leather_boots
		minecraft:wool

//// Start ////

GROUP {PULSE Grp_Start}:
	// Summon flags
	@a[CTFflag=1] CTFflag = 0
	
	SPLIT {Arr_Coords $RedFlagPos$} { }
	Red CTFscore = 0
	$red_base CTFflag = 1
	summon $RedFlag$(Arr_Coords[S])
	J> CTFVRed @e[CTFRedFlag]
	UNASSIGN {ARRAY Arr_Coords}
	
	SPLIT {Arr_Coords $BlueFlagPos$} { }
	Blue CTFscore = 0
	$blue_base CTFflag = 1
	summon $BlueFlag$(Arr_Coords[S])
	J> CTFVBlue @e[CTFBlueFlag]
	UNASSIGN {ARRAY Arr_Coords}
	
	// Set the timers for the round and warmup
	
	ScOP time_left CTFgen = Time CTFgen
	countdown CTFgen = $CountdownLength$
	
	Func_UpdateTime()
	
	CCU_Activate(Grp_Warmup)
	CCU_Deactivate(GSELF)

//// Warmup ////

GROUP {CLOCK Grp_Warmup}:
	kill @e[$ArenaArea$,type=Item]
	kill @e[$ArenaArea$,type=XPOrb]
	kill @e[$ArenaArea$,type=Arrow]
	
	Func_Respawn()
	
	// Keep players in team spawn

	@e[CTFSpawnPoint] @a[r=5] + CTFInSpawn
	@a[$ArenaArea$,!CTFInSpawn,team=CTFRed,m=2] tp @p @e[CTFRedSpawn,c=1]
	@a[$ArenaArea$,!CTFInSpawn,team=CTFRed,m=2] playsound Snd_TP voice @p
	@a[$ArenaArea$,!CTFInSpawn,team=CTFBlue,m=2] tp @p @e[CTFBlueSpawn,c=1]
	@a[$ArenaArea$,!CTFInSpawn,team=CTFBlue,m=2] playsound Snd_TP voice @p
	@a[CTFInSpawn] - CTFInSpawn
	
	countdown CTFgen ? 200 200
	COND:
		tellraw @a[$ArenaArea$] ["","Capture the flag begins in 10 seconds"]
		@a[$ArenaArea$] playsound Snd_Pling voice @p
		
	countdown CTFgen ? 100 100
	COND:
		tellraw @a[$ArenaArea$] ["","5"]
		@a[$ArenaArea$] playsound Snd_Pling voice @p
		
	countdown CTFgen ? 80 80
	COND:
		tellraw @a[$ArenaArea$] ["","4"]
		@a[$ArenaArea$] playsound Snd_Pling voice @p
	
	countdown CTFgen ? 60 60
	COND:
		tellraw @a[$ArenaArea$] ["","3"]
		@a[$ArenaArea$] playsound Snd_Pling voice @p
		
	countdown CTFgen ? 40 40
	COND:
		tellraw @a[$ArenaArea$] ["","2"]
		@a[$ArenaArea$] playsound Snd_Pling voice @p
		
	countdown CTFgen ? 20 20
	COND:
		tellraw @a[$ArenaArea$] ["","1"]
		@a[$ArenaArea$] playsound Snd_Pling voice @p
	
	countdown CTFgen ? 0 0
	COND:
		tellraw @a[$ArenaArea$] ["","Begin!"]
		@a[$ArenaArea$] playsound Snd_Wither voice @p
		CCU_Activate(Grp_Play)
		CCU_Deactivate(GSELF)
		
	countdown CTFgen - 1

//// Play ////

FUNC {Func_Resupply}:
	@a[$ArenaArea$] CTFres + 0
	@e[CTFResupply] effect @a[CTFres=0,r=1,m=2] minecraft:instant_health 1 0
	@e[CTFResupply] clear @a[CTFres=0,r=1,m=2] minecraft:arrow
	@e[CTFResupply] give @a[CTFres=0,r=1,m=2] minecraft:arrow 32
	@e[CTFResupply] @a[CTFres=0,r=1,m=2] CTFres = 60
	@a[CTFres>0] CTFres - 1
	
FUNC {Func_UpdateFlags}:
	// Flag holder debuffs
	effect @a[CTFflag=1] minecraft:glowing 1
	effect @a[CTFflag=1] minecraft:slowness 1 1

	// Drop the flag on death
	@a[CTFflag=1,CTFdeath>0] + CTFDropFlag
	@a[CTFDropFlag] CTFflag = 0
	
	USE {@a[CTFDropFlag,team=CTFRed]}:
		summon $BlueFlag$(~;~;~)
		J> CTFVBlue @e[CTFBlueFlag]
		tellraw @a[$ArenaArea$] [{"selector":"@a[CTFDropFlag,team=CTFRed]"}, " has dropped the ", {"text":"Blue","color":"blue"}, " flag!"]
		@a[$ArenaArea$] playsound Snd_Hat voice @p
		$blue_base CTFflag = 1
		blue_flag_reset CTFgen = $FlagReturnTime$
	
	USE {@a[CTFDropFlag,team=CTFBlue]}:
		summon $RedFlag$(~;~;~)
		J> CTFVRed @e[CTFRedFlag]
		tellraw @a[$ArenaArea$] [{"selector":"@a[CTFDropFlag,team=CTFBlue]"}, " has dropped the ", {"text":"Red","color":"red"}, " flag!"]
		@a[$ArenaArea$] playsound Snd_Hat voice @p
		$red_base CTFflag = 1
		red_flag_reset CTFgen = $FlagReturnTime$
	
	@a[CTFDropFlag] - CTFDropFlag
	@a[CTFdeath>0] CTFdeath = 0
	
	// Return the flag if the player disconnected while holding the flag
	@a[CTFflag=1,CTFleave>0] CTFflag = 0
	@a[CTFleave>0] CTFleave = 0
	
	// Return the flag if the player changed game mode while holding the flag
	@a[$ArenaArea$,m=2] + CTFGoodGM
	@a[CTFflag=1,!CTFGoodGM] CTFflag = 0
	@a[CTFGoodGM] - CTFGoodGM
	
	// Return the flag if the player went out of bounds with the flag
	@a[$ArenaArea$] + CTFInArena
	@a[CTFflag=1,!CTFInArena] CTFflag = 0
	@a[CTFInArena] - CTFInArena
	
	// Give the flag to the ones who pick it up
	@e[CTFRedFlag] @p[team=CTFBlue,r=1,m=2] + CTFPickupRedFlag
	USE {@a[CTFPickupRedFlag]}:
		kill @e[CTFRedFlag]
		$red_base CTFflag = 0
		tellraw @a[$ArenaArea$] [{"selector":"@a[CTFPickupRedFlag]"}, " has taken the ", {"text":"Red","color":"red"}, " flag!"]
		@a[$ArenaArea$,team=CTFBlue] playsound Snd_Item voice @p
		@a[$ArenaArea$,team=CTFRed] playsound Snd_Hat voice @p
		red_flag_reset = -1
		CTFflag = 1
		- CTFPickupRedFlag
	
	
	@e[CTFBlueFlag] @p[team=CTFRed,r=1,m=2] + CTFPickupBlueFlag
	USE {@a[CTFPickupBlueFlag]}:
		kill @e[CTFBlueFlag]
		$blue_base CTFflag = 0
		tellraw @a[$ArenaArea$] [{"selector":"@a[CTFPickupBlueFlag]"}, " has taken the ", {"text":"Blue","color":"blue"}, " flag!"]
		@a[$ArenaArea$,team=CTFRed] playsound Snd_Item voice @p
		@a[$ArenaArea$,team=CTFBlue] playsound Snd_Hat voice @p
		blue_flag_reset = -1
		CTFflag = 1
		- CTFPickupBlueFlag
	
	// Flag returns and verifying flag holders
	LOOP {red;blue}:
		$|0|_base CTFflag + 0
		|0|_flag_verify CTFgen = 0
		
		$|0|_base CTFflag ? 1 MAX_INT
		COND:
			|0|_flag_verify CTFgen = 1
	
		$|0|_base CTFflag ? 0 0
		COND:
			// The base does not have the flag. Verify that a player has it instead
			IF {|0| = red}:
				@a[CTFflag=1,team=CTFBlue] |0|_flag_verify CTFgen = 1
			IF {|0| = blue}:
				@a[CTFflag=1,team=CTFRed] |0|_flag_verify CTFgen = 1
		
		|0|_flag_verify CTFgen ? 0 0
		COND:
			$|0|_base CTFflag = 1
			// No player completed the verification request. Return the flag to the base
			IF {|0| = red}:
				red_flag_reset CTFgen = -1
				tellraw @a[$ArenaArea$] ["The ", {"text":"Red","color":"red"}, " flag has been returned to its base!"]
			IF {|0| = blue}:
				blue_flag_reset CTFgen = -1
				tellraw @a[$ArenaArea$] ["The ", {"text":"Blue","color":"blue"}, " flag has been returned to its base!"]
			@a[$ArenaArea$] playsound Snd_XP voice @p
	
	// Flag reset timers
	LOOP {red;blue}:
		|0|_flag_reset CTFgen ? 0 0
		COND:
			IF {|0| = red}:
				kill @e[CTFRedFlag]
				tellraw @a[$ArenaArea$] ["The ", {"text":"Red","color":"red"}, " flag has been returned to its base!"]
			IF {|0| = blue}:
				kill @e[CTFBlueFlag]
				tellraw @a[$ArenaArea$] ["The ", {"text":"Blue","color":"blue"}, " flag has been returned to its base!"]
			@a[$ArenaArea$] playsound Snd_XP voice @p
		|0|_flag_reset CTFgen - 1
	
	// Create a flag if needed
	LOOP {red;blue}:
		create_flag CTFgen = 0
		$|0|_base CTFflag ? 1 MAX_INT
		COND:
			create_flag CTFgen = 1
		
		|0|_flag_exists CTFgen = 0
		IF {|0| = red}:
			@e[CTFRedFlag] red_flag_exists CTFgen = 1
		IF {|0| = blue}:
			@e[CTFBlueFlag] blue_flag_exists CTFgen = 1
			
		|0|_flag_exists CTFgen ? 0 0
		COND:
			create_flag CTFgen ? 1 MAX_INT
			IF {|0| = red}:
				SPLIT {Arr_Coords $RedFlagPos$} { }
				summon $RedFlag$(Arr_Coords[S])
				UNASSIGN {ARRAY Arr_Coords}
				J> CTFVRed @e[CTFRedFlag]
			IF {|0| = blue}:
				SPLIT {Arr_Coords $BlueFlagPos$} { }
				summon $BlueFlag$(Arr_Coords[S])
				UNASSIGN {ARRAY Arr_Coords}
				J> CTFVBlue @e[CTFBlueFlag]
	
	// Capturing the flag
	LOOP {Red;Blue}:
		SPLIT {Arr_Coords $|0|FlagPos$} { }
		DEF $FlagPosSelector$ x=Arr_Coords[0],y=Arr_Coords[1],z=Arr_Coords[2],dy=1
		effect @a[$FlagPosSelector$,CTFflag=1,team=CTF|0|] minecraft:glowing 0
		effect @a[$FlagPosSelector$,CTFflag=1,team=CTF|0|] minecraft:slowness 0
		
		IF {|0| = Red}:
			USE {@a[$FlagPosSelector$,CTFflag=1,team=CTF|0|]}:
				tellraw @a[$ArenaArea$] [{"selector":"@p"}, " has successfully captured the ", {"text":"Blue","color":"blue"}, " flag!"]
				@a[$ArenaArea$,team=CTFRed] playsound Snd_Level voice @p
				@a[$ArenaArea$,team=CTFBlue] playsound Snd_Hat voice @p
				$blue_base CTFflag = 1
		IF {|0| = Blue}:
			USE {@a[$FlagPosSelector$,CTFflag=1,team=CTF|0|]}:
				tellraw @a[$ArenaArea$] [{"selector":"@p"}, " has successfully captured the ", {"text":"Red","color":"red"}, " flag!"]
				@a[$ArenaArea$,team=CTFBlue] playsound Snd_Level voice @p
				@a[$ArenaArea$,team=CTFRed] playsound Snd_Hat voice @p
				$red_base CTFflag = 1
			
		@a[$FlagPosSelector$,CTFflag=1,team=CTF|0|] |0| CTFscore + 1
		@a[$FlagPosSelector$,CTFflag=1,team=CTF|0|] CTFflag = 0
		UNASSIGN {DEF $FlagPosSelector$}
		UNASSIGN {ARRAY Arr_Coords}

FUNC {Func_CheckScore}:
	score_reached CTFgen + 0
	LOOP {red;blue}:
		|0|_score_check CTFgen + 0
		IF {|0| = red}:
			ScOP |0|_score_check CTFgen = Red CTFscore
		IF {|0| = blue}:
			ScOP |0|_score_check CTFgen = Blue CTFscore
		ScOP |0|_score_check CTFgen -= Win_Score CTFscore
		|0|_score_check CTFgen ? 0 MAX_INT
		COND:
			score_reached CTFgen = 1

GROUP {CLOCK Grp_Play}:
	kill @e[$ArenaArea$,type=Item]
	kill @e[$ArenaArea$,type=XPOrb]
	
	@e[$ArenaArea$,type=Arrow] + CTFInGround {inGround:1b}
	kill @e[CTFInGround]
	
	Func_Respawn()
	Func_Resupply()
	Func_UpdateFlags()
	Func_CheckScore()
	
	score_reached CTFgen ? 1 MAX_INT
	COND:
		score_reached CTFgen = 0
		CCU_Activate(Grp_Evaluation)
		finish_timer CTFgen = $CountdownLength$
		CCU_Activate(Grp_Finish)
		CCU_Deactivate(GSELF)
	
	time_left CTFgen ? 0 0
	COND:
		CCU_Activate(Grp_Evaluation)
		finish_timer CTFgen = $CountdownLength$
		CCU_Activate(Grp_Finish)
		CCU_Deactivate(GSELF)
	
	time_left CTFgen - 1

//// Evaluate Winners ////

GROUP {PULSE Grp_Evaluation}:
	winner_evaluation CTFgen = 0
	ScOP winner_evaluation CTFgen = Red CTFscore
	ScOP winner_evaluation CTFgen -= Blue CTFscore
	
	winner_evaluation CTFgen ? 0 0
	COND:
		tellraw @a[$ArenaArea$] ["Stalemate!"]
		title @a[$ArenaArea$] subtitle ["Stalemate!"]
		
	winner_evaluation CTFgen ? 1 MAX_INT
	COND:
		tellraw @a[$ArenaArea$] ["", {"text":"Red","color":"red"}, " team has won!"]
		tellraw @a[$ArenaArea$] ["", "Players on ", {"text":"Red","color":"red"}, " team: ", {"selector":"@a[$ArenaArea$,m=2,team=CTFRed]"}]
		title @a[$ArenaArea$] subtitle ["", {"text":"Red","color":"red"}, " team has won!"]
	
	winner_evaluation CTFgen ? MIN_INT -1
	COND:
		tellraw @a[$ArenaArea$] ["", {"text":"Blue","color":"blue"}, " team has won!"]
		tellraw @a[$ArenaArea$] ["", "Players on ", {"text":"Blue","color":"blue"}, " team: ", {"selector":"@a[$ArenaArea$,m=2,team=CTFBlue]"}]
		title @a[$ArenaArea$] subtitle ["", {"text":"Blue","color":"blue"}, " team has won!"]
		
	title @a[$ArenaArea$] title [""]
	
	CCU_Deactivate(GSELF)

//// Finish ////

GROUP {CLOCK Grp_Finish}:
	firework_timer CTFgen + 0
	kill @e[$ArenaArea$,type=Item]
	kill @e[$ArenaArea$,type=XPOrb]
	kill @e[$ArenaArea$,type=Arrow]
	
	effect @a[$ArenaArea$,m=2] minecraft:weakness 1 100 true
	effect @a[$ArenaArea$,m=2] minecraft:instant_health 1 2 true
	clear @a[$ArenaArea$,m=2] minecraft:arrow
	
	winner_evaluation CTFgen ? 1 MAX_INT
	COND:
		firework_timer CTFgen ? 0 0
		@a[$ArenaArea$,team=CTFRed] summon $Firework$(~;~3;~)
		firework_timer CTFgen = $FireworkInterval$
	
	winner_evaluation CTFgen ? MIN_INT -1
	COND:
		firework_timer CTFgen ? 0 0
		@a[$ArenaArea$,team=CTFBlue] summon $Firework$(~;~3;~)
		firework_timer CTFgen = $FireworkInterval$
	
	finish_timer CTFgen ? 0 0
	COND:
		CCU_Activate(Grp_Reset)
		CCU_Deactivate(GSELF)
	
	firework_timer CTFgen - 1
	finish_timer CTFgen - 1

//// Reset ////

GROUP {PULSE Grp_Reset}:
	tp @a[$ArenaArea$,team=CTFRed] $SpawnPoint$ 
	tp @a[$ArenaArea$,team=CTFBlue] $SpawnPoint$ 
	red_flag_reset CTFgen = -1
	blue_flag_reset CTFgen = -1
	kill @e[CTFFlag]
	CCU_Deactivate(GSELF)
	
//// Assign Teams ////

GROUP {CLOCK Grp_Teams}:
	L> @a[$SA$,team=NoPVP]
	red_minus_blue_count CTFgen + 0
	red_count CTFgen = 0
	blue_count CTFgen = 0
	
	@a[team=CTFRed,m=2] red_count CTFgen + 1
	@a[team=CTFBlue,m=2] blue_count CTFgen + 1

	ScOP red_minus_blue_count CTFgen = red_count CTFgen
	ScOP red_minus_blue_count CTFgen -= blue_count CTFgen
	
	red_minus_blue_count CTFgen ? 0 0
	COND:
		@r[$SA$,m=2,team=] + CTFJoinRandom
		
	@r[type=AreaEffectCloud,CTFTeamAoE,c=1] + CTFRandTeamAoE
	@e[CTFRandTeamAoE] @e[CTFRedAoE,r=0,c=1] J> CTFRed @a[CTFJoinRandom]
	@e[CTFRandTeamAoE] @e[CTFBlueAoE,r=0,c=1] J> CTFBlue @a[CTFJoinRandom]
	@e[CTFRandTeamAoE] - CTFRandTeamAoE
	@a[CTFJoinRandom] - CTFJoinRandom
	
	
	red_minus_blue_count CTFgen ? 1 MAX_INT
	COND:
		J> CTFBlue @r[$SA$,m=2,team=]
	
	red_minus_blue_count CTFgen ? MIN_INT -1
	COND:
		J> CTFRed @r[$SA$,m=2,team=]
	
	
	CCU_Deactivate(GSELF)
	
	testfor @a[$SA$,m=2,team=]
	COND:
		CCU_Activate(GSELF)

//// Uninstall ////

GROUP {PULSE Grp_Uninstall}:
	CCU.spawnStandEnd()
	
	CCU.teamRemove(Teams)
	CCU.objectiveRemove(Arr_ScoreboardObjectives)
	
	kill @e[CTFEntity]
	
	USE {clear @a[$ArenaArea$]}:
		minecraft:iron_sword
		minecraft:bow
		minecraft:arrow
		minecraft:leather_helmet
		minecraft:leather_chestplate
		minecraft:
		minecraft:leather_boots
		minecraft:wool
	
	LOOP {Grp_Install;Grp_Main;Grp_Lobby;Grp_Start;Grp_Warmup;Grp_Play;Grp_Finish;Grp_AutoJoinGame;Grp_Teams;GSELF}:
		CCU_Deactivate(|0|)
	
	gamerule naturalRegeneration true

//// Command Save ////

FINALIZE:
	CCU.cmdSave()




