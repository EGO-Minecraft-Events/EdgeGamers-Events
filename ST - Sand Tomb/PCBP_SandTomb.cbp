//#-p stonebrick 0 -b -220 5 31 -print
///	C:\Users\Austin\Documents\Austin\powder game code\eGO\Events & Commands\ST - Sand Tomb\CBP_SandTomb.cbp

//<Defines
DEFINE $SA$ x=-279,y=4,z=5,dx=83,dy=50,dz=44
DEFINE $SPid$ 14
DEFINE $SPpvp$ 1
DEFINE $SPsat$ 0

DEFINE $CMDName$ ST
DEFINE $CMDSizeX$ 16
DEFINE $CMDSizeY$ 16
DEFINE $CMDSizeZ$ 16

DEFINE $TextStart$ {"text":"","extra":[{"text":"[","color":"gray"},{"text":"ST","color":"yellow","bold":"true","hoverEvent":{"action":"show_text","value":{"text":"Sand Tomb","color":"yellow"}}},{"text":"]","color":"gray"},{"text":": "},

PCBPIMPORT PCBPI_PosZ.txt

DEFINE CCItem_Potion potion |0| |1| {CustomPotionEffects:[{Id:8,Amplifier:6,Duration:72000}],Potion:"leaping",HideFlags:127,display:{Name:"Kangaroo potion",Lore:["One time use potion","Use wisely!"]}}

///	Select Arenas
DEFINE $SAAF$ x=-272,y=4,z=35,dx=44,dy=45,dz=6
DEFINE $SACL$ x=-222,y=4,z=12,dx=20,dy=40,dz=13

///	Particle coordinates
DEFINE $TpParticleAF$ -252 26.25 27
DEFINE $TpParticleCL$ -237 26.25 18
///	Select for the portal
DEFINE $TpSelectAF$ x=-253,y=25,z=27,dx=2,dy=2
DEFINE $TpSelectCL$ x=-237,y=25,z=17,dy=2,dz=2
///	Door bar
DEFINE $TpDoorAF$ -251 26 27 -253 26 27
DEFINE $TpDoorCL$ -237 26 17 -237 26 19
///	Teleport to the arena
DEFINE $TpToAF$ -249 5 37.0
DEFINE $TpToCL$ -216 4 18

///	Select winners
DEFINE $WinSelectAF$ x=-272,y=40,z=40,dx=44
DEFINE $WinSelectCL$ x=-207,y=11,z=16,dy=1,dz=3
///	Pre select winners
DEFINE $WinPreSelectAF$ x=-272,y=41,z=40,dx=44,dy=1
DEFINE $WinPreSelectCL$ x=-207,y=13,z=16,dy=21,dz=3

///	Sand block area
DEFINE $SBlockAreaAF$ -273 42 36 -273 42 37
DEFINE $SBlockAreaCL$ -207 40 18 -207 40 18

///	Clearing the sand in the arena
DEFINE $SandAreaAF$ -229 41 36 -271 5 37
DEFINE $SandAreaCL$ -211 4 13 -221 33 23

///	Select spawn
DEFINE $SpawnArea$ x=-250,y=25,z=18,r=1
///	Teleport to spawn
DEFINE $SpawnCoords$ -250 25 18 -90 0
///	Firework
DEFINE $Firework$ FireworksRocketEntity ~ ~ ~ {LifeTime:20,FireworksItem:{id:fireworks,Count:1,tag:{Fireworks:{Explosions:[{Type:0,Flicker:1,Trail:0,Colors:[41728,65280],FadeColors:[2883328]}]}}}}
///	Spawnpoint
DEFINE $SPid$ 14
//>

//<Comments
FUNC Func_Comments
Groups:
	CL:
	Teleport start/
	Teleport clock/
	Teleport end/
	Sign setting x2
	Main start
	Main clock
	Main end
	Countdown
	
For all functions:
	|0| = Ant Farm or Classic
	|1| = Ant_Farm or Classic
	|2| = AF or CL
	|3| = Opposite of |2|
END
//>


//<Portal Start
FUNC Func_PortalStart
	USE $ObjA$
		ST dummy Sand Tomb
		STcl dummy Sand Tomb Calc
	UNUSE
	
	USE $TeamA$
		STd_dg
		STd_g
	UNUSE
	
	USE $TeamO$
		STd_dg color dark_green
		STd_g color green
	UNUSE
	
	scoreboard objectives setdisplay sidebar ST
	\/spawn ST = 1
	title @a[$SA$] title {"text":"|0| Arena","color":"green"}
	title @a[$SA$] subtitle {"text":"The door is now open","color":"green"}
	
	|1| ST = 0
	fill $TpDoor|2|$ air 0
	ScTJ STd_dg |1|
	ScTJ STd_g /spawn
	@a[$SA$] playsound minecraft:entity.experience_orb.touch voice @a[c=1]
	
	@e[type=ArmorStand,SpawnStand] SPtp = $SPid$
	
	BothArenas STcl = 0
	|2| STcl = 1
	ScTE |3| STcl 1 1
	CONDITION
		BothArenas STcl = 1
END
//>

//<Portal Clock
FUNC Func_PortalClock
	particle enchantmenttable $TpParticle|2|$ 0 0 0 1 4 force
	$s$gamemode 2 @a[$TpSelect|2|$,m=0]
	ScTG @a[$TpSelect|2|$,m=2] add STtp
	$s$tp @a[$TpSelect|2|$,m=2,STtp] $TpTo|2|$
	@a[$SA|2|$,m=2,STtp] playsound minecraft:entity.endermen.teleport voice @a[STtp,r=0,c=1]
	ScTG @a[$SA|2|$,m=2,STtp] remove STtp
END
//>

//<Portal End
FUNC Func_PortalEnd
	ScTE BothArenas STcl 0 0 
	CONSTART
		USE $ObjR$
			ST
			STcl
		UNUSE
		
		USE $TeamR$
			STd_dg
			STd_g
		UNUSE
		
		@e[type=ArmorStand,SpawnStand] SPtp = 0
		
		Activate(CBPPortalEnd|3|)
	CONEND
	
	ScRE |1| ST
	fill $TpDoor|2|$ stone_slab 1
	ScTG * remove STtp
	
	BothArenas STcl = 0
END
//>


//<General Scoreboard Start
FUNC Func_GeneralScoreboardStart
///	This is meant to be played with 2 arenas
///	Resets SPgac
	@e[type=ArmorStand,SpawnStand,SPgam=$SPid$] SPgac = 0
	
	USE $ObjA$
		NumberObjAdd
		ST|2|de deathCount Sand Tomb Deaths
		ST|2|pl dummy Sand Tomb Player List
		ST|2|as dummy Sand Tomb Armor Stands
		ST|2|ti dummy Sand Tomb Timer
		ST|2|po dummy Sand Tomb Potion
		ST|2|ju stat.jump Sand Tomb Jump
		STki dummy Sand Tomb Suffocation
		STdi dummy Sand Tomb
		STba dummy Sand Tomb Both Arenas
	UNUSE
	scoreboard objectives setdisplay sidebar STdi
	
	USE $TeamA$
		ST|2|p
		ST|2|d_y
		ST|2|d_g
	UNUSE
	
	USE $TeamO$
		ST|2|p color yellow
		ST|2|p friendlyfire false
		
		ST|2|d_y color yellow
		ST|2|d_g color green
	UNUSE
	
	NumberAdd(20)
	
	$s$clear @a[$SA|2|$,m=2]
	effect @a[$SA|2|$,m=2] clear
	
///	Summon Entities
	summon ArmorStand ~ ~ ~ {Tags:["ST","STAS","ST|2|","ST|2|AS"],Invulnerable:1,NoGravity:1,CustomNameVisible:0,Invisible:1}
	summon AreaEffectCloud ~ ~ ~ {Tags:["ST","STCD","ST|2|",ST|2|CD],Duration:130}
	
///	General
	@e[type=ArmorStand,ST|2|AS] ST|2|as + 0
	@a[$SA|2|$,m=2] ST|2|pl + 0
	@a[$SA|2|$,m=2] STki = 0
	ScTJ ST|2|p @a[$SA|2|$,m=2]
	ScTJ ST|2|d_g (|2|_1st_place) (|2|_2nd_place) (|2|_3rd_place)
	ScTJ ST|2|d_y |2|Players:
	
///	Countdown beginning
	
	
///	Potion start
	ScTE Potions|2| STSave 3 3
	CONSTART
		$s$give @a[$SA|2|$,m=2,ST|2|pl=0] CCItem_Potion(1;0)
	CONEND

///	Collision start
	ScTE Collisions|2| STSave 3 3
	CONDITION
		scoreboard teams option ST|2|p collisionRule pushOtherTeams
	ScTE Collisions|2| STSave 1 1
	CONDITION
		scoreboard teams option ST|2|p collisionRule never
	@a[$SA$,EC=0] SPbk = $SPid$
END
//>

//<Countdown Clock
FUNC Func_CountdownClock
	@e[type=AreaEffectCloud,ST|2|CD] ST|2|ti + 1
	@e[type=AreaEffectCloud,ST|2|CD,ST|2|ti=20] @a[$SA$] playsound minecraft:block.note.pling voice @a[c=1]
	@e[type=AreaEffectCloud,ST|2|CD,ST|2|ti=20] tellraw @a[$SA$] $TextStart${"text":"5","color":"yellow","bold":"true"}]}
	
	@e[type=AreaEffectCloud,ST|2|CD,ST|2|ti=40] @a[$SA$] playsound minecraft:block.note.pling voice @a[c=1]
	@e[type=AreaEffectCloud,ST|2|CD,ST|2|ti=40] tellraw @a[$SA$] $TextStart${"text":"4","color":"yellow","bold":"true"}]}
	
	@e[type=AreaEffectCloud,ST|2|CD,ST|2|ti=60] @a[$SA$] playsound minecraft:block.note.pling voice @a[c=1]
	@e[type=AreaEffectCloud,ST|2|CD,ST|2|ti=60] tellraw @a[$SA$] $TextStart${"text":"3","color":"yellow","bold":"true"}]}
	
	@e[type=AreaEffectCloud,ST|2|CD,ST|2|ti=80] @a[$SA$] playsound minecraft:block.note.pling voice @a[c=1]
	@e[type=AreaEffectCloud,ST|2|CD,ST|2|ti=80] tellraw @a[$SA$] $TextStart${"text":"2","color":"yellow","bold":"true"}]}
	
	@e[type=AreaEffectCloud,ST|2|CD,ST|2|ti=100] @a[$SA$] playsound minecraft:block.note.pling voice @a[c=1]
	@e[type=AreaEffectCloud,ST|2|CD,ST|2|ti=100] tellraw @a[$SA$] $TextStart${"text":"1","color":"yellow","bold":"true"}]}
	
	@e[type=AreaEffectCloud,ST|2|CD,ST|2|ti=120] @a[$SA$] playsound minecraft:block.note.pling voice @a[c=1] ~ ~ ~ 2 2
	@e[type=AreaEffectCloud,ST|2|CD,ST|2|ti=120] tellraw @a[$SA$] $TextStart${"text":"GO!","color":"green","bold":"true"}]}
	@e[type=AreaEffectCloud,ST|2|CD,ST|2|ti=120] @e[type=ArmorStand,tag=ST|2|AS] ST|2|ti = 0
	testfor @e[type=AreaEffectCloud,ST|2|CD,ST|2|ti=120]
	CONDITION
		Deactivate(CountdownClock|2|)
END
//>

//<General Scoreboard Clock
FUNC Func_GeneralScoreboardClock
///	General
	$s$clear @a[$SA$,m=2,ST|2|pl>=0] sand
	effect @a[$SpawnArea$,m=2] 23 2 0 true
	$s$kill @e[$SA|2|$,type=Item]
	
///	Both Arenas Check
	@e[type=ArmorStand,STAS] STba = 0
	@e[type=ArmorStand,STAS] @e[type=ArmorStand,STAS] STba + 1

///	Sets sand
	@e[type=ArmorStand,ST|2|AS] STSave = 0
	ScOP @e[type=ArmorStand,ST|2|AS] STSave = Delay|2| STSave
	ScOP @e[type=ArmorStand,ST|2|AS] STSave *= 20 Number
	@e[type=ArmorStand,ST|2|AS,ST|2|ti>=1] ST|2|ti - 1
	@e[type=ArmorStand,ST|2|AS,ST|2|ti=0] @a[$SA|2|$] playsound minecraft:entity.experience_orb.touch voice @a[c=1] ~ ~ ~ 0.5
	@e[type=ArmorStand,ST|2|AS,ST|2|ti=0] fill $SBlockArea|2|$ redstone_block 0
	@e[type=ArmorStand,ST|2|AS,ST|2|ti=0] fill $SBlockArea|2|$ air 0
	ScOP @e[type=ArmorStand,ST|2|AS,ST|2|ti=0] ST|2|ti = @e[type=ArmorStand,ST|2|AS,ST|2|ti=0] STSave

///	Gives players resistance and fire resistance in the preselect region
	effect @a[$WinPreSelect|2|$,m=2,ST|2|pl>=0] 12 15 0 true
	effect @a[$WinPreSelect|2|$,m=2,ST|2|pl>=0] 11 2 10 true
	
///	Scoreboard display
	@e[type=ArmorStand,ST|2|AS,ST|2|as=0] @a[$WinSelect|2|$,ST|2|pl=0,c=1] STdi = -|4|
	@a[ST|2|pl=0,STdi=-|4|] (|2|_1st_place) STdi = -|4|
	@a[ST|2|pl=0,STdi=-|4|] @e[type=ArmorStand,ST|2|AS,ST|2|as=0] ST|2|as = 1
	@a[ST|2|pl=0,STdi=-|4|] tellraw @a[$SA$] $TextStart${"selector":"@a[r=0,c=1,ST|2|pl=0,STdi=-|4|]"},{"text":" got 1st place!","color":"green"}]}
	@a[ST|2|pl=0,STdi=-|4|] ST|2|pl = 1

	@e[type=ArmorStand,ST|2|AS,ST|2|as=1] @a[$WinSelect|2|$,ST|2|pl=0,c=1] STdi = -ADD(|4|;1)
	@a[ST|2|pl=0,STdi=-ADD(|4|;1)] (|2|_2nd_place) STdi = -ADD(|4|;1)
	@a[ST|2|pl=0,STdi=-ADD(|4|;1)] @e[type=ArmorStand,ST|2|AS,ST|2|as=1] ST|2|as = 2
	@a[ST|2|pl=0,STdi=-ADD(|4|;1)] tellraw @a[$SA$] $TextStart${"selector":"@a[r=0,c=1,ST|2|pl=0,STdi=-ADD(|4|;1)]"},{"text":" got 2nd place!","color":"green"}]}
	@a[ST|2|pl=0,STdi=-ADD(|4|;1)] ST|2|pl = 1
	
	@e[type=ArmorStand,ST|2|AS,ST|2|as=2] @a[$WinSelect|2|$,ST|2|pl=0,c=1] STdi = -ADD(|4|;2)
	@a[ST|2|pl=0,STdi=-ADD(|4|;2)] (|2|_3rd_place) STdi = -ADD(|4|;2)
	@a[ST|2|pl=0,STdi=-ADD(|4|;2)] @e[type=ArmorStand,ST|2|AS,ST|2|as=2] ST|2|as = 3
	@a[ST|2|pl=0,STdi=-ADD(|4|;2)] tellraw @a[$SA$] $TextStart${"selector":"@a[r=0,c=1,ST|2|pl=0,STdi=-ADD(|4|;2)]"},{"text":" got 3rd place!","color":"green"}]}
	@a[ST|2|pl=0,STdi=-ADD(|4|;2)] ST|2|pl = 1

///	@e[type=ArmorStand,ST|3|AS,ST|3|as>=1,STba=2] @e[type=ArmorStand,ST|2|AS,ST|2|as>=1,STba=2] say AYY LMAO DOES THIS WORK
	@e[type=ArmorStand,ST|3|AS,ST|3|as>=1,STba=2] @e[type=ArmorStand,ST|2|AS,ST|2|as>=1,STba=2] scoreboard players set'  'STdi -4

///	Teleport after scoreboard display
	$s$tp @a[$WinSelect|2|$,m=2] $SpawnCoords$
	$s$clear @a[$SpawnArea$,m=2] CCItem_Potion(-1;-1)
	ScTJ NoPVP @a[$SpawnArea$,m=2,team=!NoPVP]
	
	@a[$SpawnArea$,m=2,ST|2|pl=1] summon $Firework$
	@a[$SpawnArea$,m=2,ST|2|pl=1] ST|2|pl = 2

///	Suffocation
	@a[$SA|2|$,m=2,STki>=0] sand 0 ~ ~1 ~ @a[r=0,c=1] STki + 1
	@a[$SA|2|$,m=2,STki>=0] air 0 ~ ~2 ~ @a[r=0,c=1] STki = 0
	$s$kill @a[$SA|2|$,m=2,STki>=100]
	
///	Player Counter Display
	@e[type=ArmorStand,ST|2|AS] ST|2|pl = 0
	@a[$SA|2|$,m=2,ST|2|pl=0] @e[type=ArmorStand,ST|2|AS] ST|2|pl + 1
	ScOP |2|Players: STdi = @e[type=ArmorStand,ST|2|AS] ST|2|pl

/// Reset on death
	ScTL ST|2|p @a[ST|2|de>=1,ST|2|pl=0]
	@a[ST|2|de>=1,ST|2|pl=0,EC=0] SPbk = $SPid$
	ScRE @a[ST|2|de>=1] ST|2|pl
	ScRE @a[ST|2|de>=1] STki
	ScRE @a[ST|2|de>=1] ST|2|de

/// Options
///	Jump boost
	ScTE JumpBoost|2| STSave 1 1
	CONDITION
		effect @a[$SA|2|$,m=2,ST|2|pl=0] 8 2 1 true
	
///	Blindness
	ScTE Blindness|2| STSave 1 1
	CONDITION
		effect @a[$SA|2|$,m=2,ST|2|pl=0] 15 2 0 true
	
///	No Sprint
	ScTE NoSprint|2| STSave 1 1
	CONSTART
		effect @a[$SA|2|$,m=2,ST|2|pl=0] 17 2 127 true
		@a[$SA|2|$,m=2,ST|2|pl=0] ST|2|ti + 1
		effect @a[$SA|2|$,m=2,ST|2|pl=0,ST|2|ti>=30] 23 1 0 true
		@a[$SA|2|$,m=2,ST|2|pl=0,ST|2|ti>=30] ST|2|ti = 0
	CONEND
	effect @a[$SA|2|$,m=2,ST|2|pl=0,ST|2|ti>=20] 23 0 0 true
	
	ScTE NoSprint|2| STSave 0 0
	CONSTART
		effect @a[$SA|2|$,m=2,ST|2|pl=0] 23 2 0 true
		ScRE @a[$SA|2|$,m=2,ST|2|pl=0,ST|2|ti>=0] ST|2|ti
		effect @a[$SA|2|$,m=2,ST|2|pl=0] 17 0 0 true
	CONEND
	
///	Potions
	ScTE Potions|2| STSave 2 2
	CONDITION
		$s$clear @a[$SA|2|$,m=2,ST|2|pl=0] CCItem_Potion(-1;-1)
	
	ScTE Potions|2| STSave 2 2
	CONSTART
		Potions|2| STSave = 3
		$s$give @a[$SA|2|$,m=2,ST|2|pl=0] CCItem_Potion(1;0)
	CONEND
	
	//=
	ScTE Potions|2| STSave 0 0
	CONSTART
		effect @a[$SA|2|$,m=2,ST|2|pl=0,m=2] 8 0 0 true
	CONEND
	\\=
	
	ScTE Potions|2| STSave 0 0
	CONSTART
		Potions|2| STSave = 1
		$s$clear @a[$SA|2|$,m=2,ST|2|pl=0] CCItem_Potion(-1;-1)
	CONEND
	
///	Collisions
	ScTE Collisions|2| STSave 2 2
	CONSTART
		Collisions|2| STSave = 3
		scoreboard teams option ST|2|p collisionRule pushOtherTeams
	CONEND
	
	ScTE Collisions|2| STSave 0 0
	CONSTART
		Collisions|2| STSave = 1
		scoreboard teams option ST|2|p collisionRule never
	CONEND
END
//>

//<Reset options
FUNC Func_ResetOptions
	JumpBoost|2| STSave = 0
	Blindness|2| STSave = 0
	NoSprint|2| STSave = 0
	Collisions|2| STSave = 0
	Potions|2| STSave = 0
	Delay|2| STSave = 5
END
//>

//<Potion Testfor Clock
FUNC Func_PotionTFClock
	ScTE Potions|2| STSave 3 3
	SetblockTestfor
	
	@a[$SA|2|$,m=2,ST|2|pl=0] ST|2|po = 1 {ActiveEffects:[{Amplifier:6b,Id:8b}]}
	effect @a[$SA|2|$,m=2,ST|2|pl=0,ST|2|po=1,ST|2|ju>=1] 8 0 0 true
	@a[$SA|2|$,m=2,ST|2|pl=0] ST|2|po = 0
	@a[$SA|2|$,m=2,ST|2|pl=0] ST|2|ju = 0
	$s$clear @a[$SA$,m=2] glass_bottle
END
//>

//<General Scoreboard End
FUNC Func_GeneralScoreboardEnd
	@e[type=ArmorStand,STAS] STba = 0
	@e[type=ArmorStand,STAS] @e[type=ArmorStand,STAS] STba + 1
	@e[type=ArmorStand,STAS,STba=2,c=1] ScRE |2|Players: STdi
	@e[type=ArmorStand,STAS,STba=2,c=1] ScRE (|2|_1st_place) STdi
	@e[type=ArmorStand,STAS,STba=2,c=1] ScRE (|2|_2nd_place) STdi
	@e[type=ArmorStand,STAS,STba=2,c=1] ScRE (|2|_3rd_place) STdi
	@e[type=ArmorStand,STAS,STba=2,c=1] ScRE  STdi
	@e[type=ArmorStand,STAS,STba=2,c=1] ScRE @a[score_STdi=-|4|,score_STdi_min=-ADD(|4|;2)] STdi
	
	testfor @e[type=ArmorStand,STAS,STba=1,c=1]
	CONDITION
		Activate(CBPMainEnd|3|)
	@e[type=ArmorStand,STAS,STba=1,c=1] $ObjR$ STdi
	@e[type=ArmorStand,STAS,STba=1,c=1] $ObjR$ STki
	@e[type=ArmorStand,STAS,STba=1,c=1] $ObjR$ STba

	USE $ObjR$
		ST|2|de
		ST|2|pl
		ST|2|as
		ST|2|ti
		ST|2|po
		ST|2|ju
	UNUSE
	
	USE $TeamR$
		ST|2|p
		ST|2|d_y
		ST|2|d_g
	UNUSE
	$s$kill @e[ST|2|]
	
	$s$kill @e[$SA|2|$,type=FallingSand]
	fill $SandArea|2|$ air 0
	
	$s$clear @a[$SA$,m=2]
	@a[$SA$,m=2,EC=0] SPbk = $SPid$
	effect @a[$SA$,m=2] clear
	$s$tp @a[$SA|2|$,m=2] -250 25 18 -90 0
	effect @a[$SA$] 10 10 100 true
	ScTG * remove STtp
END
//>


//<Groups
Func_CMDSave()

:CBPPortalStartAF PULSE $Style$
Deactivate(CBPPortalStartAF)
Func_PortalStart(Ant Farm;Ant_Farm;AF;CL;5)
Activate(CBPPortalClockAF)

:CBPPortalClockAF CLOCK $Style$
Func_PortalClock(Ant Farm;Ant_Farm;AF;CL;5)

:CBPPortalEndAF PULSE $Style$
Deactivate(CBPPortalEndAF)
Func_PortalEnd(Ant Farm;Ant_Farm;AF;CL;5)
Deactivate(CBPPortalClockAF)


:CBPMainStartAF PULSE $Style$
Func_SpawnStandStart()
Deactivate(CBPMainStartAF)
Func_GeneralScoreboardStart(Ant Farm;Ant_Farm;AF;CL;5)
Activate(CBPCountdownClockAF)
Activate(CBPMainClockAF)
Activate(CBPPortalEndAF)
Activate(CBPPotionTFClockAF)

:CBPCountdownClockAF CLOCK $Style$
Func_CountdownClock(Ant Farm;Ant_Farm;AF;CL;5)

:CBPMainClockAF CLOCK $Style$
Func_SpawnIDSetting()
Func_GeneralScoreboardClock(Ant Farm;Ant_Farm;AF;CL;5)

:CBPPotionTFClockAF CLOCK $Style$
Func_PotionTFClock(Ant Farm;Ant_Farm;AF;CL;5)

:CBPMainEndAF PULSE $Style$
Func_SpawnStandEnd
Deactivate(CBPMainEndAF)
Func_GeneralScoreboardEnd(Ant Farm;Ant_Farm;AF;CL;5)
Deactivate(CBPCountdownClockAF)
Deactivate(CBPMainClockAF)
Deactivate(CBPPortalEndAF)
Deactivate(CBPPotionTFClockAF)

:CBPResetOptionsAF PULSE $Style$
Deactivate(CBPResetOptionsAF)
Func_ResetOptions(Ant Farm;Ant_Farm;AF;CL;5)




:CBPPortalStartCL PULSE $Style$
Deactivate(CBPPortalStartCL)
Func_PortalStart(Classic;Classic;CL;AF;1)
Activate(CBPPortalClockCL)

:CBPPortalClockCL CLOCK $Style$
Func_PortalClock(Classic;Classic;CL;AF;1)

:CBPPortalEndCL PULSE $Style$
Deactivate(CBPPortalEndCL)
Func_PortalEnd(Classic;Classic;CL;AF;1)
Deactivate(CBPPortalClockCL)


:CBPMainStartCL PULSE $Style$
Func_SpawnStandStart()
Deactivate(CBPMainStartCL)
Func_GeneralScoreboardStart(Classic;Classic;CL;AF;1)
Activate(CBPCountdownClockCL)
Activate(CBPMainClockCL)
Activate(CBPPortalEndCL)
Activate(CBPPotionTFClockCL)

:CBPCountdownClockCL CLOCK $Style$
Func_CountdownClock(Classic;Classic;CL;AF;1)

:CBPMainClockCL CLOCK $Style$
Func_SpawnIDSetting()
Func_GeneralScoreboardClock(Classic;Classic;CL;AF;1)

:CBPPotionTFClockCL CLOCK $Style$
Func_PotionTFClock(Classic;Classic;CL;AF;1)

:CBPMainEndCL PULSE $Style$
Func_SpawnStandEnd
Deactivate(CBPMainEndCL)
Func_GeneralScoreboardEnd(Classic;Classic;CL;AF;1)
Deactivate(CBPCountdownClockCL)
Deactivate(CBPMainClockCL)
Deactivate(CBPPortalEndCL)
Deactivate(CBPPotionTFClockCL)

:CBPResetOptionsCL PULSE $Style$
Deactivate(CBPResetOptionsCL)
Func_ResetOptions(Classic;Classic;CL;AF;1)
//>





















