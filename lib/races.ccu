$py(
from lib.consts import Effects, Sounds
from lib.scoreboard import OBJECTIVES, TEAMS


# general constants
select_all = race.event.select_all

# the initials are the same as the display objective name
initials = obj_name = race.initials

# shorthand team name
team_name = race.initials + "r"
)

# Includes the specific race type

# isinstance of the checkpoint race has to be checked first 
# because a checkpoint race is an instance of a lap race
$if(isinstance(race, CheckpointRace))
$include("lib/race_checkpoint.ccu")

$elif(isinstance(race, RegularRace))
$include("lib/race_regular.ccu")

$elif(isinstance(race, LapRace))
$include("lib/race_lap.ccu")
$endif

$py(
OBJECTIVES.new_str("""
    # This looks like a stupid whale face, but this actually is valid
    # the first dot is replaced with initials, and the second is
    # replaced with the display name
    . _ .

    # Used by the temporary area effect cloud to act as a countdown timer
    cd _ Countdown

    # Holds all of the players to initialize
    pl _ Player List

    """, initials, race.obj_disp)
    
OBJECTIVES[initials].setdisplay("sidebar")
OBJECTIVES[initials].add_const("/spawn", 99)

TEAMS.new_str("""
    # The main team that all players will join
    r .
        color {0}
        seeFriendlyInvisibles true
        collisionRule never
        friendlyfire false
""".format(race.color), initials, race.obj_disp)
)

!mfunc init
    # initialize the floo network
    $(race.cmd_init())

    # from the include files
    $race_main_start()

    # from the main.ccu file
    $edit_scoreboard_start()

    # initializes objectives and teams
    $(OBJECTIVES.cmd_init())
    $(TEAMS.cmd_init())

    # countdown start
    summon area_effect_cloud ~ ~ ~ {Duration:130,Tags:["$(initials)_cd"]}

    # adds the "/spawn" fakename to the proper color
    join $(team_name) /spawn

    # from the main.ccu file
    $edit_main_start()

    # initialize the floo network
    $(race.cmd_post_init())

!mfunc main
    # the floo network clock for setting spawnpoints
    $(race.cmd_main())
    
    @e[type=area_effect_cloud,$(initials)_cd] function countdown
    kill @e[$(select_all),type=item]
    
    # adds everyone to the clear objective
    @a[gSA=1,m=2] $(initials)pl + 0

    # resets all players that have either just joined the game or just died
    @a[gSA=1,m=2,$(initials)pl=0] function reset_player
    @a[gSA=1,m=2,gDE=1] function reset_player

    # joins the race team
    join $(team_name) @a[gSA=1,team=!$(team_name)]

    # gives invis to all
    effect @a[gSA=1,m=2,team=$(team_name)] $(Effects.invis) 3 0 true
    
    $race_main_clock()
    $edit_main_clock()
    
    $race_end_main_clock()


# used on a given player to initialize and clear their inventory
!mfunc reset_player
    # resets the player's everything upon death
    # clears effects and inventory
    effect @s clear
    clear @s

    # gives all ECs books
    $(race.event.cmd_book())
    @s[$(initials)pl=0] $(initials)pl = 1


# used on a given player to reset them completely,
# meant to be used at the end of a race
!mfunc full_reset_player
    function reset_player
    $(race.cmd_spawn())


!mfunc term
    # terminates the floo network
    $(race.cmd_term())

    # general reset
    @a[$(initials)pl=1,m=2] function full_reset_player
    
    # terminates all scoreboards
    $(OBJECTIVES.cmd_term())
    $(TEAMS.cmd_term())
    
    # removes the area effect cloud
    kill @e[type=area_effect_cloud,$(initials)_cd]
    
    $edit_main_end()
    $race_main_end()


# used on the area effect cloud countdown entity to display
# the countdown using tellraw and playsounds
!mfunc countdown
    @s $(initials)cd + 1
    
    @s[$(initials)cd=20] tellraw @a[gSA=1] $(race.event.begin(r'{"text":"5","color":"yellow","bold":"true"}'))
    @s[$(initials)cd=20] @a[gSA=1] playsound $(Sounds.pling) voice @a[c=1] ~ ~ ~ 0.5
    
    @s[$(initials)cd=40] tellraw @a[gSA=1] $(race.event.begin(r'{"text":"4","color":"yellow","bold":"true"}'))
    @s[$(initials)cd=40] @a[gSA=1] playsound $(Sounds.pling) voice @a[c=1] ~ ~ ~ 1
    
    @s[$(initials)cd=60] tellraw @a[gSA=1] $(race.event.begin(r'{"text":"3","color":"yellow","bold":"true"}'))
    @s[$(initials)cd=60] @a[gSA=1] playsound $(Sounds.pling) voice @a[c=1] ~ ~ ~ 1.5
    
    @s[$(initials)cd=80] tellraw @a[gSA=1] $(race.event.begin(r'{"text":"2","color":"yellow","bold":"true"}'))
    @s[$(initials)cd=80] @a[gSA=1] playsound $(Sounds.pling) voice @a[c=1] ~ ~ ~ 2
    
    @s[$(initials)cd=100] tellraw @a[gSA=1] $(race.event.begin(r'{"text":"1","color":"yellow","bold":"true"}'))
    @s[$(initials)cd=100] @a[gSA=1] playsound $(Sounds.pling) voice @a[c=1] ~ ~ ~ 2
    
    @s[$(initials)cd=120] @a[gSA=1] playsound $(Sounds.wither) voice @a[c=1]
    @s[$(initials)cd=120] tellraw @a[gSA=1] $(race.event.begin(r'{"text":"GO!","color":"green","bold":"true"}'))
    @s[$(initials)cd=120] title @a[gSA=1] title {"text":"GO!","color":"green"}
    
    $edit_countdown_clock()

$add_functions()
$race_add_functions()

